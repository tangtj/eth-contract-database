
// File: contracts/Staking.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

    import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
    import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
    import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
    import {IStakingContract} from "./IStakingContract.sol";
	
contract StakingContract is ReentrancyGuard {
    using SafeERC20 for IERC20;
	IERC20 public token;
    IERC20 public stakingToken;
    uint256 public currentPrice;
    uint256 private _batchSize;
    address public owner;
    mapping(address => bool) public isStaker;
    mapping(address => uint256) private stakerIndex;
    mapping(address => StakerInfo) public stakers;
    mapping(address => Stake) public stakes;
    address[] public activeStakers;
	mapping (address => Stakeholder) public stakeholders;
    uint256 public constant annualInterestRate = 100;
    uint256 public constant basisPoint = 10000;
    uint256 public constant secondsInYear = 365 * 24 * 60 * 60;
/**
 * @title Stake
 * @dev Structure to store stake information.
 */
struct Stake {
    // The amount staked.
    uint256 amount;
    
    // Timestamp when staking started.
    uint256 stakingTimestamp;
    
    // The amount in USDT staked.
    uint256 amountInUSDT;
    
    // Staking period in seconds.
    uint256 stakingPeriod;
}

/**
 * @title StakerInfo
 * @dev Structure to store information about stakers.
 */
struct StakerInfo {
    // Index of the staker.
    uint256 index;
    
    // Flag indicating staker's activity.
    bool isActive;
}

/**
 * @title Stakeholder
 * @dev Structure to store information about a stakeholder.
 */
struct Stakeholder {
    // Balance of the stakeholder.
    uint balance;
    
    // Accrued interest on the stake.
    uint accruedInterest;
    
    // Flag indicating the existence of the stakeholder.
    bool exists;
}

/*
 * @title Constructor
 * @dev Initializes the contract with the specified token address.
 * @param _tokenAddress The address of the ERC20 token contract.
 */
constructor(address _tokenAddress) {
    // Set the contract owner to the sender of the transaction.
    owner = msg.sender;
    
    // Initialize the token contract with the provided address.
    token = IERC20(_tokenAddress);
}

/*
 * @title onlyOwner Modifier
 * @dev Modifier to restrict access to functions only to the contract owner.
 * @dev Reverts the transaction if the sender is not the contract owner.
 */
modifier onlyOwner() {
    require(msg.sender == owner, "Not the contract owner");
    _;
}

/*
 * @title depositTokens
 * @dev Deposits tokens into the contract.
 * @param amount The amount of tokens to deposit.
 * @dev Only accessible by the contract owner.
 * @dev Reverts the transaction if the token transfer fails.
 */
function depositTokens(uint256 amount) external onlyOwner {
    require(token.transferFrom(msg.sender, address(this), amount), "Transfer failed");
}

/*
 * @title withdrawTokens
 * @dev Withdraws tokens from the contract.
 * @param amount The amount of tokens to withdraw.
 * @dev Only accessible by the contract owner.
 * @dev Reverts the transaction if the token transfer fails.
 */
function withdrawTokens(uint256 amount) external onlyOwner {
    require(token.transfer(msg.sender, amount), "Transfer failed");
}

/*
 * @title setTokenAddress
 * @dev Sets a new token address for the contract.
 * @param _newTokenAddress The address of the new ERC20 token contract.
 * @dev Only accessible by the contract owner.
 * @dev Reverts the transaction if the new token address is the zero address.
 */
function setTokenAddress(address _newTokenAddress) external onlyOwner {
    require(_newTokenAddress != address(0), "New token address cannot be the zero address");
    token = IERC20(_newTokenAddress);
}

/*
 * @title transferOwnership
 * @dev Transfers ownership of the contract to a new address.
 * @param newOwner The address of the new owner.
 * @dev Only accessible by the contract owner.
 * @dev Reverts the transaction if the new owner address is the zero address.
 */
function transferOwnership(address newOwner) public onlyOwner {
    require(newOwner != address(0), "New owner cannot be the zero address");
    owner = newOwner;
}

/*
 * @title setStakingToken
 * @dev Sets the staking token for the contract.
 * @param _stakingToken The address of the staking token contract.
 * @dev Reverts the transaction if the staking token is already set.
 * @dev This function can be called by anyone.
 */
function setStakingToken(IERC20 _stakingToken) external {
    require(address(stakingToken) == address(0), "Staking token is already set");
    stakingToken = _stakingToken;
}

/*
 * @title stake
 * @dev Allows a user to stake tokens for a specified period.
 * @param _amount The amount of tokens to stake.
 * @param _stakingPeriod The duration of the stake in seconds.
 * @dev Reverts the transaction if the amount is not greater than 0 or if the staking token allowance is insufficient.
 * @dev Emits a Staked event after successfully staking tokens.
 */
function stake(uint256 _amount, uint256 _stakingPeriod) external nonReentrant {
    require(_amount > 0, "Amount must be greater than 0");
    require(stakingToken.allowance(msg.sender, address(this)) >= _amount, "Insufficient staking");
    
    // Calculate the amount in USDT based on the current price
    uint256 amountInUSDT = (_amount * currentPrice) / 1e18;

    // Get staker information
    StakerInfo storage staker = stakers[msg.sender];

    if (staker.isActive) {
        // Update existing stake
        Stake storage userStake = stakes[msg.sender];
        userStake.amount += _amount;
        userStake.amountInUSDT += amountInUSDT;
        // Update stakingTimestamp and stakingPeriod
        userStake.stakingTimestamp = block.timestamp;
        userStake.stakingPeriod = _stakingPeriod;
    } else {
        // Create a new stake
        staker.isActive = true;
        staker.index = activeStakers.length;
        activeStakers.push(msg.sender);
        stakerIndex[msg.sender] = activeStakers.length - 1;
        stakes[msg.sender] = Stake({
            amount: _amount,
            stakingTimestamp: block.timestamp,
            amountInUSDT: amountInUSDT,
            stakingPeriod: _stakingPeriod
        });
    }

    // Transfer tokens from the user to the contract
    stakingToken.safeTransferFrom(msg.sender, address(this), _amount);
    
    // Emit Staked event
    emit Staked(msg.sender, _amount, amountInUSDT, block.timestamp, _stakingPeriod);
}

/*
 * @title unstake
 * @dev Allows a staker to unstake their tokens.
 * @dev Reverts the transaction if the sender is not a staker.
 * @dev Calls the internal _claimAndUnstake function to process the unstaking.
 */
function unstake() external nonReentrant {
    require(isStaker[msg.sender], "Not a staker");
    _claimAndUnstake(msg.sender);
}


/*
 * @title _claimAndUnstake
 * @dev Internal function to claim rewards and unstake tokens for a specified user.
 * @param _user The address of the user.
 * @return rewardInTokens The amount of rewards claimed in tokens.
 * @dev Sets the user as a non-staker, removes their staking information, and emits an Unstaked event.
 */
function _claimAndUnstake(address _user) private returns (uint256 rewardInTokens) {
    // Calculate and transfer rewards to the user
    rewardInTokens = _calculateAndTransferReward(_user);
    
    // Mark the user as a non-staker
    isStaker[_user] = false;
    
    // Remove the user from the active stakers list
    _removeStaker(_user);
    
    // Emit Unstaked event
    emit Unstaked(_user, stakes[_user].amount, rewardInTokens, block.timestamp);
}

/*
 * @title _removeStaker
 * @dev Internal function to remove a staker from the active stakers list.
 * @param _staker The address of the staker to be removed.
 * @dev Updates the active stakers list and staker index mapping.
 */
function _removeStaker(address _staker) internal {
    // Get the index of the staker in the active stakers list
    uint256 index = stakerIndex[_staker];
    
    // If the staker is not the last one in the list, move the last staker to its position
    if (index < activeStakers.length - 1) {
        address lastStaker = activeStakers[activeStakers.length - 1];
        activeStakers[index] = lastStaker;
        stakerIndex[lastStaker] = index;
    }
    
    // Remove the last staker from the list
    activeStakers.pop();
    
    // Delete the staker index mapping
    delete stakerIndex[_staker];
}

/*
 * @title _calculateAndTransferReward
 * @dev Internal function to calculate and transfer rewards to a specified user.
 * @param _user The address of the user.
 * @return totalAmountInTokens The total amount of tokens to be transferred as rewards.
 * @dev Reverts the transaction if no active stake is found for the user, if there are insufficient funds in the contract, or if there are insufficient tokens in the user's balance.
 * @dev Transfers the total amount of tokens to the user and deletes their stake information.
 */
function _calculateAndTransferReward(address _user) private returns (uint256) {
    // Get the user's stake information
    Stake storage userStake = stakes[_user];
    
    // Ensure that the user has an active stake
    require(userStake.amount > 0, "No active stake found");

    // Calculate the total amount of tokens to be transferred as rewards
    uint256 totalAmountInTokens = userStake.amount;
    
    // Ensure that the contract has sufficient funds to cover the reward
    require(stakingToken.balanceOf(address(this)) >= totalAmountInTokens, "Insufficient funds in contract");
    
    // Ensure that the user has sufficient tokens in their balance to receive the reward
    require(stakingToken.balanceOf(_user) >= totalAmountInTokens, "Insufficient tokens in balance");
	
    // Transfer the total amount of tokens to the user
    stakingToken.safeTransfer(_user, totalAmountInTokens);
    
    // Delete the user's stake information
    delete stakes[_user];

    return totalAmountInTokens;
}

/*
 * @title calculateTotalInterestAndUpdate
 * @dev Calculates the total interest accrued for active stakers and updates their stake information.
 * @param _currentPrice The current price of the token in USDT.
 * @return totalInterest The total interest accrued in tokens.
 * @dev Only accessible by the main token contract.
 * @dev Emits an InterestPaid event for each staker who receives interest.
 */
function calculateTotalInterestAndUpdate(uint256 _currentPrice) external returns (uint256 totalInterest) {
    // Ensure that the caller is the main token contract
    require(msg.sender == address(stakingToken), "Only the main token contract");
    
    // Update the current price of the token
    currentPrice = _currentPrice;
    
    // Initialize total interest to 0
    totalInterest = 0;
    
    // Initialize the start index for batch processing
    uint256 startIndex = 0;

    // Iterate over active stakers in batches
    while (startIndex < activeStakers.length) {
        // Calculate the end index for the current batch
        uint256 endIndex = startIndex + _batchSize;
        if (endIndex > activeStakers.length) {
            endIndex = activeStakers.length;
        }

        // Iterate over stakers in the current batch
        for (uint256 i = startIndex; i < endIndex; i++) {
            address staker = activeStakers[i];
            Stake storage userStake = stakes[staker];

            // Check if the staker has an active stake
            if (userStake.amount > 0) {
                // Calculate the time duration of the stake
                uint256 timeStaked = block.timestamp - userStake.stakingTimestamp;
                
                // Calculate the interest accrued in USDT
                uint256 rewardInUSDT = (userStake.amountInUSDT * annualInterestRate / basisPoint) * timeStaked / secondsInYear;
                
                // Convert the interest from USDT to tokens based on the current price
                uint256 rewardInTokens = (rewardInUSDT * 1e18) / _currentPrice;

                // Update the stake information for the staker
                userStake.stakingTimestamp = block.timestamp;
                userStake.amountInUSDT += rewardInUSDT;
                
                // Increment the total interest accrued
                totalInterest += rewardInTokens;
                
                // Emit an InterestPaid event for the staker
                emit InterestPaid(staker, rewardInTokens);
            }
        }

        // Update the start index for the next batch
        startIndex = endIndex;
    }

    return totalInterest;
}

/*
 * @title calculateReward
 * @dev Calculates the reward accrued for a specified user based on their stake information.
 * @param _user The address of the user.
 * @return reward The reward accrued in tokens.
 * @dev Returns 0 if the user has no active stake or if the staking timestamp is 0.
 */
function calculateReward(address _user) public view returns (uint256) {
    // Retrieve the stake information for the user
    Stake memory userStake = stakes[_user];
    
    // Check if the user has an active stake
    if (userStake.amount == 0 || userStake.stakingTimestamp == 0) {
        return 0;
    }
    
    // Calculate the time duration of the stake
    uint256 timeStaked = block.timestamp - userStake.stakingTimestamp;
    
    // Calculate the reward accrued based on the stake information
    uint256 reward = (userStake.amountInUSDT * annualInterestRate * timeStaked) / (basisPoint * secondsInYear);
    
    return reward;
}

/*
 * @title InterestPaid
 * @dev Event emitted when interest is paid to a staker.
 * @param user The address of the staker.
 * @param interestAmount The amount of interest paid in tokens.
 */
event InterestPaid(address indexed user, uint256 interestAmount);

/*
 * @title Staked
 * @dev Event emitted when tokens are staked by a user.
 * @param user The address of the user.
 * @param amount The amount of tokens staked.
 * @param amountInUSDT The equivalent amount in USDT.
 * @param timestamp The timestamp when staking occurred.
 * @param stakingPeriod The duration of the stake in seconds.
 */
event Staked(address indexed user, uint256 amount, uint256 amountInUSDT, uint256 timestamp, uint256 stakingPeriod);

/*
 * @title Unstaked
 * @dev Event emitted when tokens are unstaked by a user.
 * @param user The address of the user.
 * @param amount The amount of tokens unstaked.
 * @param reward The reward received by the user.
 * @param timestamp The timestamp when unstaking occurred.
 */
event Unstaked(address indexed user, uint256 amount, uint256 reward, uint256 timestamp);
}

// File: contracts/IStakingContract.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.24;


interface IStakingContract {
    function calculateTotalInterestAndUpdate(uint256 currentPrice, uint256 transactionVolume) external returns (uint256);
}



// File: @openzeppelin/contracts/security/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Returns true if the reentrancy guard is currently set to "entered", which indicates there is a
     * `nonReentrant` function in the call stack.
     */
    function _reentrancyGuardEntered() internal view returns (bool) {
        return _status == _ENTERED;
    }
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.20;

import {IERC20} from "../IERC20.sol";
import {IERC20Permit} from "../extensions/IERC20Permit.sol";
import {Address} from "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    /**
     * @dev An operation with an ERC20 token failed.
     */
    error SafeERC20FailedOperation(address token);

    /**
     * @dev Indicates a failed `decreaseAllowance` request.
     */
    error SafeERC20FailedDecreaseAllowance(address spender, uint256 currentAllowance, uint256 requestedDecrease);

    /**
     * @dev Transfer `value` amount of `token` from the calling contract to `to`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeTransfer(IERC20 token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeCall(token.transfer, (to, value)));
    }

    /**
     * @dev Transfer `value` amount of `token` from `from` to `to`, spending the approval given by `from` to the
     * calling contract. If `token` returns no value, non-reverting calls are assumed to be successful.
     */
    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeCall(token.transferFrom, (from, to, value)));
    }

    /**
     * @dev Increase the calling contract's allowance toward `spender` by `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 oldAllowance = token.allowance(address(this), spender);
        forceApprove(token, spender, oldAllowance + value);
    }

    /**
     * @dev Decrease the calling contract's allowance toward `spender` by `requestedDecrease`. If `token` returns no
     * value, non-reverting calls are assumed to be successful.
     */
    function safeDecreaseAllowance(IERC20 token, address spender, uint256 requestedDecrease) internal {
        unchecked {
            uint256 currentAllowance = token.allowance(address(this), spender);
            if (currentAllowance < requestedDecrease) {
                revert SafeERC20FailedDecreaseAllowance(spender, currentAllowance, requestedDecrease);
            }
            forceApprove(token, spender, currentAllowance - requestedDecrease);
        }
    }

    /**
     * @dev Set the calling contract's allowance toward `spender` to `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful. Meant to be used with tokens that require the approval
     * to be set to zero before setting it to a non-zero value, such as USDT.
     */
    function forceApprove(IERC20 token, address spender, uint256 value) internal {
        bytes memory approvalCall = abi.encodeCall(token.approve, (spender, value));

        if (!_callOptionalReturnBool(token, approvalCall)) {
            _callOptionalReturn(token, abi.encodeCall(token.approve, (spender, 0)));
            _callOptionalReturn(token, approvalCall);
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data);
        if (returndata.length != 0 && !abi.decode(returndata, (bool))) {
            revert SafeERC20FailedOperation(address(token));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     *
     * This is a variant of {_callOptionalReturn} that silents catches all reverts and returns a bool instead.
     */
    function _callOptionalReturnBool(IERC20 token, bytes memory data) private returns (bool) {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We cannot use {Address-functionCall} here since this should return false
        // and not revert is the subcall reverts.

        (bool success, bytes memory returndata) = address(token).call(data);
        return success && (returndata.length == 0 || abi.decode(returndata, (bool))) && address(token).code.length > 0;
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the value of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the value of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves a `value` amount of tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 value) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets a `value` amount of tokens as the allowance of `spender` over the
     * caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 value) external returns (bool);

    /**
     * @dev Moves a `value` amount of tokens from `from` to `to` using the
     * allowance mechanism. `value` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 value) external returns (bool);
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/Address.sol)

pragma solidity ^0.8.20;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev The ETH balance of the account is not enough to perform the operation.
     */
    error AddressInsufficientBalance(address account);

    /**
     * @dev There's no code at `target` (it is not a contract).
     */
    error AddressEmptyCode(address target);

    /**
     * @dev A call to an address target failed. The target may have reverted.
     */
    error FailedInnerCall();

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.8.20/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        if (address(this).balance < amount) {
            revert AddressInsufficientBalance(address(this));
        }

        (bool success, ) = recipient.call{value: amount}("");
        if (!success) {
            revert FailedInnerCall();
        }
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason or custom error, it is bubbled
     * up by this function (like regular Solidity function calls). However, if
     * the call reverted with no returned reason, this function reverts with a
     * {FailedInnerCall} error.
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        if (address(this).balance < value) {
            revert AddressInsufficientBalance(address(this));
        }
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and reverts if the target
     * was not a contract or bubbling up the revert reason (falling back to {FailedInnerCall}) in case of an
     * unsuccessful call.
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata
    ) internal view returns (bytes memory) {
        if (!success) {
            _revert(returndata);
        } else {
            // only check if target is a contract if the call was successful and the return data is empty
            // otherwise we already know that it was a contract
            if (returndata.length == 0 && target.code.length == 0) {
                revert AddressEmptyCode(target);
            }
            return returndata;
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and reverts if it wasn't, either by bubbling the
     * revert reason or with a default {FailedInnerCall} error.
     */
    function verifyCallResult(bool success, bytes memory returndata) internal pure returns (bytes memory) {
        if (!success) {
            _revert(returndata);
        } else {
            return returndata;
        }
    }

    /**
     * @dev Reverts with returndata if present. Otherwise reverts with {FailedInnerCall}.
     */
    function _revert(bytes memory returndata) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert FailedInnerCall();
        }
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Permit.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/extensions/IERC20Permit.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 *
 * ==== Security Considerations
 *
 * There are two important considerations concerning the use of `permit`. The first is that a valid permit signature
 * expresses an allowance, and it should not be assumed to convey additional meaning. In particular, it should not be
 * considered as an intention to spend the allowance in any specific way. The second is that because permits have
 * built-in replay protection and can be submitted by anyone, they can be frontrun. A protocol that uses permits should
 * take this into consideration and allow a `permit` call to fail. Combining these two aspects, a pattern that may be
 * generally recommended is:
 *
 * ```solidity
 * function doThingWithPermit(..., uint256 value, uint256 deadline, uint8 v, bytes32 r, bytes32 s) public {
 *     try token.permit(msg.sender, address(this), value, deadline, v, r, s) {} catch {}
 *     doThing(..., value);
 * }
 *
 * function doThing(..., uint256 value) public {
 *     token.safeTransferFrom(msg.sender, address(this), value);
 *     ...
 * }
 * ```
 *
 * Observe that: 1) `msg.sender` is used as the owner, leaving no ambiguity as to the signer intent, and 2) the use of
 * `try/catch` allows the permit to fail and makes the code tolerant to frontrunning. (See also
 * {SafeERC20-safeTransferFrom}).
 *
 * Additionally, note that smart contract wallets (such as Argent or Safe) are not able to produce permit signatures, so
 * contracts should have entry points that don't rely on permit.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     *
     * CAUTION: See Security Considerations above.
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}

