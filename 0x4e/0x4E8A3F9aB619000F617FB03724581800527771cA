
// File: /contracts/TweetRewardSystem.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.19;
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

contract TweetRewardSystem is Ownable {
    uint256 public withdrawCoolDown = 1 hours;
    address public secret;
    bool public useOnlyWhitelistedTokens = false;

    address[] public whiteListedTokens;
    mapping(address => bool) public isTokenWhitelisted;
    mapping(bytes => bool) private usedSignatures;
    mapping(address => uint256) public lastWithdraw;

    event Withdraw(address token, address recipient, uint256 amount);
    event Deposit(address token, uint256 amount, address sender);
    event WithdrawBatch(address[] tokens, address to, uint256[] amounts);
    event TokenAdded(address token);
    event TokenRemoved(address token);

    constructor(address _signer) {
        secret = _signer;
    }

    function setSecret(address _secret) external onlyOwner {
        secret = _secret;
    }

    function toggleUseOnlyWhitelistedTokens() external onlyOwner {
        useOnlyWhitelistedTokens = !useOnlyWhitelistedTokens;
    }

    function isValidToken(address _token) public view returns (bool) {
        if (_token == address(0)) {
            return true;
        }
        return isTokenWhitelisted[_token] || !useOnlyWhitelistedTokens;
    }

    function batchAddWhitelistedTokens(
        address[] calldata _tokens
    ) external onlyOwner {
        for (uint256 i = 0; i < _tokens.length; i++) {
            address token = _tokens[i];
            require(!isValidToken(token), "addTokens: Token already exists");
            isTokenWhitelisted[token] = true;
            whiteListedTokens.push(token);
            emit TokenAdded(token);
        }
    }

    function removeToken(address _token, uint256 index) external onlyOwner {
        require(isValidToken(_token), "removeToken: Token not found");
        require(index < whiteListedTokens.length, "removeToken: Invalid index");
        isTokenWhitelisted[_token] = false;
        _removeTokenAtIndex(index);
        emit TokenRemoved(_token);
    }

    function _removeTokenAtIndex(uint256 index) internal {
        if (index < whiteListedTokens.length - 1) {
            whiteListedTokens[index] = whiteListedTokens[
                whiteListedTokens.length - 1
            ];
        }
        whiteListedTokens.pop();
    }

    function getWhiteListedTokens() external view returns (address[] memory) {
        return whiteListedTokens;
    }

    function depositERC20(address _token, uint256 _amount) external {
        require(isValidToken(_token), "Invalid token");
        require(_amount > 0, "depositERC20: Amount must be greater than zero");
        require(
            IERC20(_token).allowance(msg.sender, address(this)) >= _amount,
            "depositERC20: Allowance not sufficient"
        );
        require(
            IERC20(_token).balanceOf(msg.sender) >= _amount,
            "depositERC20: You don't have enough balance"
        );

        IERC20(_token).transferFrom(msg.sender, address(this), _amount);

        emit Deposit(_token, _amount, msg.sender);
    }

    function depositETH() external payable {
        require(msg.value > 0, "depositETH: Invalid Ether deposit amount");
        emit Deposit(address(0), msg.value, msg.sender);
    }

    function withdraw(
        address _token,
        uint256 _amount,
        uint256 _timeout,
        bytes calldata _signature
    ) external {
        require(_timeout > block.timestamp, "withdraw: Signature is expired");
        require(_amount > 0, "withdraw: Amount must be greater than zero");
        require(
            !usedSignatures[_signature],
            "withdraw: Signature already used"
        );
        require(
            lastWithdraw[msg.sender] + withdrawCoolDown < block.timestamp,
            "withdraw: Withdrawal is too soon"
        );
        require(
            _verifyHashSignature(
                keccak256(abi.encode(msg.sender, _token, _amount, _timeout)),
                _signature
            ),
            "withdraw: Signature is invalid"
        );

        usedSignatures[_signature] = true;
        lastWithdraw[msg.sender] = block.timestamp;

        if (_token == address(0)) {
            payable(msg.sender).transfer(_amount);
        } else {
            IERC20(_token).transfer(msg.sender, _amount);
        }
        emit Withdraw(_token, msg.sender, _amount);
    }

    function withdrawBatch(
        address[] memory _tokens,
        uint256[] memory _amounts,
        uint256 _timeout,
        bytes calldata _signature
    ) external {
        require(
            _timeout > block.timestamp,
            "withdrawBatch: Signature is expired"
        );
        require(
            !usedSignatures[_signature],
            "withdrawBatch: Signature already used"
        );
        require(
            lastWithdraw[msg.sender] + withdrawCoolDown < block.timestamp,
            "withdrawBatch: Withdrawal is too soon"
        );
        require(
            _tokens.length == _amounts.length,
            "withdrawBatch: Invalid _tokens or _amounts length"
        );
        require(
            _verifyHashSignature(
                keccak256(abi.encode(msg.sender, _tokens, _amounts, _timeout)),
                _signature
            ),
            "withdrawBatch: Signature is invalid"
        );

        usedSignatures[_signature] = true;
        lastWithdraw[msg.sender] = block.timestamp;

        withdrawAllTokens(_tokens, _amounts);

        emit WithdrawBatch(_tokens, msg.sender, _amounts);
    }

    function withdrawAllTokens(
        address[] memory _tokens,
        uint256[] memory _amounts
    ) internal {
        for (uint256 i = 0; i < _tokens.length; i++) {
            address token = _tokens[i];
            uint256 tokenBalance = _amounts[i];
            if (tokenBalance > 0) {
                if (token == address(0)) {
                    payable(msg.sender).transfer(tokenBalance);
                } else {
                    IERC20(token).transfer(msg.sender, tokenBalance);
                }
            }
        }
    }

    function _verifyHashSignature(
        bytes32 freshHash,
        bytes memory signature
    ) internal view returns (bool) {
        bytes32 hash = keccak256(
            abi.encodePacked("\x19Ethereum Signed Message:\n32", freshHash)
        );

        bytes32 r;
        bytes32 s;
        uint8 v;

        if (signature.length != 65) {
            return false;
        }
        assembly {
            r := mload(add(signature, 32))
            s := mload(add(signature, 64))
            v := byte(0, mload(add(signature, 96)))
        }

        if (v < 27) {
            v += 27;
        }

        address signer = address(0);
        if (v == 27 || v == 28) {
            // solium-disable-next-line arg-overflow
            signer = ecrecover(hash, v, r, s);
        }
        return secret == signer;
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}


// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

