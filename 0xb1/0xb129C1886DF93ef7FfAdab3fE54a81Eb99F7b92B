
// File: @chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface AggregatorV3Interface {
  function decimals() external view returns (uint8);

  function description() external view returns (string memory);

  function version() external view returns (uint256);

  function getRoundData(
    uint80 _roundId
  ) external view returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound);

  function latestRoundData()
    external
    view
    returns (uint80 roundId, int256 answer, uint256 startedAt, uint256 updatedAt, uint80 answeredInRound);
}


// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/ERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The default value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on
     * `transferFrom`. This is semantically equivalent to an infinite approval.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * NOTE: Does not update the allowance if the current allowance
     * is the maximum `uint256`.
     *
     * Requirements:
     *
     * - `from` and `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     * - the caller must have allowance for ``from``'s tokens of at least
     * `amount`.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) public virtual override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, _allowances[owner][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        address owner = _msgSender();
        uint256 currentAllowance = _allowances[owner][spender];
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `sender` to `recipient`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
        }
        _balances[to] += amount;

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
        }
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Spend `amount` form the allowance of `owner` toward `spender`.
     *
     * Does not update the allowance amount in case of infinite allowance.
     * Revert if not enough allowance is available.
     *
     * Might emit an {Approval} event.
     */
    function _spendAllowance(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            require(currentAllowance >= amount, "ERC20: insufficient allowance");
            unchecked {
                _approve(owner, spender, currentAllowance - amount);
            }
        }
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @openzeppelin/contracts/utils/structs/EnumerableSet.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/structs/EnumerableSet.sol)

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastvalue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastvalue;
                // Update the index for the moved value
                set._indexes[lastvalue] = valueIndex; // Replace lastvalue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        return _values(set._inner);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        assembly {
            result := store
        }

        return result;
    }
}


// File: contracts/B2E.sol
// SPDX-License-Identifier: Unlicensed
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";
import "@chainlink/contracts/src/v0.8/interfaces/AggregatorV3Interface.sol";

/**
 * Standard SafeMath, stripped down to just add/sub/mul/div
 */
library SafeMath {
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");

        return c;
    }
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return sub(a, b, "SafeMath: subtraction overflow");
    }
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        uint256 c = a - b;

        return c;
    }
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }

        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");

        return c;
    }
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return div(a, b, "SafeMath: division by zero");
    }
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        // Solidity only automatically asserts when dividing by 0
        require(b > 0, errorMessage);
        uint256 c = a / b;
        // assert(a == b * c + a % b); // There is no case in which this doesn't hold

        return c;
    }
}

interface IDEXFactory {
    function createPair(address tokenA, address tokenB) external returns (address pair);
}

interface IDEXRouter {
    function factory() external pure returns (address);
    function WETH() external pure returns (address);

    function addLiquidity(
        address tokenA,
        address tokenB,
        uint amountADesired,
        uint amountBDesired,
        uint amountAMin,
        uint amountBMin,
        address to,
        uint deadline
    ) external returns (uint amountA, uint amountB, uint liquidity);

    function addLiquidityETH(
        address token,
        uint amountTokenDesired,
        uint amountTokenMin,
        uint amountETHMin,
        address to,
        uint deadline
    ) external payable returns (uint amountToken, uint amountETH, uint liquidity);

    function swapExactTokensForTokensSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;

    function swapExactETHForTokensSupportingFeeOnTransferTokens(
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external payable;

    function swapExactTokensForETHSupportingFeeOnTransferTokens(
        uint amountIn,
        uint amountOutMin,
        address[] calldata path,
        address to,
        uint deadline
    ) external;
}

interface IDividendDistributor {
    function burnToken(uint256 _dayId, address _burner, uint256 _amount) external;
    function deposit(uint256 _dayId) external payable;
    function claimDividend(uint256 _dayId, address _burner) external;
    function getDividendsClaimedOf(address _burner) external view returns(uint256);
    function getBurnAmount(uint256 _dayId, address _burner) external view returns(uint256);
    function dailyGiveawayWinner(uint256 dayId, uint256 price, address account) external view returns(address);
    function getClaimableReward(uint256 _dayId, address _burner) external view returns(uint256);
    function getTotalBurn(uint256 _dayId) external view returns(uint256);
    function getTotalDividend(uint256 _dayId) external view returns(uint256);

    event Deposited(uint256 dayId, uint256 amount);
    event BurnedToken(uint256 dayId, address burner, uint256 amount);
    event DistributedDividend(uint256 dayId, address shareholder, uint256 amount);
    event ClaimedDividend(uint256 dayId, address burner, uint256 rewards);
}

contract DividendDistributor is IDividendDistributor {
    using SafeMath for uint256;
    using EnumerableSet for EnumerableSet.AddressSet;

    address public _token;
    address public _owner;

    struct BurnerInfo {
        mapping(uint256 => uint256) amount;
        mapping(uint256 => uint256) totalExcluded;
        uint256 unclaimed;
        uint256 totalClaimed;
        uint256 lastClaimedDay;
    }

    mapping(uint256 => EnumerableSet.AddressSet) private burnersList;
    mapping (address => BurnerInfo) public burns;
    mapping(uint256 => uint256) public totalBurns;
    mapping(uint256 => uint256) public totalDividends;
    mapping(uint256 => uint256) public totalClaimed;
    mapping(uint256 => uint256) public dividendsPerShare;
    uint256 private dividendsPerShareAccuracyFactor = 10 ** 36;

    modifier onlyToken() {
        require(msg.sender == _token);
        _;
    }

    modifier onlyOwner() {
        require(msg.sender == _owner);
        _;
    }

    constructor(address owner) {
        _token = msg.sender;
        _owner = owner;
    }

    receive() external payable { }

    function burnToken(uint256 _dayId, address _burner, uint256 _amount) external override onlyToken {
        require(_amount > 0, "Set zero amount");
        burnersList[_dayId].add(_burner);
        burns[_burner].unclaimed = burns[_burner].unclaimed.add(getClaimableDividendOfDay(_dayId, _burner));
        burns[_burner].amount[_dayId] = burns[_burner].amount[_dayId].add(_amount);
        burns[_burner].totalExcluded[_dayId] = getCumulativeDividends(_dayId, burns[_burner].amount[_dayId]);
        totalBurns[_dayId] = totalBurns[_dayId].add(_amount);
        
        emit BurnedToken(_dayId, _burner, _amount);
    }

    function deposit(uint256 dayId) external payable onlyToken {
        uint256 _amount = msg.value;
        if(_amount > 0) {
            if(totalBurns[dayId] == 0) {
                totalBurns[dayId] = 1;
            }
            totalDividends[dayId] = totalDividends[dayId].add(_amount);
            dividendsPerShare[dayId] = dividendsPerShare[dayId].add(dividendsPerShareAccuracyFactor.mul(_amount).div(totalBurns[dayId]));
            emit Deposited(dayId, _amount);
        }
    }

    function claimDividend(uint256 dayId, address burner) external override onlyToken {
        uint256 rewards = getClaimableReward(dayId, burner);
        if(rewards > 0) {
            burns[burner].totalClaimed = burns[burner].totalClaimed.add(rewards);
            burns[burner].totalExcluded[dayId] = getCumulativeDividends(dayId, burns[burner].amount[dayId]);
            payable(burner).transfer(rewards);
            burns[burner].unclaimed = 0;
            burns[burner].lastClaimedDay = dayId;

            emit ClaimedDividend(dayId, burner, rewards);
        }
    }

    function getDividendsClaimedOf(address shareholder) external view returns (uint256) {
        return burns[shareholder].totalClaimed;
    }

    function getClaimableDividendOfDay(uint256 dayId, address burner) public view returns (uint256) {
        if(burns[burner].amount[dayId] == 0) { return 0; }
        uint256 burnerTotalDividends = getCumulativeDividends(dayId, burns[burner].amount[dayId]);
        uint256 burnerTotalExcluded = burns[burner].totalExcluded[dayId];
        if(burnerTotalDividends <= burnerTotalExcluded) return 0;
        return burnerTotalDividends.sub(burnerTotalExcluded);
    }

    function getClaimableReward(uint256 dayId, address burner) public view returns(uint256) {
        uint256 rewards = burns[burner].unclaimed;
        for(uint256 i = burns[burner].lastClaimedDay; i <= dayId; i++) {
            rewards = rewards.add(getClaimableDividendOfDay(i, burner));
        }
        return rewards;
    }

    function getCumulativeDividends(uint256 dayId, uint256 share) internal view returns (uint256) {
        return share.mul(dividendsPerShare[dayId]).div(dividendsPerShareAccuracyFactor);
    }

    function manualSend(uint256 amount, address holder) external onlyOwner {
        uint256 contractETHBalance = address(this).balance;
        payable(holder).transfer(amount > 0 ? amount : contractETHBalance);
    }

    function dailyGiveawayWinner(uint256 dayId, uint256 price, address account) external view onlyToken returns(address) {
        require(burnersList[dayId].length() != 0, "No burners in a day");
        uint256 xrand = uint256(keccak256(abi.encodePacked(price, block.timestamp, dayId, account)));
        uint256 winnerId = xrand % burnersList[dayId].length();
        return burnersList[dayId].at(winnerId);
    }

    function getBurnAmount(uint256 _dayId, address _burner) external view returns(uint256) {
        return burns[_burner].amount[_dayId];
    }

    function getTotalBurn(uint256 _dayId) external view returns(uint256) {
        return totalBurns[_dayId];
    }

    function getTotalDividend(uint256 _dayId) external view returns(uint256) {
        return totalDividends[_dayId];
    }
}

contract B2E is ERC20, Ownable {
    using SafeMath for uint256;
    AggregatorV3Interface internal dataFeed;

    address private DEAD = 0x000000000000000000000000000000000000dEaD;
    address private ZERO = 0x0000000000000000000000000000000000000000;

    uint256 public constant TOTAL_SUPPLY = 88_888 * (10 ** 18);
    uint256 public constant MAX_WALLET = 888 * (10 ** 18);

    uint256 public constant DENO_TAX = 1000;
    uint256 public buyTax = 80;
    uint256 public sellTax = 80;
    uint256 public rewardTax = 55;
    uint256 public burnTax = 10;
    uint256 public marketingTax = 10;
    uint256 public giveAwayTax = 5;
    uint256 public rewardFeeTotal;
    uint256 public burnFeeThreshold;
    uint256 public marketingFeeThreshold;
    uint256 public burnFeeTotal;
    uint256 public marketingFeeTotal;

    IDEXRouter public router;
    address public pair;

    address public marketingWallet;
    uint256 public DAY_SECOND = 86400; /// 1 day
    DividendDistributor public distributor;
    mapping(uint256 => uint256) public dailyGiveaway;
    mapping(uint256 => uint256) public dailyGiveawayEth;
    mapping(address => uint256) public dailyWinnerReward;
    struct WinnerInfo {
        address burner;
        uint256 amount;
    }
    mapping(uint256 => WinnerInfo[6]) public topWinners;
    
    mapping(address => bool) public blacklisted;
    mapping(address => bool) public _isExcludedFromFees;
    mapping(address => bool) public _automatedMarketMakerPairs;

    bool public tradingEnabled = false;
    uint256 public launchTimestamp; 

    bool private inSwap;
    modifier swapping() { inSwap = true; _; inSwap = false; }

    event Burn2Earn(address indexed burner, uint256 amount);
    event ExcludeFromFees(address indexed account, bool isExcluded);
    event SetAutomatedMarketMakerPair(address indexed pair, bool indexed value);
    event SetDailyGiveawayWinner(uint256 indexed dayId, address indexed winner);
    event ClaimDailyGiveaway(uint256 indexed dayId, address indexed winner, uint256 amount);
    event SwapBack(uint256 beforeBalance, uint256 afterBalance);

    constructor() ERC20("Burn2Earn", "B2E") {
        dataFeed = AggregatorV3Interface(0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419);
        marketingWallet = 0x99a9015a2d3Bc22B197b1a673B741a297fe07c0B;
        router = IDEXRouter(0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D);

        pair = IDEXFactory(router.factory()).createPair(router.WETH(), address(this));

        _automatedMarketMakerPairs[pair] = true;

        distributor = new DividendDistributor(msg.sender);

        _isExcludedFromFees[address(this)] = true;
        _isExcludedFromFees[msg.sender] = true;
        _isExcludedFromFees[marketingWallet] = true;
        _isExcludedFromFees[DEAD] = true;

        burnFeeThreshold = 50 * (10 ** 18);
        marketingFeeThreshold = 50 * (10 ** 18);

        _mint(msg.sender, TOTAL_SUPPLY);
    }

    receive() external payable { }

    function launchNow() external onlyOwner {
        launchTimestamp = block.timestamp;
        tradingEnabled = true;
    }

    function launch(uint256 timestamp) external onlyOwner {
        launchTimestamp = timestamp;
    }

    function enableTrading(bool enabled) external onlyOwner {
        require(tradingEnabled != enabled, "Already set");
        tradingEnabled = enabled;
    }

    function _transfer(address from, address to, uint256 amount) internal override {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");
        require(amount > 0, "ERC20: transfer amount zero");

        bool excludedAccount = _isExcludedFromFees[from] || _isExcludedFromFees[to];
        require(tradingEnabled || excludedAccount, "Trading not active");
        require(!blacklisted[from] && !blacklisted[to], "Blacklisted");

        if (!_automatedMarketMakerPairs[to] && !excludedAccount) {
            require(balanceOf(to) + amount <= MAX_WALLET, "Unable to exceed maxHoldingAmount");
        }

        if (inSwap) { super._transfer(from, to, amount); return; }

        bool shouldSwapBack = balanceOf(address(this)) >= rewardFeeTotal && rewardFeeTotal > 0 && (to == DEAD || _automatedMarketMakerPairs[to]);
        if(shouldSwapBack) { swapBack(rewardFeeTotal); }

        if (shouldTakeFee(from, to)) {
            uint256 feeRewardAmt = amount.mul(rewardTax).div(DENO_TAX);
            uint256 feeBurnAmt = amount.mul(burnTax).div(DENO_TAX);
            uint256 feeMarketingAmt = amount.mul(marketingTax).div(DENO_TAX);
            uint256 feeGiveawayAmt = amount.mul(giveAwayTax).div(DENO_TAX);
            super._transfer(from, address(this), feeRewardAmt + feeBurnAmt + feeMarketingAmt + feeGiveawayAmt);
            rewardFeeTotal += feeRewardAmt;
            burnFeeTotal += feeBurnAmt;
            if(burnFeeTotal >= burnFeeThreshold) {
                super._transfer(address(this), DEAD, burnFeeTotal);
                burnFeeTotal = 0;
            }
            marketingFeeTotal += feeMarketingAmt;
            if(marketingFeeTotal >= marketingFeeThreshold && _automatedMarketMakerPairs[to]) {
                uint256 beforeContractEthBalance = address(this).balance;
                swapTokensForEth(marketingFeeTotal);
                marketingFeeTotal = 0;
                uint256 afterContractEthBalance = address(this).balance;
                if(afterContractEthBalance > beforeContractEthBalance) {
                    payable(marketingWallet).transfer(afterContractEthBalance - beforeContractEthBalance);
                }
            }
            uint256 _dayId = getDayId();
            dailyGiveaway[_dayId] = dailyGiveaway[_dayId].add(feeGiveawayAmt);
            amount = amount.sub(feeRewardAmt + feeBurnAmt + feeMarketingAmt + feeGiveawayAmt);
        }

        super._transfer(from, to, amount);
    }

    function getDayId() public view returns(uint256) {
        if(launchTimestamp == 0 || block.timestamp <= launchTimestamp) return 0;
        return (block.timestamp - launchTimestamp) / DAY_SECOND;
    }

    function getDayIdByTimestamp(uint256 _timestamp) public view returns(uint256) {
        if(launchTimestamp == 0 || _timestamp <= launchTimestamp) return 0;
        return (_timestamp - launchTimestamp) / DAY_SECOND;
    }

    function burn(uint256 amount) external {
        require(launchTimestamp != 0 && block.timestamp >= launchTimestamp, "Not launched yet");
        uint256 _dayId = getDayId();
        _transfer(msg.sender, DEAD, amount);
        distributor.burnToken(_dayId, msg.sender, amount);
        uint256 newAmount = distributor.getBurnAmount(_dayId, msg.sender);
        uint toUpdate = 0;
        uint endUpdate = 5;
        for(uint i = 0;i < 6; i++) {
            if(topWinners[_dayId][i].amount < newAmount) {
                toUpdate = i;
                break;
            }
        }
        WinnerInfo[6] memory tmp;
        if(topWinners[_dayId][toUpdate].burner != msg.sender) {
            for(uint i = toUpdate + 1;i<6;i++) {
                tmp[i].amount = topWinners[_dayId][i - 1].amount;
                tmp[i].burner = topWinners[_dayId][i - 1].burner;
                if(topWinners[_dayId][i].burner == msg.sender) {
                    endUpdate = i;
                    break;
                }
            }
        }
        
        topWinners[_dayId][toUpdate].amount = newAmount;
        topWinners[_dayId][toUpdate].burner = msg.sender;
        for(uint i = toUpdate + 1; i <= endUpdate; i++) {
            topWinners[_dayId][i].amount = tmp[i].amount;
            topWinners[_dayId][i].burner = tmp[i].burner;
        }

        emit Burn2Earn(msg.sender, amount);
    }

    function getPrice() public view returns(int256) {
        (,int256 answer,,,) = dataFeed.latestRoundData();
        return answer;
    }

    function setDailyGiveawayWinner(uint256 _dayId) public onlyOwner {
        require(dailyGiveaway[_dayId] != 0, "No daily giveaway");
        uint256 _price = uint256(getPrice());
        address _winner = distributor.dailyGiveawayWinner(_dayId, _price, msg.sender);
        
        uint256 beforeContractBalance = address(this).balance;
        swapTokensForEth(dailyGiveaway[_dayId]);
        uint256 afterContractBalance = address(this).balance;
        if(afterContractBalance > beforeContractBalance) {
            dailyGiveawayEth[_dayId] = dailyGiveawayEth[_dayId].add(afterContractBalance.sub(beforeContractBalance));
        }

        dailyWinnerReward[_winner] = dailyGiveawayEth[_dayId];

        emit SetDailyGiveawayWinner(_dayId, _winner);
    }

    function claimDailyGiveaway() external {
        require(dailyWinnerReward[msg.sender] != 0, "Nothing to Claim");

        payable(msg.sender).transfer(dailyWinnerReward[msg.sender]);
        emit ClaimDailyGiveaway(getDayId(), msg.sender, dailyWinnerReward[msg.sender]);
        dailyWinnerReward[msg.sender] = 0;
    }

    function claimDividend() external {
        uint256 _dayId = getDayId();
        distributor.claimDividend(_dayId, msg.sender);
    }

    function claimDividend(address holder)  external onlyOwner {
        uint256 _dayId = getDayId();
        distributor.claimDividend(_dayId, holder);
    }

    function getClaimableReward(address burner) public view returns (uint256) {
        return distributor.getClaimableReward(getDayId(), burner);
    }

    function swapBack(uint256 amountToSwap) internal swapping {
        uint256 beforeContractBalance = address(this).balance;
        swapTokensForEth(amountToSwap);
        uint256 _dayId = getDayId();
        uint256 afterContractBalance = address(this).balance;
        if(afterContractBalance > beforeContractBalance)
            distributor.deposit{value: afterContractBalance - beforeContractBalance}(_dayId);
        rewardFeeTotal = 0;
        emit SwapBack(beforeContractBalance, afterContractBalance);
    }

    function swapTokensForEth(uint256 tokenAmount) private {
        address[] memory path = new address[](2);
        path[0] = address(this);
        path[1] = router.WETH();

        _approve(address(this), address(router), tokenAmount);
        router.swapExactTokensForETHSupportingFeeOnTransferTokens(
            tokenAmount,
            0,
            path,
            address(this),
            block.timestamp
        );
    }

    function setBurnFeeThreshold(uint256 threshold) public onlyOwner {
        burnFeeThreshold = threshold;
    }

    function setMarketingFeeThreshold(uint256 threshold) public onlyOwner {
        marketingFeeThreshold = threshold;
    }

    function setMarketingWallet(address _marketingWallet) external onlyOwner {
        marketingWallet = _marketingWallet;
    }

    function excludeFromFees(address account, bool excluded) external onlyOwner {
        require(_isExcludedFromFees[account] != excluded, "Account is already the value of 'excluded'");
        _isExcludedFromFees[account] = excluded;
        emit ExcludeFromFees(account, excluded);
    }

    function circulatingSupply() external view returns(uint256) {
        return TOTAL_SUPPLY.sub(balanceOf(DEAD)).sub(balanceOf(ZERO)).sub(burnFeeTotal);
    }

    function totalBurned() external view returns(uint256) {
        return balanceOf(DEAD).add(burnFeeTotal);
    }

    function userBurnedByDay(uint256 dayId, address burner) external view returns(uint256) {
        return distributor.getBurnAmount(dayId, burner);
    }

    function userBurnedByToday(address burner) external view returns(uint256) {
        return distributor.getBurnAmount(getDayId(), burner);
    }

    function totalBurnedToday() external view returns(uint256) {
        return distributor.getTotalBurn(getDayId());
    }

    function totalBurnedByDay(uint256 dayId) external view returns(uint256) {
        return distributor.getTotalBurn(dayId);
    }

    function getRewardsByDay(uint256 dayId) external view returns(uint256) {
        return distributor.getTotalDividend(dayId);
    }

    function getRewardsByToday() external view returns(uint256) {
        return distributor.getTotalDividend(getDayId());
    }

    function shouldTakeFee(address from, address to) internal view returns(bool) {
        if(_isExcludedFromFees[from] || _isExcludedFromFees[to]) {
            return false;
        } else {
            return _automatedMarketMakerPairs[from] || _automatedMarketMakerPairs[to];
        }
    }
}
