// File: @openzeppelin/contracts/security/ReentrancyGuard.sol


// OpenZeppelin Contracts (last updated v4.9.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Returns true if the reentrancy guard is currently set to "entered", which indicates there is a
     * `nonReentrant` function in the call stack.
     */
    function _reentrancyGuardEntered() internal view returns (bool) {
        return _status == _ENTERED;
    }
}

// File: @openzeppelin/contracts/utils/Context.sol


// OpenZeppelin Contracts (last updated v5.0.1) (utils/Context.sol)

pragma solidity ^0.8.20;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    function _contextSuffixLength() internal view virtual returns (uint256) {
        return 0;
    }
}

// File: @openzeppelin/contracts/access/Ownable.sol


// OpenZeppelin Contracts (last updated v5.0.0) (access/Ownable.sol)

pragma solidity ^0.8.20;


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * The initial owner is set to the address provided by the deployer. This can
 * later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    /**
     * @dev The caller account is not authorized to perform an operation.
     */
    error OwnableUnauthorizedAccount(address account);

    /**
     * @dev The owner is not a valid owner account. (eg. `address(0)`)
     */
    error OwnableInvalidOwner(address owner);

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the address provided by the deployer as the initial owner.
     */
    constructor(address initialOwner) {
        if (initialOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(initialOwner);
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        if (owner() != _msgSender()) {
            revert OwnableUnauthorizedAccount(_msgSender());
        }
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        if (newOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// File: @openzeppelin/contracts/token/ERC20/IERC20.sol


// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the value of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the value of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves a `value` amount of tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 value) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets a `value` amount of tokens as the allowance of `spender` over the
     * caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 value) external returns (bool);

    /**
     * @dev Moves a `value` amount of tokens from `from` to `to` using the
     * allowance mechanism. `value` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 value) external returns (bool);
}

// File: contracts/Cadaico Tokensale/Cadaico Token Vesting.sol


pragma solidity ^0.8.0;




/**
 * @title TokenVesting
 * @dev A token holder contract that can release its token balance gradually like a
 * typical vesting scheme, with a cliff and vesting period. Optionally revocable by the owner.
 */
contract TokenVesting is Ownable, ReentrancyGuard {
    IERC20 private _token;

    struct VestingSchedule {
        uint256 amountTotal;
        uint256 amountReleased;
        bool isBlacklisted;
    }

    uint256 public start;
    uint256 public duration;

    mapping(address => VestingSchedule) public vestingSchedules;

    event BeneficiaryAdded(address indexed beneficiary, uint256 totalAmount);
    event TokensReleased(address indexed beneficiary, uint256 amount);
    event BeneficiaryBlacklisted(address indexed beneficiary);
    event BeneficiaryWhitelisted(address indexed beneficiary);
    event VestingScheduleUpdated(uint256 start, uint256 duration);

    /**
     * @dev Sets the initial parameters for the vesting contract.
     */
    constructor(address initialOwner) Ownable(initialOwner) {}

    /**
     * @notice Adds multiple beneficiaries to the vesting schedule in a single transaction.
     * @param beneficiaries Array of addresses of the beneficiaries to whom vested tokens are transferred.
     * @param totalAmounts Array of total amounts of tokens to be vested for each beneficiary.
     */
    function addBeneficiaries(address[] calldata beneficiaries, uint256[] calldata totalAmounts) public onlyOwner {
        require(beneficiaries.length == totalAmounts.length, "TokenVesting: Array lengths do not match");
        for (uint256 i = 0; i < beneficiaries.length; i++) {
            address beneficiary = beneficiaries[i];
            uint256 totalAmount = totalAmounts[i]*(10**18);
            require(beneficiary != address(0), "TokenVesting: beneficiary is the zero address");
            require(vestingSchedules[beneficiary].amountTotal == 0, "TokenVesting: beneficiary already added");
            vestingSchedules[beneficiary] = VestingSchedule(totalAmount, 0, false);
            emit BeneficiaryAdded(beneficiary, totalAmount);
        }
    }

    /**
     * @notice Sets the global vesting schedule for all beneficiaries.
     * @param _start The time (as Unix time) at which point vesting starts.
     * @param _duration Duration in seconds of the period in which the tokens will vest.
     */
    function setVestingSchedule(uint256 _start, uint256 _duration) public onlyOwner {
        start = _start;
        duration = _duration;
        emit VestingScheduleUpdated(_start, _duration);
    }

    /**
     * @notice Transfers vested tokens to beneficiary
     */
    function Withdraw() public nonReentrant {
        address beneficiary = msg.sender; // beneficiary is the message sender
        require(!vestingSchedules[beneficiary].isBlacklisted, "TokenVesting: beneficiary is blacklisted");
        require(vestingSchedules[beneficiary].amountTotal > 0, "TokenVesting: sender is not a beneficiary");
        uint256 unreleased = _releasableAmount(beneficiary);

        require(unreleased > 0, "TokenVesting: no tokens are due");

        vestingSchedules[beneficiary].amountReleased += unreleased;
        _token.transfer(beneficiary, unreleased);

        emit TokensReleased(beneficiary, unreleased);
    }

    /**
     * @notice Calculates the amount that has already vested but hasn't been released yet.
     * @param beneficiary address of the beneficiary
     */
    function _releasableAmount(address beneficiary) public view returns (uint256) {
        return _vestedAmount(beneficiary) - vestingSchedules[beneficiary].amountReleased;
    }

    /**
     * @notice Calculates the amount that has already vested.
     * @param beneficiary address of the beneficiary
     */
    function _vestedAmount(address beneficiary) private view returns (uint256) {
        VestingSchedule storage schedule = vestingSchedules[beneficiary];
        uint256 totalBalance = schedule.amountTotal;

        if (block.timestamp < start) {
            return 0;
        } else if (block.timestamp >= start + duration) {
            return totalBalance;
        } else {
            uint256 initialUnlock = totalBalance * 15 / 100;
            uint256 postStart = block.timestamp - start;
            uint256 remainingBalance = totalBalance - initialUnlock;
            uint256 vestingDuration = duration;

            return initialUnlock + (remainingBalance * postStart / vestingDuration);
        }
    }

    /**
     * @notice Blacklists a beneficiary
     * @param beneficiary address of the beneficiary
     */
    function blacklistBeneficiary(address beneficiary) public onlyOwner {
        vestingSchedules[beneficiary].isBlacklisted = true;
        emit BeneficiaryBlacklisted(beneficiary);
    }

    /**
     * @notice Whitelists a beneficiary
     * @param beneficiary address of the beneficiary
     */
    function whitelistBeneficiary(address beneficiary) public onlyOwner {
        vestingSchedules[beneficiary].isBlacklisted = false;
        emit BeneficiaryWhitelisted(beneficiary);
    }

    /**
     * @dev Set the token for vesting.
     * @param newToken address of the new ERC20 token
     */
    function setToken(address newToken) public onlyOwner {
        require(newToken != address(0), "TokenVesting: new token is the zero address");
        _token = IERC20(newToken);
    }

    function ownerWithdraw(uint256 amount) public onlyOwner nonReentrant {
        require(amount > 0, "TokenVesting: Amount must be greater than 0");
       
        _token.transfer(owner(), amount);
    }

}