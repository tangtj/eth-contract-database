
// File: contracts/MigrationZapper.sol
// SPDX-License-Identifier: MIT
pragma solidity 0.8.10;

import {IStaking} from "./interfaces/IStaking.sol";
import {IMigrator} from "./interfaces/IMigrator.sol";
import {IMintableERC20} from "./interfaces/IMintableERC20.sol";

contract MigrationZapper {
    IMintableERC20 public immutable ogv;
    IMintableERC20 public immutable ogn;

    IMigrator public immutable migrator;
    IStaking public immutable ognStaking;

    address public immutable governor;

    error NotGovernor();

    constructor(address _ogv, address _ogn, address _migrator, address _ognStaking, address _governor) {
        ogv = IMintableERC20(_ogv);
        ogn = IMintableERC20(_ogn);
        migrator = IMigrator(_migrator);
        ognStaking = IStaking(_ognStaking);

        governor = _governor;
    }

    function initialize() external {
        // Migrator can move OGV and OGN from this contract
        ogv.approve(address(migrator), type(uint256).max);
        ogn.approve(address(ognStaking), type(uint256).max);
    }

    /**
     * @notice Migrates the specified amount of OGV to OGN.
     * @param ogvAmount Amount of OGV to migrate
     */
    function migrate(uint256 ogvAmount) external {
        // Take tokens in
        ogv.transferFrom(msg.sender, address(this), ogvAmount);

        // Proxy migrate call
        uint256 ognReceived = migrator.migrate(ogvAmount);

        // Transfer OGN to the receiver
        ogn.transfer(msg.sender, ognReceived);
    }

    /**
     * @notice Migrates the specified amount of OGV to OGN
     *         and stakes it.
     * @param ogvAmount Amount of OGV to migrate
     */
    function migrate(uint256 ogvAmount, uint256 newStakeAmount, uint256 newStakeDuration) external {
        // Take tokens in
        ogv.transferFrom(msg.sender, address(this), ogvAmount);

        // Migrate
        uint256 ognReceived = migrator.migrate(ogvAmount);

        // Stake on behalf of user
        ognStaking.stake(
            newStakeAmount,
            newStakeDuration,
            msg.sender,
            false,
            -1 // New stake
        );

        // Transfer remaining OGN to the receiver
        if (ognReceived > newStakeAmount) {
            ogn.transfer(msg.sender, ognReceived - newStakeAmount);
        }
    }

    /**
     * Transfers any tokens sent by mistake out of the contract
     * @param token Token address
     * @param amount Amount of token to transfer
     */
    function transferTokens(address token, uint256 amount) external {
        if (msg.sender != governor) {
            revert NotGovernor();
        }

        IMintableERC20(token).transfer(governor, amount);
    }
}


// File: contracts/interfaces/IStaking.sol
// SPDX-License-Identifier: MIT
pragma solidity 0.8.10;

interface IStaking {
    function delegates(address staker) external view returns (address);

    // From OGVStaking.sol
    function unstakeFrom(address staker, uint256[] memory lockupIds) external returns (uint256, uint256);

    // From ExponentialStaking.sol
    function stake(uint256 amountIn, uint256 duration, address to, bool stakeRewards, int256 lockupId) external;
}


// File: contracts/interfaces/IMigrator.sol
// SPDX-License-Identifier: MIT
pragma solidity 0.8.10;

interface IMigrator {
    function migrate(uint256 ogvAmount) external returns (uint256);

    function migrate(
        uint256[] calldata lockupIds,
        uint256 ogvAmountFromWallet,
        uint256 ognAmountFromWallet,
        bool migrateRewards,
        uint256 newStakeAmount,
        uint256 newStakeDuration
    ) external;
}


// File: contracts/interfaces/IMintableERC20.sol
// SPDX-License-Identifier: Unlicense
pragma solidity 0.8.10;

interface IMintableERC20 {
    function mint(address to, uint256 amount) external;
    function balanceOf(address owner) external view returns (uint256);
    function totalSupply() external view returns (uint256);
    function transfer(address to, uint256 amount) external returns (bool);
    function transferFrom(address _from, address to, uint256 amount) external returns (bool);
    function approve(address spender, uint256 allowance) external;
}

