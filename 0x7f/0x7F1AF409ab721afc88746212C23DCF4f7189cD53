
// File: contracts/Aiden/PrivateSale.sol
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "@openzeppelin/contracts/utils/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/Pausable.sol";

/**
 * @dev This contract is designed to work as pre sale token.
 * Has function to buy token with stable coin.
 * Has function to withdraw token after period of time.
 */
contract PrivateSale is Ownable, AccessControl, ReentrancyGuard, Pausable {
    using SafeERC20 for IERC20;

    struct StableCoinInfo {
        address stableCoinAddress;
        string symbol;
        uint256 totalAmount;
    }
    struct UserInfo {
        uint256 totalStableCoinAmount;
        uint256 timesOfBuy;
    }

    struct OrderInfo {
        uint256 stableCoinId;
        uint256 stableCoinAmount;
        uint256 tokenAmount;
        uint256 claimedTokenAmount;
        uint256 boughtDate;
    }

    struct RoundPaid {
        uint256 paidIntervals;
        uint256 paidPortions;
    }

    bytes32 public immutable PROJECTOWNER = keccak256("PROJECTOWNER");
    bytes32 public immutable BUYER = keccak256("BUYER");
    uint256 public constant DAY = 86400;
    uint256 public constant TOKENDECIMAL = 1000000000000000000;
    IERC20 public token;
    uint256 public immutable rate;
    uint256 public immutable maxUserBuyingLimit;
    uint256 public startDate;
    uint256 public endDate;
    uint256 public totalToken;
    uint256 public totalTokenBought;
    bool public closeSale;

    RoundPaid[] roundPaid;
    uint256 public currentTotalToken;
    uint256 public totalClaimedToken;
    uint256 public totalStableCoin;
    uint256 public totalWithdrawnStableCoin;

    mapping(address => UserInfo) users;
    mapping(uint256 => StableCoinInfo) stableCoinInfo;
    mapping(address => mapping(uint256 => OrderInfo)) orders;

    event RoundPaidAdded(
        uint256 indexed index,
        uint256 paidInterval,
        uint256 paidPortion
    );
    event TokenBought(
        address indexed userAddress,
        uint256 stableCoinId,
        uint256 stableCoinAmount,
        uint256 tokenAmount,
        uint256 timesOfBuy
    );
    event TokenWithdrawn(address indexed userAddress, uint256 tokenAmount);
    event TokenClaimed(
        address indexed userAddress,
        uint256 tokenAmount,
        uint256 timesOfBuy
    );
    event ClaimAlls(address indexed userAddress, uint256 totalClaimAmount);
    event StableCoinWithdrawn(
        address indexed receiver,
        uint256 stableCoinId,
        uint256 stableCoinAmount
    );
    event AddStableCoin(
        uint256 stableCoinId,
        address stableCoinAddress,
        string symbol
    );
    event EditStableCoin(
        uint256 stableCoinId,
        address stableCoinAddress,
        string symbol
    );
    event RemoveStableCoin(
        uint256 stableCoinId,
        address stableCoinAddress,
        string symbol
    );
    event StartSale(uint256 tokenAmount, uint256 startSale, uint256 endSale);
    event EndSale(uint256 endSaleTimes);

    /**
     * @dev Setup contract params and interface.
     * Setup role for deployer.
     */

    constructor(
        address _tokenAddress,
        uint256 _rate,
        uint256 _maxUserBuyingLimit,
        RoundPaid[] memory _roundPaid,
        address _projectOwner
    ) Ownable(_projectOwner) {
        token = IERC20(_tokenAddress);
        rate = _rate;
        maxUserBuyingLimit = _maxUserBuyingLimit * 10**6;
        for (uint256 i = 0; i < _roundPaid.length; i++) {
            roundPaid.push(_roundPaid[i]);
            emit RoundPaidAdded(
                i,
                _roundPaid[i].paidIntervals,
                _roundPaid[i].paidPortions
            );
        }
        _grantRole(DEFAULT_ADMIN_ROLE, _projectOwner);
        _grantRole(PROJECTOWNER, _projectOwner);
    }

    /**
     * @dev Revert receive and fallback functions.
     */
    receive() external payable {
        revert("Revert receive function");
    }

    fallback() external payable {
        revert("Revert fallback function");
    }

    /**
     * @dev Add a new stable coin to the list of accepted stable coins.
     * @param _stableCoinId The ID of the stable coin to be added.
     * @param _stableCoinAddress The address of the stable coin contract.
     * @param _symbol The symbol of the stable coin.
     * This function can only be called by the project owner.
     */
    function addStableCoin(
        uint256 _stableCoinId,
        address _stableCoinAddress,
        string memory _symbol
    ) external onlyRole(PROJECTOWNER) {
        require(
            stableCoinInfo[_stableCoinId].stableCoinAddress == address(0),
            "This stable coin id has been added"
        );
        stableCoinInfo[_stableCoinId].stableCoinAddress = _stableCoinAddress;
        stableCoinInfo[_stableCoinId].symbol = _symbol;
        emit AddStableCoin(_stableCoinId, _stableCoinAddress, _symbol);
    }

    /**
     * @dev Edit the details of an existing stable coin in the list.
     * @param _stableCoinId The ID of the stable coin to be edited.
     * @param _stableCoinAddress The new address of the stable coin contract.
     * @param _symbol The new symbol of the stable coin.
     * This function can only be called by the project owner.
     */
    function editStableCoin(
        uint256 _stableCoinId,
        address _stableCoinAddress,
        string memory _symbol
    ) external onlyRole(PROJECTOWNER) {
        require(
            stableCoinInfo[_stableCoinId].stableCoinAddress != address(0),
            "This stable coin id has not been added yet"
        );
        stableCoinInfo[_stableCoinId].stableCoinAddress = _stableCoinAddress;
        stableCoinInfo[_stableCoinId].symbol = _symbol;
        emit EditStableCoin(_stableCoinId, _stableCoinAddress, _symbol);
    }

    /**
     * @dev Remove a stable coin from the list of accepted stable coins.
     * @param _stableCoinId The ID of the stable coin to be removed.
     * This function can only be called by the project owner.
     */
    function removeStableCoin(uint256 _stableCoinId)
        external
        onlyRole(PROJECTOWNER)
    {
        StableCoinInfo memory stableCoin = stableCoinInfo[_stableCoinId];
        require(
            stableCoin.stableCoinAddress != address(0),
            "This stable coin id has not been added yet"
        );
        emit RemoveStableCoin(
            _stableCoinId,
            stableCoin.stableCoinAddress,
            stableCoin.symbol
        );
        delete stableCoinInfo[_stableCoinId];
    }

    /**
     * @dev Allow the project owner to withdraw stable coins from the contract.
     * @param _to The address to receive the withdrawn stable coins.
     * @param _stableCoinId The ID of the stable coin to be withdrawn.
     * This function can only be called by the project owner.
     */
    function withdrawStableCoin(address _to, uint256 _stableCoinId)
        external
        onlyRole(PROJECTOWNER)
    {
        StableCoinInfo storage stableCoin = stableCoinInfo[_stableCoinId];
        uint256 withdrawAmount = stableCoin.totalAmount;
        require(
            stableCoin.stableCoinAddress != address(0),
            "This stable coin id has not been added yet"
        );
        require(
            withdrawAmount > 0,
            "Insufficient stable coin available for withdraw"
        );
        IERC20(stableCoin.stableCoinAddress).safeTransfer(_to, withdrawAmount);
        stableCoin.totalAmount -= withdrawAmount;
        totalWithdrawnStableCoin += withdrawAmount;
        totalStableCoin -= withdrawAmount;

        emit StableCoinWithdrawn(_to, _stableCoinId, withdrawAmount);
    }

    /**
     * @dev Allow the project owner to withdraw unsold tokens from the contract after the sale period ends.
     * This function can only be called by the project owner, and only after the sale period has ended.
     */
    function withdrawToken() external onlyRole(PROJECTOWNER) {
        require(
            block.timestamp >= endDate || closeSale,
            "Pre sale must close the sale first"
        );

        require(
            currentTotalToken != 0,
            "Insufficient tokens available for withdraw"
        );
        uint256 withdrawAmount = currentTotalToken;
        currentTotalToken = 0;
        token.safeTransfer(msg.sender, withdrawAmount);
        emit TokenWithdrawn(msg.sender, withdrawAmount);
    }

    /**
     * @dev Allow users to buy tokens using stable coins.
     * @param _stableCoinId The ID of the stable coin used for the purchase.
     * @param _stableAmount The amount of stable coins used for the purchase.
     * This function enforces the maximum user and contract buying limits, and ensures that
     * there are enough tokens available for purchase.
     */
    function buyToken(uint256 _stableCoinId, uint256 _stableAmount)
        external
        onlyRole(BUYER)
    {
        uint256 tokenAmount = (rate * _stableAmount) / TOKENDECIMAL;
        StableCoinInfo memory stableCoin = stableCoinInfo[_stableCoinId];
        UserInfo memory userInfo = users[msg.sender];
        require(block.timestamp < endDate, "Sale period has ended");
        require(!closeSale, "Close Salesed");
        require(
            stableCoin.stableCoinAddress != address(0),
            "This stable coin id has not been added yet"
        );
        require(
            tokenAmount <= currentTotalToken,
            "Insufficient tokens available for buy"
        );
        require(_stableAmount > 0, "Stable Coin amount must be greater than 0");
        require(
            userInfo.totalStableCoinAmount + _stableAmount <=
                maxUserBuyingLimit,
            "Exceed max user buying limit"
        );
        totalStableCoin += _stableAmount;
        users[msg.sender].totalStableCoinAmount += _stableAmount;
        users[msg.sender].timesOfBuy++;
        uint256 timesOfbuy = users[msg.sender].timesOfBuy;
        orders[msg.sender][timesOfbuy].boughtDate = block.timestamp;
        orders[msg.sender][timesOfbuy].stableCoinId = _stableCoinId;
        orders[msg.sender][timesOfbuy].stableCoinAmount += _stableAmount;
        orders[msg.sender][timesOfbuy].tokenAmount += tokenAmount;
        totalTokenBought += tokenAmount;
        currentTotalToken -= tokenAmount;
        stableCoinInfo[_stableCoinId].totalAmount += _stableAmount;
        IERC20(stableCoinInfo[_stableCoinId].stableCoinAddress)
            .safeTransferFrom(msg.sender, address(this), _stableAmount);

        emit TokenBought(
            msg.sender,
            _stableCoinId,
            _stableAmount,
            tokenAmount,
            timesOfbuy
        );
    }

    /**
     * @dev Allow users to claim their purchased tokens after a certain period of time.
     * @param _numOfbuy The number of the user's purchase order.
     * This function uses the `onlyBought` modifier to ensure that the user has a valid purchase order,
     * and the `nonReentrant` modifier to prevent reentrancy attacks.
     */
    function claimToken(uint256 _numOfbuy) public nonReentrant {
        uint256 claimAmount = calculateClaimAmount(msg.sender, _numOfbuy);
        require(claimAmount > 0, "Zero claim amount");

        totalClaimedToken += claimAmount;
        orders[msg.sender][_numOfbuy].claimedTokenAmount += claimAmount;
        token.safeTransfer(msg.sender, claimAmount);

        emit TokenClaimed(msg.sender, claimAmount, _numOfbuy);
    }

    /**
     * @dev Allow users to claim all their purchased tokens at once.
     * This function uses the `nonReentrant` modifier to prevent reentrancy attacks.
     */
    function claimAll() external nonReentrant {
        uint256 totalClaimAmount;
        UserInfo memory userInfo = users[msg.sender];
        for (uint256 i = 1; i <= userInfo.timesOfBuy; i++) {
            uint256 claimAmount = calculateClaimAmount(msg.sender, i);
            if (claimAmount > 0) {
                totalClaimAmount += claimAmount;
                orders[msg.sender][i].claimedTokenAmount += claimAmount;
                emit TokenClaimed(msg.sender, claimAmount, i);
            }
        }
        require(totalClaimAmount > 0, "Zero claim amount");
        totalClaimedToken += totalClaimAmount;
        token.safeTransfer(msg.sender, totalClaimAmount);
        emit ClaimAlls(msg.sender, totalClaimAmount);
    }

    function startSale(uint256 _tokenAmount, uint64 _periodSale)
        external
        onlyRole(PROJECTOWNER)
    {
        require(startDate == 0, "Sale has already started");
        require(_tokenAmount > 0, "Token amount must be greater than 0");
        require(_periodSale > 0, "Sale period must be greater than 0");
        totalToken += _tokenAmount;
        currentTotalToken += _tokenAmount;
        startDate = block.timestamp;
        endDate = startDate + _periodSale * DAY;
        token.safeTransferFrom(msg.sender, address(this), _tokenAmount);
        emit StartSale(_tokenAmount, startDate, endDate);
    }

    function endSale() external onlyRole(PROJECTOWNER) {
        require(startDate > 0, "Sale has not already started");
        require(!closeSale, "Sale has already close Salesed");
        closeSale = true;
        emit EndSale(block.timestamp);
    }

    function setTokenAddress(address _newTokenAddress)
        external
        onlyRole(PROJECTOWNER)
    {
        require(startDate == 0, "Sale has already started");
        token = IERC20(_newTokenAddress);
    }

    /**
     * @dev Calculate the amount of tokens that a user can claim for a given purchase order.
     * @param _userAddress The address of the user.
     * @param _numOfbuy The number of the user's purchase order.
     * @return The amount of tokens that the user can claim.
     */
    function calculateClaimAmount(address _userAddress, uint256 _numOfbuy)
        public
        view
        returns (uint256)
    {
        OrderInfo memory order = orders[_userAddress][_numOfbuy];
        uint256 claimAmount;
        uint256 totalPaidPortion;

        uint256 currentTime = block.timestamp;

        // Create a memory array from roundPaid
        RoundPaid[] memory roundPaidMemory = roundPaid;

        if (
            currentTime >=
            order.boughtDate + (roundPaidMemory[0].paidIntervals * DAY)
        ) {
            for (uint256 i = 0; i < roundPaidMemory.length; i++) {
                if (
                    currentTime >=
                    order.boughtDate + (roundPaidMemory[i].paidIntervals * DAY)
                ) {
                    totalPaidPortion += roundPaidMemory[i].paidPortions;
                } else {
                    break;
                }
            }
            claimAmount =
                ((totalPaidPortion * order.tokenAmount) / 10000) -
                order.claimedTokenAmount;
        }
        return claimAmount;
    }

    /**
     * @dev Get the round paid schedule for token claims.
     * @return An array of `RoundPaid` structs representing the round paid schedule.
     */
    function getRoundPaid() external view returns (RoundPaid[] memory) {
        return roundPaid;
    }

    /**
     * @dev Get the details of a user's purchase order.
     * @param _userAddress The address of the user.
     * @param _numOfbuy The number of the user's purchase order.
     */

    function getOrderInfo(address _userAddress, uint256 _numOfbuy)
        public
        view
        returns (
            uint256 stableCoinId,
            uint256 stableCoinAmount,
            uint256 tokenAmount,
            uint256 claimedTokenAmount,
            uint256 boughtDate
        )
    {
        OrderInfo memory order = orders[_userAddress][_numOfbuy];
        stableCoinId = order.stableCoinId;
        stableCoinAmount = order.stableCoinAmount;
        tokenAmount = order.tokenAmount;
        claimedTokenAmount = order.claimedTokenAmount;
        boughtDate = order.boughtDate;
    }

    /**
     * @dev Get the total stable coin amount and number of purchases for a user.
     * @param _userAddress The address of the user.
     */
    function getUserInfo(address _userAddress)
        public
        view
        returns (uint256 totalStableCoinAmount, uint256 timesOfBuy)
    {
        UserInfo memory user = users[_userAddress];
        totalStableCoinAmount = user.totalStableCoinAmount;
        timesOfBuy = user.timesOfBuy;
    }

    /**
     * @dev Get the details of a stable coin in the list.
     * @param _stableCoinId The ID of the stable coin.
     */
    function getStableCoinInfo(uint256 _stableCoinId)
        public
        view
        returns (
            address stableCoinAddress,
            string memory symbol,
            uint256 totalAmount
        )
    {
        StableCoinInfo memory stableCoin = stableCoinInfo[_stableCoinId];
        stableCoinAddress = stableCoin.stableCoinAddress;
        symbol = stableCoin.symbol;
        totalAmount = stableCoin.totalAmount;
    }

    /**
     * @dev Get the number of days passed since a user's purchase order.
     * @param _userAddress The address of the user.
     * @param _numOfbuy The number of the user's purchase order.
     * @return The number of days passed since the purchase order.
     */
    function getDayPassed(address _userAddress, uint256 _numOfbuy)
        public
        view
        returns (uint256)
    {
        OrderInfo memory order = orders[_userAddress][_numOfbuy];
        uint256 currentTime = block.timestamp;
        uint256 elapsedTime = currentTime - order.boughtDate;
        uint256 dayPassed = elapsedTime / DAY;
        return dayPassed;
    }

    /**
     * @dev Check the total amount of tokens that a user can claim for all their purchase orders.
     * @param _userAddress The address of the user.
     * @return The total amount of tokens that the user can claim.
     */
    function checkClaimAllAmount(address _userAddress)
        public
        view
        returns (uint256)
    {
        uint256 totalClaimAmount;
        UserInfo memory userInfo = users[_userAddress];
        for (uint256 i = 1; i <= userInfo.timesOfBuy; i++) {
            uint256 claimAmount = calculateClaimAmount(_userAddress, i);
            if (claimAmount > 0) {
                totalClaimAmount += claimAmount;
            }
        }
        return totalClaimAmount;
    }

    /**
     * @dev Get the stable coin amounts and purchase dates for a user's purchase orders.
     * @param _userAddress The address of the user.
     */
    function getStableCoinAndTimeBought(address _userAddress)
        public
        view
        returns (uint256[] memory stableCoinAmount, uint256[] memory timeBought)
    {
        uint256 length = users[_userAddress].timesOfBuy;
        stableCoinAmount = new uint256[](length);
        timeBought = new uint256[](length);
        for (uint256 i = 1; i <= length; i++) {
            OrderInfo memory order = orders[_userAddress][i];
            stableCoinAmount[i - 1] = order.stableCoinAmount;
            timeBought[i - 1] = order.boughtDate;
        }
    }

    /**
     * @dev Function for PROJECTOWNER to grant user a buyer role.
     * Only PROJECTOWNER can grant buyer role.
     */
    function adminGrantRoleBuyer(address _buyerAddress)
        external
        onlyRole(PROJECTOWNER)
    {
        _grantRole(BUYER, _buyerAddress);
    }
}


// File: @openzeppelin/contracts/utils/Pausable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/Pausable.sol)

pragma solidity ^0.8.20;

import {Context} from "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    bool private _paused;

    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    /**
     * @dev The operation failed because the contract is paused.
     */
    error EnforcedPause();

    /**
     * @dev The operation failed because the contract is not paused.
     */
    error ExpectedPause();

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        if (paused()) {
            revert EnforcedPause();
        }
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        if (!paused()) {
            revert ExpectedPause();
        }
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}


// File: @openzeppelin/contracts/utils/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/ReentrancyGuard.sol)

pragma solidity ^0.8.20;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant NOT_ENTERED = 1;
    uint256 private constant ENTERED = 2;

    uint256 private _status;

    /**
     * @dev Unauthorized reentrant call.
     */
    error ReentrancyGuardReentrantCall();

    constructor() {
        _status = NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be NOT_ENTERED
        if (_status == ENTERED) {
            revert ReentrancyGuardReentrantCall();
        }

        // Any calls to nonReentrant after this point will fail
        _status = ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = NOT_ENTERED;
    }

    /**
     * @dev Returns true if the reentrancy guard is currently set to "entered", which indicates there is a
     * `nonReentrant` function in the call stack.
     */
    function _reentrancyGuardEntered() internal view returns (bool) {
        return _status == ENTERED;
    }
}


// File: @openzeppelin/contracts/access/AccessControl.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (access/AccessControl.sol)

pragma solidity ^0.8.20;

import {IAccessControl} from "./IAccessControl.sol";
import {Context} from "../utils/Context.sol";
import {ERC165} from "../utils/introspection/ERC165.sol";

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```solidity
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```solidity
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it. We recommend using {AccessControlDefaultAdminRules}
 * to enforce additional security measures for this role.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address account => bool) hasRole;
        bytes32 adminRole;
    }

    mapping(bytes32 role => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with an {AccessControlUnauthorizedAccount} error including the required role.
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role);
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view virtual returns (bool) {
        return _roles[role].hasRole[account];
    }

    /**
     * @dev Reverts with an {AccessControlUnauthorizedAccount} error if `_msgSender()`
     * is missing `role`. Overriding this function changes the behavior of the {onlyRole} modifier.
     */
    function _checkRole(bytes32 role) internal view virtual {
        _checkRole(role, _msgSender());
    }

    /**
     * @dev Reverts with an {AccessControlUnauthorizedAccount} error if `account`
     * is missing `role`.
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!hasRole(role, account)) {
            revert AccessControlUnauthorizedAccount(account, role);
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view virtual returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleGranted} event.
     */
    function grantRole(bytes32 role, address account) public virtual onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleRevoked} event.
     */
    function revokeRole(bytes32 role, address account) public virtual onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `callerConfirmation`.
     *
     * May emit a {RoleRevoked} event.
     */
    function renounceRole(bytes32 role, address callerConfirmation) public virtual {
        if (callerConfirmation != _msgSender()) {
            revert AccessControlBadConfirmation();
        }

        _revokeRole(role, callerConfirmation);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Attempts to grant `role` to `account` and returns a boolean indicating if `role` was granted.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleGranted} event.
     */
    function _grantRole(bytes32 role, address account) internal virtual returns (bool) {
        if (!hasRole(role, account)) {
            _roles[role].hasRole[account] = true;
            emit RoleGranted(role, account, _msgSender());
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Attempts to revoke `role` to `account` and returns a boolean indicating if `role` was revoked.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleRevoked} event.
     */
    function _revokeRole(bytes32 role, address account) internal virtual returns (bool) {
        if (hasRole(role, account)) {
            _roles[role].hasRole[account] = false;
            emit RoleRevoked(role, account, _msgSender());
            return true;
        } else {
            return false;
        }
    }
}


// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (access/Ownable.sol)

pragma solidity ^0.8.20;

import {Context} from "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * The initial owner is set to the address provided by the deployer. This can
 * later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    /**
     * @dev The caller account is not authorized to perform an operation.
     */
    error OwnableUnauthorizedAccount(address account);

    /**
     * @dev The owner is not a valid owner account. (eg. `address(0)`)
     */
    error OwnableInvalidOwner(address owner);

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the address provided by the deployer as the initial owner.
     */
    constructor(address initialOwner) {
        if (initialOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(initialOwner);
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        if (owner() != _msgSender()) {
            revert OwnableUnauthorizedAccount(_msgSender());
        }
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        if (newOwner == address(0)) {
            revert OwnableInvalidOwner(address(0));
        }
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.20;

import {IERC20} from "../IERC20.sol";
import {IERC20Permit} from "../extensions/IERC20Permit.sol";
import {Address} from "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    /**
     * @dev An operation with an ERC20 token failed.
     */
    error SafeERC20FailedOperation(address token);

    /**
     * @dev Indicates a failed `decreaseAllowance` request.
     */
    error SafeERC20FailedDecreaseAllowance(address spender, uint256 currentAllowance, uint256 requestedDecrease);

    /**
     * @dev Transfer `value` amount of `token` from the calling contract to `to`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeTransfer(IERC20 token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeCall(token.transfer, (to, value)));
    }

    /**
     * @dev Transfer `value` amount of `token` from `from` to `to`, spending the approval given by `from` to the
     * calling contract. If `token` returns no value, non-reverting calls are assumed to be successful.
     */
    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeCall(token.transferFrom, (from, to, value)));
    }

    /**
     * @dev Increase the calling contract's allowance toward `spender` by `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 oldAllowance = token.allowance(address(this), spender);
        forceApprove(token, spender, oldAllowance + value);
    }

    /**
     * @dev Decrease the calling contract's allowance toward `spender` by `requestedDecrease`. If `token` returns no
     * value, non-reverting calls are assumed to be successful.
     */
    function safeDecreaseAllowance(IERC20 token, address spender, uint256 requestedDecrease) internal {
        unchecked {
            uint256 currentAllowance = token.allowance(address(this), spender);
            if (currentAllowance < requestedDecrease) {
                revert SafeERC20FailedDecreaseAllowance(spender, currentAllowance, requestedDecrease);
            }
            forceApprove(token, spender, currentAllowance - requestedDecrease);
        }
    }

    /**
     * @dev Set the calling contract's allowance toward `spender` to `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful. Meant to be used with tokens that require the approval
     * to be set to zero before setting it to a non-zero value, such as USDT.
     */
    function forceApprove(IERC20 token, address spender, uint256 value) internal {
        bytes memory approvalCall = abi.encodeCall(token.approve, (spender, value));

        if (!_callOptionalReturnBool(token, approvalCall)) {
            _callOptionalReturn(token, abi.encodeCall(token.approve, (spender, 0)));
            _callOptionalReturn(token, approvalCall);
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data);
        if (returndata.length != 0 && !abi.decode(returndata, (bool))) {
            revert SafeERC20FailedOperation(address(token));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     *
     * This is a variant of {_callOptionalReturn} that silents catches all reverts and returns a bool instead.
     */
    function _callOptionalReturnBool(IERC20 token, bytes memory data) private returns (bool) {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We cannot use {Address-functionCall} here since this should return false
        // and not revert is the subcall reverts.

        (bool success, bytes memory returndata) = address(token).call(data);
        return success && (returndata.length == 0 || abi.decode(returndata, (bool))) && address(token).code.length > 0;
    }
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the value of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the value of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves a `value` amount of tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 value) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets a `value` amount of tokens as the allowance of `spender` over the
     * caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 value) external returns (bool);

    /**
     * @dev Moves a `value` amount of tokens from `from` to `to` using the
     * allowance mechanism. `value` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 value) external returns (bool);
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.1) (utils/Context.sol)

pragma solidity ^0.8.20;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    function _contextSuffixLength() internal view virtual returns (uint256) {
        return 0;
    }
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/Address.sol)

pragma solidity ^0.8.20;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev The ETH balance of the account is not enough to perform the operation.
     */
    error AddressInsufficientBalance(address account);

    /**
     * @dev There's no code at `target` (it is not a contract).
     */
    error AddressEmptyCode(address target);

    /**
     * @dev A call to an address target failed. The target may have reverted.
     */
    error FailedInnerCall();

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.8.20/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        if (address(this).balance < amount) {
            revert AddressInsufficientBalance(address(this));
        }

        (bool success, ) = recipient.call{value: amount}("");
        if (!success) {
            revert FailedInnerCall();
        }
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason or custom error, it is bubbled
     * up by this function (like regular Solidity function calls). However, if
     * the call reverted with no returned reason, this function reverts with a
     * {FailedInnerCall} error.
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        if (address(this).balance < value) {
            revert AddressInsufficientBalance(address(this));
        }
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and reverts if the target
     * was not a contract or bubbling up the revert reason (falling back to {FailedInnerCall}) in case of an
     * unsuccessful call.
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata
    ) internal view returns (bytes memory) {
        if (!success) {
            _revert(returndata);
        } else {
            // only check if target is a contract if the call was successful and the return data is empty
            // otherwise we already know that it was a contract
            if (returndata.length == 0 && target.code.length == 0) {
                revert AddressEmptyCode(target);
            }
            return returndata;
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and reverts if it wasn't, either by bubbling the
     * revert reason or with a default {FailedInnerCall} error.
     */
    function verifyCallResult(bool success, bytes memory returndata) internal pure returns (bytes memory) {
        if (!success) {
            _revert(returndata);
        } else {
            return returndata;
        }
    }

    /**
     * @dev Reverts with returndata if present. Otherwise reverts with {FailedInnerCall}.
     */
    function _revert(bytes memory returndata) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert FailedInnerCall();
        }
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Permit.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC20/extensions/IERC20Permit.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 *
 * ==== Security Considerations
 *
 * There are two important considerations concerning the use of `permit`. The first is that a valid permit signature
 * expresses an allowance, and it should not be assumed to convey additional meaning. In particular, it should not be
 * considered as an intention to spend the allowance in any specific way. The second is that because permits have
 * built-in replay protection and can be submitted by anyone, they can be frontrun. A protocol that uses permits should
 * take this into consideration and allow a `permit` call to fail. Combining these two aspects, a pattern that may be
 * generally recommended is:
 *
 * ```solidity
 * function doThingWithPermit(..., uint256 value, uint256 deadline, uint8 v, bytes32 r, bytes32 s) public {
 *     try token.permit(msg.sender, address(this), value, deadline, v, r, s) {} catch {}
 *     doThing(..., value);
 * }
 *
 * function doThing(..., uint256 value) public {
 *     token.safeTransferFrom(msg.sender, address(this), value);
 *     ...
 * }
 * ```
 *
 * Observe that: 1) `msg.sender` is used as the owner, leaving no ambiguity as to the signer intent, and 2) the use of
 * `try/catch` allows the permit to fail and makes the code tolerant to frontrunning. (See also
 * {SafeERC20-safeTransferFrom}).
 *
 * Additionally, note that smart contract wallets (such as Argent or Safe) are not able to produce permit signatures, so
 * contracts should have entry points that don't rely on permit.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     *
     * CAUTION: See Security Considerations above.
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}


// File: @openzeppelin/contracts/utils/introspection/ERC165.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/introspection/ERC165.sol)

pragma solidity ^0.8.20;

import {IERC165} from "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}


// File: @openzeppelin/contracts/access/IAccessControl.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (access/IAccessControl.sol)

pragma solidity ^0.8.20;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev The `account` is missing a role.
     */
    error AccessControlUnauthorizedAccount(address account, bytes32 neededRole);

    /**
     * @dev The caller of a function is not the expected one.
     *
     * NOTE: Don't confuse with {AccessControlUnauthorizedAccount}.
     */
    error AccessControlBadConfirmation();

    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `callerConfirmation`.
     */
    function renounceRole(bytes32 role, address callerConfirmation) external;
}


// File: @openzeppelin/contracts/utils/introspection/IERC165.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/introspection/IERC165.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

