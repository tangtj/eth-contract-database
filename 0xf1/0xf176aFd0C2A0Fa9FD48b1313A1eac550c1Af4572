// SPDX-License-Identifier: MIT

pragma solidity ^0.8.19;

interface IERC20 {

    function name() external view returns (string memory);

    function symbol() external view returns (string memory);

    function decimals() external view returns (uint8);

    function totalSupply() external view returns (uint);

    function balanceOf(address owner) external view returns (uint);

    function allowance(address owner, address spender) external view returns (uint);

    function approve(address spender, uint value) external returns (bool);

    function transfer(address to, uint value) external returns (bool);

    function transferFrom(address from, address to, uint value) external returns (bool);
}

interface IL1MessageQueue {
    /**********
     * Events *
     **********/

    /// @notice Emitted when a new L1 => L2 transaction is appended to the queue.
    /// @param sender The address of account who initiates the transaction.
    /// @param target The address of account who will receive the transaction.
    /// @param value The value passed with the transaction.
    /// @param queueIndex The index of this transaction in the queue.
    /// @param gasLimit Gas limit required to complete the message relay on L2.
    /// @param data The calldata of the transaction.
    event QueueTransaction(
        address indexed sender,
        address indexed target,
        uint256 value,
        uint64 queueIndex,
        uint256 gasLimit,
        bytes data
    );

    /// @notice Emitted when some L1 => L2 transactions are included in L1.
    /// @param startIndex The start index of messages popped.
    /// @param count The number of messages popped.
    /// @param skippedBitmap A bitmap indicates whether a message is skipped.
    event DequeueTransaction(uint256 startIndex, uint256 count, uint256 skippedBitmap);

    /// @notice Emitted when a message is dropped from L1.
    /// @param index The index of message dropped.
    event DropTransaction(uint256 index);

    /// @notice Emitted when owner updates gas oracle contract.
    /// @param _oldGasOracle The address of old gas oracle contract.
    /// @param _newGasOracle The address of new gas oracle contract.
    event UpdateGasOracle(address indexed _oldGasOracle, address indexed _newGasOracle);

    /// @notice Emitted when owner updates max gas limit.
    /// @param _oldMaxGasLimit The old max gas limit.
    /// @param _newMaxGasLimit The new max gas limit.
    event UpdateMaxGasLimit(uint256 _oldMaxGasLimit, uint256 _newMaxGasLimit);

    /**********
     * Errors *
     **********/

    /// @dev Thrown when the given address is `address(0)`.
    error ErrorZeroAddress();

    /*************************
     * Public View Functions *
     *************************/

    /// @notice The start index of all pending inclusion messages.
    function pendingQueueIndex() external view returns (uint256);

    /// @notice Return the index of next appended message.
    /// @dev Also the total number of appended messages.
    function nextCrossDomainMessageIndex() external view returns (uint256);

    /// @notice Return the message of in `queueIndex`.
    /// @param queueIndex The index to query.
    function getCrossDomainMessage(uint256 queueIndex) external view returns (bytes32);

    /// @notice Return the amount of ETH should pay for cross domain message.
    /// @param gasLimit Gas limit required to complete the message relay on L2.
    function estimateCrossDomainMessageFee(uint256 gasLimit) external view returns (uint256);

    /// @notice Return the amount of intrinsic gas fee should pay for cross domain message.
    /// @param _calldata The calldata of L1-initiated transaction.
    function calculateIntrinsicGasFee(bytes calldata _calldata) external view returns (uint256);

    /// @notice Return the hash of a L1 message.
    /// @param sender The address of sender.
    /// @param queueIndex The queue index of this message.
    /// @param value The amount of Ether transfer to target.
    /// @param target The address of target.
    /// @param gasLimit The gas limit provided.
    /// @param data The calldata passed to target address.
    function computeTransactionHash(
        address sender,
        uint256 queueIndex,
        uint256 value,
        address target,
        uint256 gasLimit,
        bytes calldata data
    ) external view returns (bytes32);

    /// @notice Return whether the message is skipped.
    /// @param queueIndex The queue index of the message to check.
    function isMessageSkipped(uint256 queueIndex) external view returns (bool);

    /// @notice Return whether the message is dropped.
    /// @param queueIndex The queue index of the message to check.
    function isMessageDropped(uint256 queueIndex) external view returns (bool);

    /*****************************
     * Public Mutating Functions *
     *****************************/

    /// @notice Append a L1 to L2 message into this contract.
    /// @param target The address of target contract to call in L2.
    /// @param gasLimit The maximum gas should be used for relay this message in L2.
    /// @param data The calldata passed to target contract.
    function appendCrossDomainMessage(
        address target,
        uint256 gasLimit,
        bytes calldata data
    ) external;

    /// @notice Append an enforced transaction to this contract.
    /// @dev The address of sender should be an EOA.
    /// @param sender The address of sender who will initiate this transaction in L2.
    /// @param target The address of target contract to call in L2.
    /// @param value The value passed
    /// @param gasLimit The maximum gas should be used for this transaction in L2.
    /// @param data The calldata passed to target contract.
    function appendEnforcedTransaction(
        address sender,
        address target,
        uint256 value,
        uint256 gasLimit,
        bytes calldata data
    ) external;

    /// @notice Pop finalized messages from queue.
    ///
    /// @dev We can pop at most 256 messages each time. And if the message is not skipped,
    ///      the corresponding entry will be cleared.
    ///
    /// @param startIndex The start index to pop.
    /// @param count The number of messages to pop.
    /// @param skippedBitmap A bitmap indicates whether a message is skipped.
    function popCrossDomainMessage(
        uint256 startIndex,
        uint256 count,
        uint256 skippedBitmap
    ) external;

    /// @notice Drop a skipped message from the queue.
    function dropCrossDomainMessage(uint256 index) external;
}

interface IL1NativeMessenger {

    function sendMessage(
        address target,
        uint256 value,
        bytes calldata message,
        uint256 gasLimit
    ) external payable;

}

interface IL1ERC20Messenger {
    function depositERC20(
        address _token,
        address _to,
        uint256 _amount,
        uint256 _gasLimit
    ) external payable;
}

interface IL2NativeMessenger {
    function sendMessage(
        address target,
        uint256 value,
        bytes calldata message,
        uint256 gasLimit
    ) external payable;
}

interface IL2ERC20Messenger {
    function withdrawERC20(
        address token,
        address to,
        uint256 amount,
        uint256 gasLimit
    ) external payable;
}

interface IWETH {
    function deposit() external payable;
    function transfer(address to, uint value) external returns (bool);
    function withdraw(uint) external;
}

contract FoundationEthereum {

    address public owner;
    address public caller;
    mapping(address=>bool) public whitelist;

    event OnSetOwner(address owner);
    event OnSetCaller(address caller);
    event OnSetWhitelist(address[] callers, bool status);
    event OnSetAllowance(address[] tokens, address spender, uint256 amount);
    event OnRefund(address token, address receiver, uint256 amount);
    event OnWithdraw(address token, address receiver, uint256 amount);
    
    address internal constant WETH_ADDRESS = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;
    address internal constant ZERO_ADDRESS = 0x0000000000000000000000000000000000000000;

    address internal constant ERC20_MESSAGER = 0xF8B1378579659D8F7EE5f3C929c2f3E332E41Fd6;
    address internal constant NATIVE_MESSAGER = 0x6774Bcbd5ceCeF1336b5300fb5186a12DDD8b367;

    constructor() {
        owner = msg.sender;
        caller = msg.sender;
    }

    receive() external payable {}

    // check owner
    modifier onlyOwner() {
        require(msg.sender==owner,"NOT_OWNER");
        _;
    }

    // check caller
    modifier onlyCaller() {
        require(msg.sender==caller,"NOT_CALLER");
        _;
    }

    // wrap eth to weth
    function wrap() external onlyCaller() {
        IWETH(WETH_ADDRESS).deposit{value: address(this).balance }();
    }

    // unwrap weth to eth
    function unwrap(uint256 amount) external onlyCaller() {
        IWETH(WETH_ADDRESS).withdraw(amount);
    }

    // transfer to bridge
    function bridge(address token, address receiver, uint256 amount) external payable onlyCaller() {
        require(whitelist[receiver],"NOT_IN_WHITELIST");
        if (token==ZERO_ADDRESS) {
            IL1NativeMessenger(NATIVE_MESSAGER).sendMessage{value: amount}(receiver, amount, new bytes(0), 168000);
        }else{
            IL1ERC20Messenger(ERC20_MESSAGER).depositERC20{value: msg.value}(token, receiver, amount, 180000);
        }
    }

    // transfer to cex
    function withdraw(address token, address receiver, uint256 amount) external onlyCaller() {
        require(whitelist[receiver],"NOT_IN_WHITELIST");
        if (token == address(0)) {
            require(payable(receiver).send(amount), "FAILED");
        }else{
            if(token==WETH_ADDRESS){
                IWETH(WETH_ADDRESS).withdraw(amount);
                require(payable(receiver).send(amount), "FAILED");
            }else{
                safeTransfer(token, receiver, amount);
            }
        }
        emit OnWithdraw(token, receiver, amount);
    }

    // transfer to owner
    function refund(address token, address receiver, uint256 amount) external onlyOwner {
        if (token == address(0)) {
            require(payable(receiver).send(amount), "FAILED");
        }else{
            if(token==WETH_ADDRESS){
                IWETH(WETH_ADDRESS).withdraw(amount);
                require(payable(receiver).send(amount), "FAILED");
            }else{
                safeTransfer(token, receiver, amount);
            }
        }
        emit OnRefund(token, receiver, amount);
    }

    // set owner
    function setOwner(address user) external onlyOwner {
        owner = user;
        emit OnSetOwner(user);
    }

    // set caller
    function setCaller(address user) external onlyOwner {
        caller = user;
        emit OnSetCaller(user);
    }

    // set witelist
    function setWhitelist(address[] memory callers, bool status) external onlyOwner {
        for (uint8 i=0;i<callers.length;i++) {
            whitelist[callers[i]] = status;
        }
        emit OnSetWhitelist(callers, status);
    }

    // set allowance
    function setAllowance(address[] memory tokens, address spender, uint256 amount) external onlyOwner() {
        for (uint8 i=0;i<tokens.length;i++) {
            safeApprove(tokens[i],spender,amount);
        }
        emit OnSetAllowance(tokens, spender, amount);
    }
    
    // safe approve
    function safeApprove(address token, address spender, uint value) internal {
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x095ea7b3, spender, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'APPROVE_FAILED');
    }
    
    // safe transfer
    function safeTransfer(address token, address receiver, uint value) internal {
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, receiver, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TRANSFER_FAILED');
    }
}