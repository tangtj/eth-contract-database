
// File: contracts/ActusEssendi.sol
// SPDX-License-Identifier: UNLICENSED

pragma solidity 0.8.21;

import {Proxy} from "@openzeppelin/contracts/proxy/Proxy.sol";
import {ERC721Baseline} from "erc721baseline/contracts/ERC721Baseline.sol";
import {IERC721Baseline} from "erc721baseline/contracts/IERC721Baseline.sol";
import {Base64} from "solady/src/utils/Base64.sol";

/**
 * @title Actus Essendi
 * @author 0xG
 *
 * @notice
 * Actus Essendi draws from Aquinas' metaphysical concept that
 * everything has an essence or nature, but existence (actus essendi)
 * is what gives an entity its individuality and reality.
 *
 * 0xG relates this theory to generative art algorithms,
 * which symbolize the essence and inherent nature of the artwork,
 * while their static output, an image, embodies the reality and
 * individuality of Actus Essendi, capturing a momentary expression of the essence.
 *
 * This concept is formalized in this contract as tokens that start with
 * a unique on-chain generative artwork (the essence) which transforms at every resolution.
 *
 * Using the signature on the back of the corresponding physical piece
 * the collector can permanently transitions the token to Actus Essendi.
 *
 * After execution, the token visual reflects the physical piece the collector owns,
 * serving as a permanent expression of the essence at `tokenId` resolution.
 */
contract ActusEssendi is ERC721Baseline {

  /**
   * @notice Returns the address of the creator, 0xG, who signed the artworks.
   *
   * @return The creator address.
   */
  address immutable public creator = msg.sender;

  /**
   * @notice Returns the current state of the token.
   * @dev Available states are:
   *
   * 0. Unminted
   * 1. Essence
   * 2. Actus Essendi
   * 3. Burnt
   *
   * @return state The token state
   */
  mapping(uint256 tokenId => uint8 state) public tokenState;

  /**
   * @notice The artwork description.
   */
  string public description;

  event ActusEssendi(uint256 _tokenId);
  event MetadataUpdate(uint256 _tokenId);
  event Description(string description);

  error ERC721NonexistentToken(uint256 tokenId);

  constructor(address ERC721BaselineImplementation)
    ERC721Baseline(
      ERC721BaselineImplementation,
      "Actus Essendi",
      "ACTESSND"
    )
  { }

  /**
   * @notice Returns the generative on-chain artwork (the essence).
   * Every new token in the collection starts from the essence.
   *
   * @return SVG image encoded as a base64 string
   */
  function essence() external pure returns (string memory) {
    return "data:image/svg+xml;base64,PHN2ZyB4bWxucz0iaHR0cDovL3d3dy53My5vcmcvMjAwMC9zdmciIHN0eWxlPSJiYWNrZ3JvdW5kOiNmNWY1ZjUiIHZpZXdCb3g9IjAgMCAxMDAwIDEwMDAiPjxkZWZzPjxmaWx0ZXIgaWQ9ImEiPjxmZVR1cmJ1bGVuY2UgYmFzZUZyZXF1ZW5jeT0iMC4wMDAxIDEiIG51bU9jdGF2ZXM9IjMiIHNlZWQ9IjcwMSIvPjxmZU1vcnBob2xvZ3kgb3BlcmF0b3I9ImRpbGF0ZSIgcmFkaXVzPSI0LjUiLz48ZmVEaXNwbGFjZW1lbnRNYXAgaW49IlNvdXJjZUdyYXBoaWMiIHNjYWxlPSIxNTAwIiB4Q2hhbm5lbFNlbGVjdG9yPSJHIiB5Q2hhbm5lbFNlbGVjdG9yPSJCIi8+PC9maWx0ZXI+PC9kZWZzPjxyZWN0IHdpZHRoPSIxMDAwIiBoZWlnaHQ9IjEwMDAiIGZpbGw9IiNmNWY1ZjUiLz48cmVjdCB3aWR0aD0iNTAwIiBoZWlnaHQ9IjUwMCIgeD0iMjUwIiB5PSIyNTAiIGZpbGw9IiNlMGUwZTAiLz48cmVjdCB3aWR0aD0iNTAwIiBoZWlnaHQ9IjUwMCIgeD0iMjUwIiB5PSIyNTAiIGZpbGw9IiNmNWY1ZjUiIHN0cm9rZT0iIzYwNjA2MCIgc3Ryb2tlLXdpZHRoPSI0MCIgZmlsdGVyPSJ1cmwoI2EpIi8+PHJlY3Qgd2lkdGg9IjIwMCIgaGVpZ2h0PSI1MDAiIHg9IjU4MCIgeT0iMjUwIiBmaWxsPSJub25lIiBzdHJva2U9IiNmZmYiIHN0cm9rZS1kYXNoYXJyYXk9IjIiIHN0cm9rZS13aWR0aD0iNDAiIGZpbHRlcj0idXJsKCNhKSIvPjwvc3ZnPg==";
  }

  /**
   * @notice Permanently transitions the token to Actus Essendi.
   * After execution, the token visual reflects the physical piece the collector owns,
   * serving as a permanent expression of the essence at `tokenId` resolution.
   *
   * @param tokenId The ID of the token to perform Actus Essendi on
   * @param signature The signature on the back of the physical piece counterpart
   */
  function actusEssendi(uint256 tokenId, bytes calldata signature) external {
    if (
      tokenState[tokenId] != 1 ||
      creator != baseline().recoverCalldata(
        keccak256(abi.encodePacked(address(this), tokenId)),
        signature
      ) ||
      msg.sender != baseline().__ownerOf(tokenId)
    ) revert IERC721Baseline.Unauthorized();

    tokenState[tokenId] = 2;

    emit ActusEssendi(tokenId);
    emit MetadataUpdate(tokenId);
  }

  /**
   * @notice Retrieves the token URI for a given tokenId.
   *
   * @param tokenId The ID of the token
   * @return The token URI
   */
  function tokenURI(uint256 tokenId) external view returns (string memory) {
    uint8 state = tokenState[tokenId];
    if (state == 0 || state == 3) revert ERC721NonexistentToken(tokenId);

    return string.concat(
      'data:application/json;base64,',
      Base64.encode(
        abi.encodePacked(
          '{"name":"',(state == 2 ?'Actus Essendi' : 'Essence'),' #',baseline().toString(tokenId),'",',
          '"created_by":"0xG","description":"',description,'",',
          '"image":"',(state == 2 ? baseline().__tokenURI(tokenId) : this.essence()),'"}'
        ),
        true
      )
    );
  }

  /**
   * @notice Allows the collector to update the Actus Essendi URI for a given tokenId.
   * This method exists only as an escape hatch to update the URI in case of issues.
   *
   * @param tokenId The ID of the token
   * @param uri The new URI for Actus Essendi (provided by the creator)
   * @param signature The signature for authorization (provided by the creator)
   */
  function updateActusEssendiUri(uint256 tokenId, string calldata uri, bytes calldata signature) external {
    if (
      tokenState[tokenId] != 2 ||
      false == _isAuthorized(
        keccak256(abi.encodePacked(address(this), tokenId, uri)),
        signature
      ) ||
      msg.sender != baseline().__ownerOf(tokenId)
    ) revert IERC721Baseline.Unauthorized();

    baseline().__setTokenURI(tokenId, uri);
  }

  /**
   * @notice Allows a collector to redeem a specific tokenId.
   *
   * @param tokenId The ID of the token to redeem
   * @param uri The URI for the token (provided by the creator)
   * @param signature The signature for authorization (provided by the creator)
   */
  function mint(uint256 tokenId, string calldata uri, bytes calldata signature) external {
    if (
      tokenState[tokenId] > 0 ||
      false == _isAuthorized(
        keccak256(
          abi.encodePacked(
            msg.sender,
            address(this),
            tokenId,
            uri
          )
        ),
        signature
      )
    ) revert IERC721Baseline.Unauthorized();

    tokenState[tokenId] = 1;
    baseline().__mint(msg.sender, tokenId, uri);
  }

  /**
   * @notice Burns a token with the given tokenId.
   *
   * @param tokenId The ID of the token to burn
   */
  function burn(uint256 tokenId) external {
    if (
      msg.sender != baseline().__ownerOf(tokenId)
    ) revert IERC721Baseline.Unauthorized();

    tokenState[tokenId] = 3;
    baseline().__burn(tokenId);
  }

  /**
   * @notice Sets the artwork description.
   *
   * @param desc The new description
   * @param store Whether the description should be persisted on storage
   * @param emitEvent Whether calling the function should emit a Description event with the description
   */
  function setDescription(string calldata desc, bool store, bool emitEvent) external {
    baseline().requireAdmin(msg.sender);
    if (store) {
      description = desc;
    }
    if (emitEvent) {
      emit Description(desc);
    }
  }

  function _isAuthorized(bytes32 digest, bytes calldata signature) internal returns (bool) {
    IERC721Baseline impl = baseline();
    return impl.isAdmin(impl.recoverCalldata(digest, signature));
  }
}


// File: solady/src/utils/Base64.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

/// @notice Library to encode strings in Base64.
/// @author Solady (https://github.com/vectorized/solady/blob/main/src/utils/Base64.sol)
/// @author Modified from Solmate (https://github.com/transmissions11/solmate/blob/main/src/utils/Base64.sol)
/// @author Modified from (https://github.com/Brechtpd/base64/blob/main/base64.sol) by Brecht Devos - <brecht@loopring.org>.
library Base64 {
    /// @dev Encodes `data` using the base64 encoding described in RFC 4648.
    /// See: https://datatracker.ietf.org/doc/html/rfc4648
    /// @param fileSafe  Whether to replace '+' with '-' and '/' with '_'.
    /// @param noPadding Whether to strip away the padding.
    function encode(bytes memory data, bool fileSafe, bool noPadding)
        internal
        pure
        returns (string memory result)
    {
        /// @solidity memory-safe-assembly
        assembly {
            let dataLength := mload(data)

            if dataLength {
                // Multiply by 4/3 rounded up.
                // The `shl(2, ...)` is equivalent to multiplying by 4.
                let encodedLength := shl(2, div(add(dataLength, 2), 3))

                // Set `result` to point to the start of the free memory.
                result := mload(0x40)

                // Store the table into the scratch space.
                // Offsetted by -1 byte so that the `mload` will load the character.
                // We will rewrite the free memory pointer at `0x40` later with
                // the allocated size.
                // The magic constant 0x0670 will turn "-_" into "+/".
                mstore(0x1f, "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdef")
                mstore(0x3f, xor("ghijklmnopqrstuvwxyz0123456789-_", mul(iszero(fileSafe), 0x0670)))

                // Skip the first slot, which stores the length.
                let ptr := add(result, 0x20)
                let end := add(ptr, encodedLength)

                // Run over the input, 3 bytes at a time.
                for {} 1 {} {
                    data := add(data, 3) // Advance 3 bytes.
                    let input := mload(data)

                    // Write 4 bytes. Optimized for fewer stack operations.
                    mstore8(0, mload(and(shr(18, input), 0x3F)))
                    mstore8(1, mload(and(shr(12, input), 0x3F)))
                    mstore8(2, mload(and(shr(6, input), 0x3F)))
                    mstore8(3, mload(and(input, 0x3F)))
                    mstore(ptr, mload(0x00))

                    ptr := add(ptr, 4) // Advance 4 bytes.
                    if iszero(lt(ptr, end)) { break }
                }
                mstore(0x40, add(end, 0x20)) // Allocate the memory.
                // Equivalent to `o = [0, 2, 1][dataLength % 3]`.
                let o := div(2, mod(dataLength, 3))
                // Offset `ptr` and pad with '='. We can simply write over the end.
                mstore(sub(ptr, o), shl(240, 0x3d3d))
                // Set `o` to zero if there is padding.
                o := mul(iszero(iszero(noPadding)), o)
                mstore(sub(ptr, o), 0) // Zeroize the slot after the string.
                mstore(result, sub(encodedLength, o)) // Store the length.
            }
        }
    }

    /// @dev Encodes `data` using the base64 encoding described in RFC 4648.
    /// Equivalent to `encode(data, false, false)`.
    function encode(bytes memory data) internal pure returns (string memory result) {
        result = encode(data, false, false);
    }

    /// @dev Encodes `data` using the base64 encoding described in RFC 4648.
    /// Equivalent to `encode(data, fileSafe, false)`.
    function encode(bytes memory data, bool fileSafe)
        internal
        pure
        returns (string memory result)
    {
        result = encode(data, fileSafe, false);
    }

    /// @dev Decodes base64 encoded `data`.
    ///
    /// Supports:
    /// - RFC 4648 (both standard and file-safe mode).
    /// - RFC 3501 (63: ',').
    ///
    /// Does not support:
    /// - Line breaks.
    ///
    /// Note: For performance reasons,
    /// this function will NOT revert on invalid `data` inputs.
    /// Outputs for invalid inputs will simply be undefined behaviour.
    /// It is the user's responsibility to ensure that the `data`
    /// is a valid base64 encoded string.
    function decode(string memory data) internal pure returns (bytes memory result) {
        /// @solidity memory-safe-assembly
        assembly {
            let dataLength := mload(data)

            if dataLength {
                let decodedLength := mul(shr(2, dataLength), 3)

                for {} 1 {} {
                    // If padded.
                    if iszero(and(dataLength, 3)) {
                        let t := xor(mload(add(data, dataLength)), 0x3d3d)
                        // forgefmt: disable-next-item
                        decodedLength := sub(
                            decodedLength,
                            add(iszero(byte(30, t)), iszero(byte(31, t)))
                        )
                        break
                    }
                    // If non-padded.
                    decodedLength := add(decodedLength, sub(and(dataLength, 3), 1))
                    break
                }
                result := mload(0x40)

                // Write the length of the bytes.
                mstore(result, decodedLength)

                // Skip the first slot, which stores the length.
                let ptr := add(result, 0x20)
                let end := add(ptr, decodedLength)

                // Load the table into the scratch space.
                // Constants are optimized for smaller bytecode with zero gas overhead.
                // `m` also doubles as the mask of the upper 6 bits.
                let m := 0xfc000000fc00686c7074787c8084888c9094989ca0a4a8acb0b4b8bcc0c4c8cc
                mstore(0x5b, m)
                mstore(0x3b, 0x04080c1014181c2024282c3034383c4044484c5054585c6064)
                mstore(0x1a, 0xf8fcf800fcd0d4d8dce0e4e8ecf0f4)

                for {} 1 {} {
                    // Read 4 bytes.
                    data := add(data, 4)
                    let input := mload(data)

                    // Write 3 bytes.
                    // forgefmt: disable-next-item
                    mstore(ptr, or(
                        and(m, mload(byte(28, input))),
                        shr(6, or(
                            and(m, mload(byte(29, input))),
                            shr(6, or(
                                and(m, mload(byte(30, input))),
                                shr(6, mload(byte(31, input)))
                            ))
                        ))
                    ))
                    ptr := add(ptr, 3)
                    if iszero(lt(ptr, end)) { break }
                }
                mstore(0x40, add(end, 0x20)) // Allocate the memory.
                mstore(end, 0) // Zeroize the slot after the bytes.
                mstore(0x60, 0) // Restore the zero slot.
            }
        }
    }
}


// File: erc721baseline/contracts/IERC721Baseline.sol
// SPDX-License-Identifier: AGPL-3.0-or-later

pragma solidity 0.8.21;

import {IERC721} from "@openzeppelin/contracts/token/ERC721/IERC721.sol";

/**
 * @title IERC721Baseline
 * @custom:version v0.1.0-alpha.4
 * @notice A baseline ERC721 contract implementation that exposes internal methods to a proxy instance.
 */
interface IERC721Baseline is IERC721 {

  /**
   * @dev The version of the implementation contract.
   */
  function VERSION() external view returns (string memory);

  /**
   * @dev Indicates an unauthorized operation or unauthorized access attempt.
   */
  error Unauthorized();


  /************************************************
   * Initializer
   ************************************************/

  /**
   * @notice Initializes a proxy contract.
   * @dev This method MUST be called in the proxy constructor via delegatecall
   * to initialize the proxy with a name and symbol for the underlying ERC721.
   *
   * Additionally this method sets the deployer as owner and admin of the proxy.
   *
   * @param name contract name
   * @param symbol contract symbol
   */
  function initialize(string memory name, string memory symbol) external;


  /************************************************
   * Metadata
   ************************************************/

  /**
   * Metadata > ERC-4906 events
   */

  /**
   * @dev This event emits when the metadata of a token is changed.
   * So that the third-party platforms such as NFT market could
   * timely update the images and related attributes of the NFT.
   *
   * @param _tokenId the token ID being updated
   */
  event MetadataUpdate(uint256 _tokenId);

  /**
   * @dev This event emits when the metadata of a range of tokens is changed.
   * So that the third-party platforms such as NFT market could
   * timely update the images and related attributes of the NFTs.
   *
   * @param _fromTokenId the starting token ID
   * @param _toTokenId the ending token ID
   */
  event BatchMetadataUpdate(uint256 _fromTokenId, uint256 _toTokenId);

  /**
   * @notice The total minted supply.
   * @dev The supply is decreased when a token is burned.
   * Generally it is recommended to use a separate counter variable to track the supply available for minting.
   */
  function totalSupply() external view returns (uint256);

  /**
   * Token URI methods.
   *
   * The ERC721Baseline tokenURI implementation allows to define uris in the following order:
   *
   * 1. Token-specific URI by ID.
   * 2. Shared URI.
   * 3. Shared base URI + token ID.
   * 4. Empty string if none of the above was found.
   */

  /**
   * @notice Returns the token URI for a token ID.
   *
   * @param tokenId token ID
   */
  function __tokenURI(uint256 tokenId) external view returns (string memory);

  /**
   * @notice Sets the token URI for a token ID.
   * @dev Emits EIP-4906's `MetadataUpdate` event with the `tokenId`.
   * This method is internal and only the proxy contract can call it.
   *
   *
   * @param tokenId token ID
   * @param tokenURI URI pointing to the metadata
   */
  function __setTokenURI(uint256 tokenId, string calldata tokenURI) external;

  /**
   * @notice Returns the shared URI for the tokens.
   * @dev This method is internal and only the proxy contract can call it.
   */
  function __sharedURI() external view returns (string memory);

  /**
   * @notice Sets a shared URI for the tokens.
   * @dev This method doesn't emit the EIP-4906's `BatchMetadataUpdate` event
   * because ERC721Baseline allows to mint any token ID, starting at any index.
   * The proxy should emit `BatchMetadataUpdate`.
   *
   * This method is internal and only the proxy contract can call it.
   *
   * @param sharedURI shared URI for the tokens
   */
  function __setSharedURI(string calldata sharedURI) external;

  /**
   * @notice Returns the base URI for the tokens.
   * @dev When set this URI is prepended to the token ID.
   */
  function __baseURI() external view returns (string memory);

  /**
   * @notice Sets a contract-wide base URI.
   * @dev This method doesn't emit the EIP-4906 `BatchMetadataUpdate` event
   * because ERC721Baseline allows to mint any token ID, starting at any index.
   * The proxy should emit `BatchMetadataUpdate`.
   *
   * This method is internal and only the proxy contract can call it.
   *
   * @param baseURI shared base URI for the tokens
   */
  function __setBaseURI(string calldata baseURI) external;


  /************************************************
   * Internal ERC721 methods exposed to the proxy
   ************************************************/

  /**
   * @dev Indicates an invalid attempt to call a method from outside of the proxy.
   */
  error NotProxy();

  /**
   * @dev See {ERC721-_ownerOf}.
   */
  function __ownerOf(uint256 tokenId) external returns (address);

  /**
   * @dev See {ERC721-_update}.
   * This method is internal and only the proxy contract can call it.
   */
  function __update(address to, uint256 tokenId, address auth) external returns (address);

  /**
   * @dev See {ERC721-_mint}.
   * This method is internal and only the proxy contract can call it.
   */
  function __mint(address to, uint256 tokenId) external;

  /**
   * @dev Similar to {ERC721-_mint} but allows to set a dedicated tokenURI for the token.
   * This method is internal and only the proxy contract can call it.
   */
  function __mint(address to, uint256 tokenId, string calldata tokenURI) external;

  /**
   * @dev See {ERC721-_burn}.
   * This method is internal and only the proxy contract can call it.
   */
  function __burn(uint256 tokenId) external;

  /**
   * @dev See {ERC721-_transfer}.
   * This method is internal and only the proxy contract can call it.
   */
  function __transfer(address from, address to, uint256 tokenId) external;

  /**
   * @notice Allows to enable or disable a `_beforeTokenTransfer` hook method defined in the proxy contract.
   * @dev This method is internal and only the proxy contract can call it.
   *
   * When enabled, the proxy's `_beforeTokenTransfer` hook method is invoked prior to a transfer.
   *
   * The proxy's `_beforeTokenTransfer` method is called with the following params:
   *
   * - address the transaction's _msgSender()
   * - address from
   * - address to
   * - uint256 tokenId
   *
   * This method is internal and only the proxy contract can call it.
   */
  function __setBeforeTokenTransferHookEnabled(bool enabled) external;

  /**
   * @dev See {ERC721-_checkOnERC721Received}.
   *
   * NOTE: this method accepts an additional first parameter that is the original transaction's `msg.sender`.
   */
  function __checkOnERC721Received(address sender, address from, address to, uint256 tokenId, bytes memory data) external;

  /**
   * @dev See {ERC721-_isAuthorized}.
   */
  function __isAuthorized(address owner, address spender, uint256 tokenId) external view returns (bool);

  /**
   * @dev See {ERC721-_approve}.
   * This method is internal and only the proxy contract can call it.
   */
  function __approve(address to, uint256 tokenId, address auth, bool emitEvent) external;

  /**
   * @dev See {ERC721-_setApprovalForAll}.
   * This method is internal and only the proxy contract can call it.
   */
  function __setApprovalForAll(address owner, address operator, bool approved) external;


  /************************************************
   * Access control
   ************************************************/

  /**
   * Implements a multi-admin system and a minimal Ownable-compatible API.
   */

  /**
   * Access control > multi-admin system
   */

  /**
   * @notice Checks if an address is the contract owner or an admin.
   *
   * @param addr address to check
   * @return bool whether the address is an admin or not
   */
  function isAdmin(address addr) external view returns (bool);

  /**
   * @dev Emits when an admin is added or removed.
   *
   * @param addr address that is being added or removed as an admin
   * @param add boolean indicating whether the address was grented or revoked admin rights
   */
  event AdminSet(address indexed addr, bool indexed add);

  /**
   * @notice Allows to add or remove an admin.
   * Can only be called by an admin.
   *
   * @param addr address to add or remove
   * @param add boolean indicating whether the address should be granted or revoked rights
   */
  function setAdmin(address addr, bool add) external;

  /**
   * @notice Checks whether an address is an admin and reverts with an `Unauthorized` error if not.
   * @dev Call `requireAdmin` in proxies to implement admin-only public methods.
   *
   * @param addr the address to check
   */
  function requireAdmin(address addr) external view;

  /**
   * @notice Allows to add or remove an admin.
   * @dev This method is internal and only the proxy contract can call it.
   *
   * @param addr address to add or remove
   * @param add boolean indicating whether the address should be granted or revoked rights
   */
  function __setAdmin(address addr, bool add) external;

  /**
   * Access control > Ownable-compatible API.
   */

  /**
   * @notice Returns the address of the contract owner.
   *
   * @return address of the contract owner
   */
  function owner() external view returns (address);

  /**
   * @dev Emits when the contract ownership is transferred.
   *
   * @param previousOwner old owner address
   * @param newOwner new owner address
   */
  event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

  /**
   * @notice Transfers ownership of the contract to a new account.
   * Can only be called by an admin.
   *
   * @param newOwner new owner address
   */
  function transferOwnership(address newOwner) external;

  /**
   * @notice Transfers ownership of the contract to a new account.
   * @dev This method is internal and only the proxy contract can call it.
   *
   * @param newOwner new owner address
   */
  function __transferOwnership(address newOwner) external;


  /************************************************
   * Utils
   ************************************************/

  /**
   * @dev Indicates an invalid signature.
   */
  error InvalidSignature();

  /**
   * @notice Recovers the signer's address from a message digest `hash`, and the `signature`.
   *
   * @param hash the message digest that was signed
   * @param signature the signature for hash
   * @return result address the recovered address
   */
  function recover(bytes32 hash, bytes memory signature) external view returns (address result);

  /**
   * @notice Recovers the signer's address from a message digest `hash`, and the `signature`.
   * @dev In this method the signature comes from calldata.
   *
   * @param hash the message digest that was signed
   * @param signature the signature for hash
   * @return result address the recovered address
   */
  function recoverCalldata(bytes32 hash, bytes calldata signature) external view returns (address result);

  /**
   * @notice Converts a uint256 to string
   *
   * @param value the uint256 to convert
   * @return string ASCII string decimal representation of `value`
   */
  function toString(uint256 value) external pure returns (string memory);

}


// File: erc721baseline/contracts/ERC721Baseline.sol
// SPDX-License-Identifier: MIT

pragma solidity 0.8.21;

import {Proxy} from "@openzeppelin/contracts/proxy/Proxy.sol";
import {IERC721Baseline} from "./IERC721Baseline.sol";

/**
 * @title ERC721Baseline
 * @custom:version v0.1.0
 * @notice A minimal proxy contract for ERC721BaselineImplementation.
 *
 * @dev ERC721BaselineImplementation uses ERC-7201 (Namespaced Storage Layout)
 * to prevent collisions with the proxies storage.
 * See https://eips.ethereum.org/EIPS/eip-7201.
 *
 * Proxies are encouraged, but not required, to use a similar pattern for storage.
 */
contract ERC721Baseline is Proxy {

  struct ImplementationSlot {
    address value;
  }

  /**
   * @dev Storage slot with the address of the current implementation.
   * This is the keccak-256 hash of "eip1967.proxy.implementation" subtracted by 1.
   */
  bytes32 internal constant _IMPLEMENTATION_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;

  constructor(
    address ERC721BaselineImplementation,
    string memory name,
    string memory symbol
  ) {
    ImplementationSlot storage implementation;
    assembly {
      implementation.slot := _IMPLEMENTATION_SLOT
    }
    implementation.value = ERC721BaselineImplementation;

    (bool success, bytes memory reason) = ERC721BaselineImplementation.delegatecall(
      abi.encodeCall(IERC721Baseline.initialize, (name, symbol))
    );

    if (success == false) {
      if (reason.length == 0) revert("Initialization Failed.");
      assembly {
        revert(add(32, reason), mload(reason))
      }
    }
  }

  function _implementation() internal view override returns (address) {
    ImplementationSlot storage implementation;
    assembly {
      implementation.slot := _IMPLEMENTATION_SLOT
    }
    return implementation.value;
  }

  function implementation() external view returns (address) {
    return _implementation();
  }

  /**
   * @notice Returns a reference to the ERC721BaselineImplementation contract.
   */
  function baseline() internal view returns (IERC721Baseline) {
    return IERC721Baseline(address(this));
  }
}


// File: @openzeppelin/contracts/proxy/Proxy.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (proxy/Proxy.sol)

pragma solidity ^0.8.20;

/**
 * @dev This abstract contract provides a fallback function that delegates all calls to another contract using the EVM
 * instruction `delegatecall`. We refer to the second contract as the _implementation_ behind the proxy, and it has to
 * be specified by overriding the virtual {_implementation} function.
 *
 * Additionally, delegation to the implementation can be triggered manually through the {_fallback} function, or to a
 * different contract through the {_delegate} function.
 *
 * The success and return data of the delegated call will be returned back to the caller of the proxy.
 */
abstract contract Proxy {
    /**
     * @dev Delegates the current call to `implementation`.
     *
     * This function does not return to its internal call site, it will return directly to the external caller.
     */
    function _delegate(address implementation) internal virtual {
        assembly {
            // Copy msg.data. We take full control of memory in this inline assembly
            // block because it will not return to Solidity code. We overwrite the
            // Solidity scratch pad at memory position 0.
            calldatacopy(0, 0, calldatasize())

            // Call the implementation.
            // out and outsize are 0 because we don't know the size yet.
            let result := delegatecall(gas(), implementation, 0, calldatasize(), 0, 0)

            // Copy the returned data.
            returndatacopy(0, 0, returndatasize())

            switch result
            // delegatecall returns 0 on error.
            case 0 {
                revert(0, returndatasize())
            }
            default {
                return(0, returndatasize())
            }
        }
    }

    /**
     * @dev This is a virtual function that should be overridden so it returns the address to which the fallback
     * function and {_fallback} should delegate.
     */
    function _implementation() internal view virtual returns (address);

    /**
     * @dev Delegates the current call to the address returned by `_implementation()`.
     *
     * This function does not return to its internal call site, it will return directly to the external caller.
     */
    function _fallback() internal virtual {
        _delegate(_implementation());
    }

    /**
     * @dev Fallback function that delegates calls to the address returned by `_implementation()`. Will run if no other
     * function in the contract matches the call data.
     */
    fallback() external payable virtual {
        _fallback();
    }
}


// File: @openzeppelin/contracts/token/ERC721/IERC721.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (token/ERC721/IERC721.sol)

pragma solidity ^0.8.20;

import {IERC165} from "../../utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon
     *   a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or
     *   {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon
     *   a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Note that the caller is responsible to confirm that the recipient is capable of receiving ERC721
     * or else they may be permanently lost. Usage of {safeTransferFrom} prevents loss, though the caller must
     * understand this adds an external call which potentially creates a reentrancy vulnerability.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the address zero.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);
}


// File: @openzeppelin/contracts/utils/introspection/IERC165.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v5.0.0) (utils/introspection/IERC165.sol)

pragma solidity ^0.8.20;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

