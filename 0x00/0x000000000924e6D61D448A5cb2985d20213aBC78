
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/security/Pausable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}


// File: @openzeppelin/contracts/security/ReentrancyGuard.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        _nonReentrantBefore();
        _;
        _nonReentrantAfter();
    }

    function _nonReentrantBefore() private {
        // On the first call to nonReentrant, _status will be _NOT_ENTERED
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;
    }

    function _nonReentrantAfter() private {
        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Returns true if the reentrancy guard is currently set to "entered", which indicates there is a
     * `nonReentrant` function in the call stack.
     */
    function _reentrancyGuardEntered() internal view returns (bool) {
        return _status == _ENTERED;
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: @openzeppelin/contracts/utils/math/SafeMath.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (utils/math/SafeMath.sol)

pragma solidity ^0.8.0;

// CAUTION
// This version of SafeMath should only be used with Solidity 0.8 or later,
// because it relies on the compiler's built in overflow checks.

/**
 * @dev Wrappers over Solidity's arithmetic operations.
 *
 * NOTE: `SafeMath` is generally not needed starting with Solidity 0.8, since the compiler
 * now has built in overflow checking.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            uint256 c = a + b;
            if (c < a) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b > a) return (false, 0);
            return (true, a - b);
        }
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
            // benefit is lost if 'b' is also tested.
            // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
            if (a == 0) return (true, 0);
            uint256 c = a * b;
            if (c / a != b) return (false, 0);
            return (true, c);
        }
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a / b);
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        unchecked {
            if (b == 0) return (false, 0);
            return (true, a % b);
        }
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        return a + b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        return a * b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator.
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        unchecked {
            require(b <= a, errorMessage);
            return a - b;
        }
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a / b;
        }
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        unchecked {
            require(b > 0, errorMessage);
            return a % b;
        }
    }
}


// File: contracts/LancetMembership.sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.9;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "@openzeppelin/contracts/utils/math/SafeMath.sol";

interface IERC721 {
    function ownerOf(uint256 tokenId) external view returns (address);
}

interface ILancetStaking{
    function ownedToken(address owner) external view returns(uint256);
}

contract LancetMembership is Ownable,ReentrancyGuard{
    using SafeMath for uint256;

    // =========================================================================
    //                               Types
    // =========================================================================
    struct MembershipType{
        uint256 membershipDays;
        bool    onSale;
        uint256 membershipPrice;
    }

    // =========================================================================
    //                               Storage
    // =========================================================================
    IERC721 public lancetPass;
    ILancetStaking public lancetStaking1;
    ILancetStaking public lancetStaking2;

    uint8 public holderDiscount;
    bool public holderMembershipPurchaseEnable;
    bool public userMembershipPurchaseEnable;

    uint256 private constant oneDay = 1 days;

    mapping(uint256 => uint256) public passMembershipEndAt;
    mapping(address => uint256) public userMembershipEndAt;

    uint256[] public membershipCategories;
    mapping(uint256 => MembershipType) public membershipState;

    // =========================================================================
    //                            Constructor
    // =========================================================================
    constructor(IERC721 _lancetPass,ILancetStaking _lancetStaking1,ILancetStaking _lancetStaking2){
        membershipState[30] = MembershipType(30,true,0.1 ether);
        membershipCategories.push(30);
        holderDiscount = 69;
        passMembershipEndAt[514] = 1696861859;
        lancetPass = _lancetPass;
        lancetStaking1 = _lancetStaking1;
        lancetStaking2 = _lancetStaking2;
    }

    // =========================================================================
    //                               Event
    // =========================================================================
    event MembershipPurchase(address indexed owner,uint256 indexed startTimestamp,uint256 indexed endTimestamp,uint256 tokenId);
    event UserMembershipPurchase(address indexed owner,uint256 indexed startTimestamp,uint256 indexed endTimestamp);
    
    // =========================================================================
    //                               Modifier
    // =========================================================================
    modifier onlyPassOwner(uint256 tokenId){
        require(tokenId != 0);
        require(lancetPass.ownerOf(tokenId) == msg.sender || lancetStaking1.ownedToken(msg.sender) == tokenId || lancetStaking2.ownedToken(msg.sender) == tokenId,"Not Lancet Pass Holder");
        _;
    }

    // =========================================================================
    //                               Function
    // =========================================================================
    function holderMembershipPurchase(uint256 purchaseDays,uint256 tokenId) onlyPassOwner(tokenId) nonReentrant payable external{
        require(holderMembershipPurchaseEnable,"Not Enable");
        require(membershipState[purchaseDays].onSale,"Not On Sale");
        require(msg.value == membershipState[purchaseDays].membershipPrice.mul(holderDiscount).div(1e2),"Invalid Price");
        uint256 passMmebershipstartTimestamp = block.timestamp;
        uint256 passMembershipEndTimestamp;
        if (passMembershipEndAt[tokenId] > passMmebershipstartTimestamp){
            passMembershipEndTimestamp = passMembershipEndAt[tokenId].add(oneDay.mul(purchaseDays));
        }else{
            passMembershipEndTimestamp = passMmebershipstartTimestamp.add(oneDay.mul(purchaseDays));
        }        
        passMembershipEndAt[tokenId] = passMembershipEndTimestamp;
        emit MembershipPurchase(msg.sender,passMmebershipstartTimestamp,passMembershipEndTimestamp,tokenId);
    }

    function userMembershipPurchase(uint256 purchaseDays) nonReentrant payable external{
        require(userMembershipPurchaseEnable,"Not Enable");
        require(membershipState[purchaseDays].onSale,"Not On Sale");
        require(msg.value == membershipState[purchaseDays].membershipPrice,"Invalid Price");
        uint256 userMembershipStartTimestamp = block.timestamp;
        uint256 userMembershipEndTimestamp;
        if (userMembershipEndAt[msg.sender] > userMembershipStartTimestamp){
            userMembershipEndTimestamp = userMembershipEndAt[msg.sender].add(oneDay.mul(purchaseDays));
        }else{
            userMembershipEndTimestamp = userMembershipStartTimestamp.add(oneDay.mul(purchaseDays));
        }   
        userMembershipEndAt[msg.sender] = userMembershipEndTimestamp;
        emit UserMembershipPurchase(msg.sender,userMembershipStartTimestamp,userMembershipEndTimestamp);
    }

    function getAllMembershipTypes() external view returns(MembershipType[] memory){
        MembershipType[] memory membershipTypes = new MembershipType[](membershipCategories.length);
        for (uint256 i = 0;i < membershipCategories.length ;i ++){
            membershipTypes[i] = membershipState[membershipCategories[i]];
        }
        return membershipTypes;
    }

    function setHolderDiscount(uint8 discount) external onlyOwner{
        holderDiscount = discount;
    }

    function setHolderMembershipPurchaseEnable() external onlyOwner{
        holderMembershipPurchaseEnable = !holderMembershipPurchaseEnable;
    }

    function setUserMembershipPurchaseEnable() external onlyOwner{
        userMembershipPurchaseEnable = !userMembershipPurchaseEnable;
    }

    function addMembership(uint256 purchaseDays,uint256 price,bool onSale) external onlyOwner{
        for (uint256 i = 0; i < membershipCategories.length ; i ++){
            if (membershipCategories[i] == purchaseDays){
                revert("Purchase Days Already Exist");
            }
        }
        membershipState[purchaseDays] = MembershipType(purchaseDays,onSale,price);
        membershipCategories.push(purchaseDays);
    }

    function updateMembership(uint256 purchaseDays,uint256 price,bool onSale) external onlyOwner{
        bool purchaseDaysExist;
        for (uint256 i = 0; i < membershipCategories.length ; i ++){
            if (membershipCategories[i] == purchaseDays){
                purchaseDaysExist = true;
                break;
            }
        }
        require(purchaseDaysExist,"Purchase Days Not Exist");
        membershipState[purchaseDays] = MembershipType(purchaseDays,onSale,price);
    }

    function removeMembership(uint256 purchaseDays) external onlyOwner{
        for (uint256 i = 0; i < membershipCategories.length; i++){
            if (membershipCategories[i] == purchaseDays){
                membershipCategories[i] = membershipCategories[membershipCategories.length - 1];
                membershipCategories.pop();
                break;
            }
        }
        membershipState[purchaseDays] = MembershipType(0,false,0);
    }

    function setLancetPass(IERC721 _lancetPass) onlyOwner external {
        lancetPass = _lancetPass;
    }

    function setLancetStaking1(ILancetStaking _lancetStaking1) onlyOwner external{
        lancetStaking1 = _lancetStaking1;
    }

    function setLancetStaking2(ILancetStaking _lancetStaking2) onlyOwner external{
        lancetStaking2 = _lancetStaking2;
    }

    function withdraw() external onlyOwner nonReentrant {
        (bool success, ) = msg.sender.call{value: (address(this).balance)}("");
        require(success, "Withdraw: Transaction Unsuccessful.");
    }
}
