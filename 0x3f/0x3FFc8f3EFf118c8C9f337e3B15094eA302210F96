
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/ERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.openzeppelin.com/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * The default value of {decimals} is 18. To change this, you should override
 * this function so it returns a different value.
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the default value returned by this function, unless
     * it's overridden.
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on
     * `transferFrom`. This is semantically equivalent to an infinite approval.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * NOTE: Does not update the allowance if the current allowance
     * is the maximum `uint256`.
     *
     * Requirements:
     *
     * - `from` and `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     * - the caller must have allowance for ``from``'s tokens of at least
     * `amount`.
     */
    function transferFrom(address from, address to, uint256 amount) public virtual override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, allowance(owner, spender) + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        address owner = _msgSender();
        uint256 currentAllowance = allowance(owner, spender);
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `from` to `to`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     */
    function _transfer(address from, address to, uint256 amount) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
            // Overflow not possible: the sum of all balances is capped by totalSupply, and the sum is preserved by
            // decrementing then incrementing.
            _balances[to] += amount;
        }

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        unchecked {
            // Overflow not possible: balance + amount is at most totalSupply + amount, which is checked above.
            _balances[account] += amount;
        }
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
            // Overflow not possible: amount <= accountBalance <= totalSupply.
            _totalSupply -= amount;
        }

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(address owner, address spender, uint256 amount) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Updates `owner` s allowance for `spender` based on spent `amount`.
     *
     * Does not update the allowance amount in case of infinite allowance.
     * Revert if not enough allowance is available.
     *
     * Might emit an {Approval} event.
     */
    function _spendAllowance(address owner, address spender, uint256 amount) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            require(currentAllowance >= amount, "ERC20: insufficient allowance");
            unchecked {
                _approve(owner, spender, currentAllowance - amount);
            }
        }
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual {}
}


// File: @openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/extensions/ERC20Burnable.sol)

pragma solidity ^0.8.0;

import "../ERC20.sol";
import "../../../utils/Context.sol";

/**
 * @dev Extension of {ERC20} that allows token holders to destroy both their own
 * tokens and those that they have an allowance for, in a way that can be
 * recognized off-chain (via event analysis).
 */
abstract contract ERC20Burnable is Context, ERC20 {
    /**
     * @dev Destroys `amount` tokens from the caller.
     *
     * See {ERC20-_burn}.
     */
    function burn(uint256 amount) public virtual {
        _burn(_msgSender(), amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, deducting from the caller's
     * allowance.
     *
     * See {ERC20-_burn} and {ERC20-allowance}.
     *
     * Requirements:
     *
     * - the caller must have allowance for ``accounts``'s tokens of at least
     * `amount`.
     */
    function burnFrom(address account, uint256 amount) public virtual {
        _spendAllowance(account, _msgSender(), amount);
        _burn(account, amount);
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Permit.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/extensions/IERC20Permit.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../extensions/IERC20Permit.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    /**
     * @dev Transfer `value` amount of `token` from the calling contract to `to`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeTransfer(IERC20 token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    /**
     * @dev Transfer `value` amount of `token` from `from` to `to`, spending the approval given by `from` to the
     * calling contract. If `token` returns no value, non-reverting calls are assumed to be successful.
     */
    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(IERC20 token, address spender, uint256 value) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    /**
     * @dev Increase the calling contract's allowance toward `spender` by `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 oldAllowance = token.allowance(address(this), spender);
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, oldAllowance + value));
    }

    /**
     * @dev Decrease the calling contract's allowance toward `spender` by `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, oldAllowance - value));
        }
    }

    /**
     * @dev Set the calling contract's allowance toward `spender` to `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful. Compatible with tokens that require the approval to be set to
     * 0 before setting it to a non-zero value.
     */
    function forceApprove(IERC20 token, address spender, uint256 value) internal {
        bytes memory approvalCall = abi.encodeWithSelector(token.approve.selector, spender, value);

        if (!_callOptionalReturnBool(token, approvalCall)) {
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, 0));
            _callOptionalReturn(token, approvalCall);
        }
    }

    /**
     * @dev Use a ERC-2612 signature to set the `owner` approval toward `spender` on `token`.
     * Revert on invalid signature.
     */
    function safePermit(
        IERC20Permit token,
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal {
        uint256 nonceBefore = token.nonces(owner);
        token.permit(owner, spender, value, deadline, v, r, s);
        uint256 nonceAfter = token.nonces(owner);
        require(nonceAfter == nonceBefore + 1, "SafeERC20: permit did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        require(returndata.length == 0 || abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     *
     * This is a variant of {_callOptionalReturn} that silents catches all reverts and returns a bool instead.
     */
    function _callOptionalReturnBool(IERC20 token, bytes memory data) private returns (bool) {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We cannot use {Address-functionCall} here since this should return false
        // and not revert is the subcall reverts.

        (bool success, bytes memory returndata) = address(token).call(data);
        return
            success && (returndata.length == 0 || abi.decode(returndata, (bool))) && Address.isContract(address(token));
    }
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     *
     * Furthermore, `isContract` will also return true if the target contract within
     * the same transaction is already scheduled for destruction by `SELFDESTRUCT`,
     * which only has an effect at the end of a transaction.
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.8.0/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: contracts/AuctionV2.sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.18;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";

/**
    @title Auction Contract Version 2

    @notice This contract allows for the creation and bidding on auction listings
    in a decentralized manner.

    @dev Users can submit auction listings on our platform and then fund them via this smart
    contract by paying the stake and setting the paramaters needed.

    The listingID is unique and generated off-chain on the platform side. It is used to identify
    the listing and what is being sold. We hash it with the address of the poster to prevent front-
    running.

    Once an auction listing is created on chain (by funding it), users can bid on it.

    The auction listing can be closed by approvers (designated by the contract owner).

    When the auction has expired, or been closed due to a buyout bid being placed, the seller (or
    approvers on behalf of the owner) can claim the funds from the auction. If this is done during
    the cooldown period, after the auction has ended, a fee is applied.

    The winning bid is the highest bid when the auction expires or the first bid to reach the
    buyout price.

    There is also the option for the seller to not put up the listing as an action but rather just
    allow a single buyout price.

    Be aware: this contract has privileged roles OWNER and APPROVER, whose abilities and
    responsibilities should be made clear in user-facing documentation.
*/

contract AuctionV2 is Ownable {
    address private immutable _arkm;

    uint256 private _listingStake;
    uint256 private _makerFee;
    uint256 private _takerFee;
    uint256 private _withdrawEarlyFee;
    uint256 private _minimumStep;
    bool    private _acceptingListings;
    uint256 private _accruedFees;
    address private _feeReceiverAddress;
    uint    private _listingDuration;
    uint    private _cooldown;
    uint256 private _minimumBuyoutPrice;
    uint32  private _minimumDuration;

    // Basis points are 1/100th of a percent. 10000 = 100%.
    uint64  private constant _MAX_BPS = 10000;

    /// @dev Payloads waiting for approval
    mapping(bytes32 => Listing[]) private _attemptedListings;

    /// @dev mapping of Listing payload => Listing
    mapping(bytes32 => Listing) private _approvedListings;

    /// @dev list of listing payloads
    bytes32[] private _listingPayloads;

    /// @dev mapping of Listing payload => Bid
    mapping(bytes32 => Bid) private _bids;

    /// @dev Approver address => is approver
    mapping(address => bool) private _approvers;

    /// @dev Listing ID => used, prevent posting same listing twice
    mapping(uint256 => bool) private _usedListingIDs;

    /// @notice Struct representing a listing
    /// @dev the struct represent a sales listing. Depending on the isAuction flag,
    /// the listing will be an auction or a buyout listing. Auctions may have a buyout price
    /// determined if the buyoutPrice is > 0.
    /// Closed listings cannot be bid on. A listing is closed when the auction is over (timed out or buyout),
    /// use the appropriate getter to find the status rather than relying on the closed flag to take auction
    /// end-time into account.
    /// A stake is payed to create a listingPayload, if the listing is not rejected by an approver the stake is refunded,
    /// when the listing is claimed.
    struct Listing {
        address poster;
        uint256 stake;
        uint64  expiresAt;
        bool    isAuction;
        uint256 buyoutPrice;
        uint256 startingPrice;
        bool    approved;
        bool    closed;
        bool    withdrawn;
    }

    /// @notice Struct representing a bid (or buyout) on a listing
    /// @dev the amount is the total amount of ARKM sent (before taker, maker and withdraw fees).
    /// The bid itself is the amount minus the taker fee.
    /// The bidID represents the bid and is is generated off-chain.
    struct Bid {
        address bidder;
        uint256 bidPlusTakerFee;
        uint256 bidID;
    }

    /// @notice Composite view of listing returned by the external listingState function
    struct ListingState {
        uint256 closesAt;
        uint256 currentBidID;
        uint256 currentBidAmount;
        bool    closedOrExpired;
        bool    approved;
        uint256 buyoutPrice;
        bool    withdrawn;
        uint256 minimumStartingBid;
        bool    isAuction;
    }

    /// @notice Contract constructor
    /// @param arkmAddress The address of the ARKM token
    /// @param initialListingStake The stake required to create a listing
    /// @param initialMakerFee The maker fee (fee paid by the seller when the auction is claimed), in basis points
    /// @param initialTakerFee The initial taker fee (fee paid by the buyer when the auction is claimed), in basis points
    /// @param initialWithdrawEarlyFee The initial fee paid by the seller if the auction is claimed before cool down period has ended, in basis points
    /// @param initialMinimumStep The initial minimum step (minimum amount a bid must be higher than the previous bid in basis point)
    /// @param initialMinimumBuyoutPrice The initial minimum buyout price (minimum amount a buyout price may be set to in a listing)
    /// @param listingDuration The initial listing duration (how long an auction will run for)
    /// @param initialFeeReceiverAddress The initial fee receiver address (where the fees are sent to)
    /// @param initialCooldown The initial cooldown period (how long after an auction has ended before the seller can claim the funds)
    /// @param minimumDuration The minimum duration for a listing (how long an auction can run for)
    constructor(address arkmAddress, uint256 initialListingStake, uint256 initialMakerFee, uint256 initialTakerFee, uint256 initialWithdrawEarlyFee, uint256 initialMinimumStep, uint256 initialMinimumBuyoutPrice, uint listingDuration, address initialFeeReceiverAddress, uint initialCooldown, uint32 minimumDuration) {
        require(initialMakerFee <= _MAX_BPS, "AuctionV2: maker fee must be <= 10000");

        require(initialTakerFee <= _MAX_BPS, "AuctionV2: taker fee must be <= 10000");

        require(initialFeeReceiverAddress != address(0), "AuctionV2: fee receiver address cannot be 0x0");

        require(listingDuration <= 36500*86400, "AuctionV2: listing duration must be <= 36500 days in seconds");

        require(initialWithdrawEarlyFee <= _MAX_BPS, "AuctionV2: withdraw early fee must be <= 100%");

        require(initialMinimumStep > 0, "AuctionV2: minimum step must be > 0");
        require(initialMinimumStep <= _MAX_BPS, "AuctionV2: minimum step must be <= 100%");

        require(initialCooldown <= 365*86400, "AuctionV2: cooldown must be <= 365 days in seconds");

        require(initialListingStake > 0, "AuctionV2: listing stake must be > 0");
        require(initialListingStake <= 10000 * 1e18, "AuctionV2: listing stake must be <= 10000 TOKEN");

        require(minimumDuration > 0, "AuctionV2: minimum duration must be > 0");
        require(minimumDuration <= 36500*86400, "AuctionV2: minimum duration must be <= 36500 days in seconds");

        try ERC20Burnable(arkmAddress).burn(0) {
            _arkm = arkmAddress;
        } catch {
            revert("AuctionV2: provided token address does not implement ERC20Burnable");
        }

        _listingStake = initialListingStake;
        _makerFee = initialMakerFee;
        _takerFee = initialTakerFee;
        _minimumStep = initialMinimumStep;
        _withdrawEarlyFee = initialWithdrawEarlyFee;
        _acceptingListings = true;
        _feeReceiverAddress = initialFeeReceiverAddress;
        _listingDuration = listingDuration;
        _minimumBuyoutPrice = initialMinimumBuyoutPrice;
        _cooldown = initialCooldown;
        _minimumDuration = minimumDuration;
    }

    /// @notice Funds a listing
    /// @param listingPayload A hash of listingID x funder address
    /// @param buyout The buyout price (if a buyer pays this or higher the auction immediatly ends and the bidder wins)
    /// @param startingPrice The starting price for the auction
    /// @param durationInSeconds The duration of the auction in seconds (if 0 will default to the contract default 30 days unless changed by the owner)
    /// @param isAuction Whether the listing is an auction or a buyout-only listing
    /// @dev Will emit an event ListingFunded if successful. Note that staking working properly
    /// requrires having matching listingIDs in the contract and on our platform. ListingIDs are
    /// currently UUIDs on the platform. The same payload can be staked multiple times, but only
    /// the first submission that correctly hashes poster address with listingID will be approved.
    function stakeListing(bytes32 listingPayload, uint256 buyout, uint256 startingPrice, uint256 durationInSeconds, bool isAuction) external {
        require(_acceptingListings, "AuctionV2: not accepting listings");
        require(_approvedListings[listingPayload].poster == address(0), "AuctionV2: listing already exists");
        require(buyout > 0 || isAuction, "AuctionV2: must have a buyout price or be an auction");
        require(buyout == 0 || buyout >= _minimumBuyoutPrice, "AuctionV2: must have a buyout price larger than the minimum buyout price");
        require(startingPrice <= buyout || buyout == 0, "AuctionV2: starting price must be lower than or equal to the buyout price");
        require(durationInSeconds >= _minimumDuration, "AuctionV2: duration must be >= minimum duration");

        // Transfer the listing stake to this contract
        SafeERC20.safeTransferFrom(IERC20(_arkm), _msgSender(), address(this), _listingStake);

        Listing memory _thisListing = Listing({
            poster: _msgSender(),
            stake: _listingStake,
            expiresAt: uint64(block.timestamp + (durationInSeconds > 0 ? durationInSeconds * 1 seconds : _listingDuration * 1 seconds)),
            isAuction: isAuction,
            buyoutPrice: buyout,
            startingPrice: startingPrice,
            approved: false,
            closed: false,
            withdrawn: false
        });

        // If it's the first time we're seeing this payload, add it to the list of payloads.
        if (_attemptedListings[listingPayload].length == 0) {
            _listingPayloads.push(listingPayload);
        }

        // Add the listing to the list of attempted listings for this payload.
        _attemptedListings[listingPayload].push(_thisListing);

        emit ListingFunded(
            listingPayload,
            _msgSender(),
            isAuction,
            startingPrice,
            buyout
        );
    }

    /// @notice Makes a bid on a listing.
    /// @param listingPayload the payload corresponding to the listing ID to bid on
    /// @param amount the total amount payed to make the bid (before fees)
    /// @param bidID an ID generated off-chain to identify the bid
    /// @dev Will emit an event BidPlaced if successful. Auctions can be extended indefinitely by
    /// repeated bids within the last 30 minutes of the auction as long as there is no buyout price
    /// or the bids remain below the buyout price.
    function placeBid(bytes32 listingPayload, uint256 amount, uint256 bidID) external {
        require(_approvedListings[listingPayload].poster != address(0), "AuctionV2: listing does not exist");
        require(_approvedListings[listingPayload].closed == false, "AuctionV2: listing is closed");
        require(_approvedListings[listingPayload].expiresAt > block.timestamp, "AuctionV2: listing has expired");
        require(_approvedListings[listingPayload].startingPrice <= amount, "AuctionV2: bid must be at least starting price");

        if (!_approvedListings[listingPayload].isAuction) {
            // Only accept bids that are equal to or higher than the buyout price if the listing
            // is not an auction.
            require (amount >= _approvedListings[listingPayload].buyoutPrice, "AuctionV2: not accepting non-buyout bids");
        } else {
            // If listing is an auction check that the bid is higher than or equal to the minimum
            // step increase OR is higher than the buyout price. A buyout price of 0 means there
            // is no buyout price.
            require(amount >= afterTakerFee(_bids[listingPayload].bidPlusTakerFee) + minimumStep(listingPayload) || (_approvedListings[listingPayload].buyoutPrice > 0 && amount >= _approvedListings[listingPayload].buyoutPrice), "AuctionV2: bid must be higher by the minimum step increase");
        }

        address _previousBidder = _bids[listingPayload].bidder;
        uint256 _previousBid = _bids[listingPayload].bidPlusTakerFee;

        // If a bid arrives within the last 30 minutes before the listing expires, extend the listing by 30 minutes.
        if (_approvedListings[listingPayload].expiresAt <= block.timestamp + 30 minutes) {
            _approvedListings[listingPayload].expiresAt = uint64(block.timestamp + 30 minutes);
        }

        uint256 _effectiveAmount = amount;

        // If it's above the buyout, end the auction
        bool buyout = false;
        if (_approvedListings[listingPayload].buyoutPrice != 0 && amount >= _approvedListings[listingPayload].buyoutPrice) {
            // If this is a successful buyout, cap the bid at the buyout price.
            _effectiveAmount = _approvedListings[listingPayload].buyoutPrice;

            _approvedListings[listingPayload].closed = true;
            buyout = true;
            _approvedListings[listingPayload].expiresAt = uint64(block.timestamp);
        }

        uint256 _amountPlusTakerFee = addTakerFee(_effectiveAmount);

        // Store the bid.
        _bids[listingPayload] = Bid({
            bidder: _msgSender(),
            bidPlusTakerFee: _amountPlusTakerFee,
            bidID: bidID
        });

        // Pay the last bid back to bidder.
        if (_previousBidder != address(0)) {
            SafeERC20.safeTransfer(IERC20(_arkm), _previousBidder, _previousBid);
        }

        // Get the new bid.
        SafeERC20.safeTransferFrom(IERC20(_arkm), _msgSender(), address(this), _amountPlusTakerFee);

        emit BidMade(
            listingPayload,
            _msgSender(),
            bidID,
            _effectiveAmount,
            buyout
        );
    }

    /// @notice Claim the tokens for the seller from a listing
    /// @param listingPayload the listing to claim
    /// @param withdrawEarly if set to true it will allow the seller to withdraw early (before the cooldown period has expired but after the listing has closed)
    /// this comes with a fee.
    /// @dev Will emit an event ListingClaimed if successful.
    /// Approvers can claim the listing early on behalf of the seller at no cost. Make sure this
    /// ends up in front-facing documentation.
    function claim(bytes32 listingPayload, bool withdrawEarly) external {
        require(!_approvedListings[listingPayload].withdrawn, "AuctionV2: has already been withdrawn");
        require(_approvedListings[listingPayload].poster != address(0), "AuctionV2: listing does not exist");
        require(closedOrExpired(listingPayload), "AuctionV2: listing is not closed");

        _approvedListings[listingPayload].withdrawn = true;
        _approvedListings[listingPayload].closed = true;

        if (withdrawEarly && cooldownExpires(listingPayload) > block.timestamp && _msgSender() == _approvedListings[listingPayload].poster) {
            // Pay the seller amount and their stake - maker, taker and withdraw early fee.
            uint256 fees = withdrawEarlyFee(_bids[listingPayload].bidPlusTakerFee) + makerFee(_bids[listingPayload].bidPlusTakerFee) + takerFee(_bids[listingPayload].bidPlusTakerFee);
            _accruedFees += fees;

            SafeERC20.safeTransfer(IERC20(_arkm), _approvedListings[listingPayload].poster, _bids[listingPayload].bidPlusTakerFee - fees + _approvedListings[listingPayload].stake);
        } else {
            require(cooldownExpires(listingPayload) <= block.timestamp || isApprover(_msgSender()), "AuctionV2: can not withdraw before cooldown period expires");
            // Pay the seller amount and their stake - maker and taker fee. Waive the withdraw
            // early fee.
            uint256 fees = makerFee(_bids[listingPayload].bidPlusTakerFee) + takerFee(_bids[listingPayload].bidPlusTakerFee);
            _accruedFees += fees;

            SafeERC20.safeTransfer(IERC20(_arkm), _approvedListings[listingPayload].poster, _bids[listingPayload].bidPlusTakerFee - fees + _approvedListings[listingPayload].stake);
        }

        emit ListingClaimed(listingPayload);
    }

    /// @notice Approve a listing, allowing it to be bid on
    /// @param listingPayload the listing to approve
    /// @param listingID the listing ID, to check to see if the submitter knew it when they posted
    /// @param rejectInvalid whether to reject the rest of the submissions for this payload
    /// @dev We do this when we confirm that the listing is as it claims.
    function approveListing(bytes32 listingPayload, uint256 listingID, bool rejectInvalid) external {
        require(isApprover(_msgSender()), "AuctionV2: approving requires approver");
        require(_attemptedListings[listingPayload].length > 0, "AuctionV2: listing does not exist");
        require(_approvedListings[listingPayload].poster == address(0), "AuctionV2: listing already approved");

        require(_approvedListings[listingPayload].closed == false, "AuctionV2: listing is closed");

        // Iterate through the attempted listings to find a match.
        for (uint32 i = 0; i < _attemptedListings[listingPayload].length; i++) {
            bytes32 _hash = keccak256(abi.encodePacked(listingID, _attemptedListings[listingPayload][i].poster));
            if (_hash != listingPayload || _usedListingIDs[listingID]) {
                if (rejectInvalid) {
                    _accruedFees += _attemptedListings[listingPayload][i].stake;
                    _attemptedListings[listingPayload][i].stake = 0;
                    _attemptedListings[listingPayload][i].closed = true;
                }
            } else {
                _attemptedListings[listingPayload][i].approved = true;
                _approvedListings[listingPayload] = _attemptedListings[listingPayload][i];
                _attemptedListings[listingPayload][i].stake = 0;
                _usedListingIDs[listingID] = true;

                emit ListingApproved(listingPayload);

                if (!rejectInvalid) {
                    return;
                }
            }
        }
    }

    /// @notice Clears pending listings for a given payload
    /// @param listingPayload the listing to clear
    /// @param maxToReject the maximum number of listings to reject
    /// @dev This should in practice never be used, but it is here in case someone tries to spam
    /// many submissions for the same listing. We can accept, and then in a separate tx reject
    /// a handful at a time.
    function rejectPending(bytes32 listingPayload, uint32 maxToReject) external {
        require(isApprover(_msgSender()), "AuctionV2: rejecting requires approver");

        uint32 _rejected = 0;
        for (uint32 i = 0; i < _attemptedListings[listingPayload].length; i++) {
            if (_attemptedListings[listingPayload][i].approved == true || _attemptedListings[listingPayload][i].closed == true) {
                continue;
            }

            if (_rejected >= maxToReject) {
                break;
            }

            _accruedFees += _attemptedListings[listingPayload][i].stake;
            _attemptedListings[listingPayload][i].stake = 0;
            _attemptedListings[listingPayload][i].closed = true;
            _rejected++;
        }

        emit RejectedPending(listingPayload, _rejected);
    }

    /// @notice Rejects a listingPayload, forfeiting the stake.
    /// @param listingPayload the listing to reject
    /// @param listingID the listing ID, to check to see if the submitter knew it when they posted
    /// @dev Will emit an event ListingClosed if successful.
    /// Will return the bid to the bidder if the listing already has bids.
    /// This is a centralized means of rejecting listings, giving strict power to approvers. Make
    /// sure users are aware that this is placing trust in the designated approvers to not abuse
    /// their position.
    function rejectListing(bytes32 listingPayload, uint256 listingID) public {
        require(isApprover(_msgSender()), "AuctionV2: closing requires approver");
        require(_approvedListings[listingPayload].closed == false, "AuctionV2: listing is closed");

        _approvedListings[listingPayload].closed = true;

        // if there's a current bid return it to the bidder.
        if (_bids[listingPayload].bidder != address(0)) {
            SafeERC20.safeTransfer(IERC20(_arkm), _bids[listingPayload].bidder, _bids[listingPayload].bidPlusTakerFee);
        }

        // mark the listingID as used so it can't be used again.
        _usedListingIDs[listingID] = true;

        // Forfeit the stake.
        _accruedFees += _approvedListings[listingPayload].stake;
        _approvedListings[listingPayload].withdrawn = true;
        _approvedListings[listingPayload].approved = false;

        emit ListingClosed(listingPayload);
    }

    /// @notice Grants an address approver status
    /// @param approver the address to grant approver status to
    /// @dev Will emit an event GrantApprover if successful
    function grantApprover(address approver) external onlyOwner {
        _approvers[approver] = true;
        emit GrantApprover(approver);
    }
    /// @notice Revokes an address approver status
    /// @param approver the address to revoke approver status from
    /// @dev Will emit an event RevokeApprover if successful
    function revokeApprover(address approver) external onlyOwner {
        _approvers[approver] = false;
        emit RevokeApprover(approver);
    }

    /// @notice makes the contract stop accepting new listings
    /// @dev Will emit an event StopAcceptingListings if successful
    function stopAcceptingListings() external onlyOwner {
        _acceptingListings = false;
        emit StopAcceptingListings();
    }

    /// @param listingID The ID of the listing to check
    /// @return The payload of the listing, if it is found
    /// @dev This is used to check if a listingID is funded. Check each existing listing record, and
    /// see if we can hash the listingID and one of the funders addresses to produce the payload.
    function findListingByID(uint256 listingID) public view virtual returns (bytes32) {
        bytes32 _payload;
        for (uint32 i = 0; i < _listingPayloads.length; i++) {
            for (uint32 j = 0; j < _attemptedListings[_listingPayloads[i]].length; j++) {
                _payload = keccak256(abi.encodePacked(listingID, _attemptedListings[_listingPayloads[i]][j].poster));
                if (_payload == _listingPayloads[i]) {
                    return _payload;
                }
            }
        }

        revert("AuctionV2: listingID not found");
    }

    // Helpers.

    //// @notice returns the winning bidder's address for a given listing
    /// @param listingPayload the listing to get the winning bidder for
    /// @dev will revert if the auction has not yet been won
    function winningBidder(bytes32 listingPayload) external view returns (address) {
        require(_approvedListings[listingPayload].poster != address(0), "AuctionV2: listing does not exist");
        require(closedOrExpired(listingPayload), "AuctionV2: listing is not closed");
        require(_bids[listingPayload].bidder != address(0), "AuctionV2: listing has not been bid on");

        return _bids[listingPayload].bidder;
    }

    /// @notice returns the winning BidID for a given listing
    /// @param listingPayload the listing to get the winning bidID for
    /// @dev will revert if the auction has not yet been won
    function winningBidID(bytes32 listingPayload) external view returns (uint256) {
        require(_approvedListings[listingPayload].poster != address(0), "AuctionV2: listing does not exist");
        require(closedOrExpired(listingPayload), "AuctionV2: listing is not closed");
        require(_bids[listingPayload].bidID > 0, "AuctionV2: listing has not been bid on");

        return _bids[listingPayload].bidID;
    }

    /// @notice returns whether a listing is approved or not
    /// @param listingPayload the listing to check if approved
    function listingApproved(bytes32 listingPayload) public view returns (bool) {
        return _approvedListings[listingPayload].approved;
    }

    /// @notice returns the minimum increase in bid needed to make a bid for a given listing
    /// @dev The 10000 accounts for denomination in basis points. Note that this is a percentage
    /// and not a fixed amount -- in the future we may want to change it to a fixed amount,
    /// depending on how the market behaves in practice.
    function minimumStep(bytes32 listingPayload) internal view returns (uint256)  {
        return afterTakerFee(_bids[listingPayload].bidPlusTakerFee) * _minimumStep / 10000;
    }

    /// @notice returns the bid given the amount that was sent in the bid
    /// by removing the taker fee from the total sent amount.
    function afterTakerFee(uint256 amount) internal view returns (uint256) {
        return amount - takerFee(amount);
    }

    /// @notice adds the taker fee to an amount
    /// @param amount the amount to add the taker fee to
    function addTakerFee(uint256 amount) internal view returns (uint256) {
        return amount * (10000 + _takerFee) / 10000;
    }

    /// @notice returns the taker fee for a given amount
    /// @param amount the amount to calculate the taker fee for
    function takerFee(uint256 amount) internal view returns (uint256) {
        return amount * _takerFee / (10000 + _takerFee);
    }

    /// @notice the maker fee for a given amount
    /// @param amount the amount to calculate the maker fee for
    /// @dev The 10000 accounts for denomination in basis points.
    /// The maker fee given the total amount payed by the bidder.
    /// The payout to the seller is the `amount payed by bidder - maker fee - taker fee - <withdraw early fee>`
    function makerFee(uint256 amount) internal view returns (uint256) {
        return (amount - takerFee(amount)) * _makerFee / 10000;
    }

    /// @notice the withdraw early fee for a given amount
    /// @param amount the amount to calculate the withdraw early fee for
    /// @dev The 10000 accounts for denomination in basis points.
    /// The withdraw early fee given the total amount payed by the bidder.
    /// The payout to the seller if withdrawn early is
    /// `amount payed by bidder - maker fee - taker fee - withdraw early fee`
    function withdrawEarlyFee(uint256 amount) internal view returns (uint256) {
        return (amount - makerFee(amount) - takerFee(amount)) * _withdrawEarlyFee / 10000;
    }
    /// @notice what timestamp does cool down period for this listing expire
    /// @param listingPayload the listing to get the cooldown expiresAt for
    function cooldownExpires(bytes32 listingPayload) public view returns (uint256) {
        return _cooldown + _approvedListings[listingPayload].expiresAt;
    }

    // Getters.
    function minimumStepBasis() external view returns (uint256) {
        return _minimumStep;
    }

    function cooldown() external view returns (uint) {
        return _cooldown;
    }

    function minimumBuyoutPrice() external view returns (uint256) {
        return _minimumBuyoutPrice;
    }

    function feeReceiverAddress() external view returns (address) {
        return _feeReceiverAddress;
    }

    function getListingStake(bytes32 listingPayload) external view returns (uint256) {
        return _approvedListings[listingPayload].stake;
    }

    function isApprover(address approver) public view returns (bool) {
        return _approvers[approver];
    }

    function arkm() external view returns (address) {
        return _arkm;
    }

    function listingStake() external view returns (uint256) {
        return _listingStake;
    }

    function makerFee() external view returns (uint256) {
        return _makerFee;
    }

    function takerFee() external view returns (uint256) {
        return _takerFee;
    }

    function withdrawEarlyFee() external view returns (uint256) {
        return _withdrawEarlyFee;
    }

    function listingDurationDays() external view returns (uint) {
        return _listingDuration;
    }

    function acceptingListings() external view returns (bool) {
        return _acceptingListings;
    }

    function accruedFees() external view returns (uint256) {
        return _accruedFees;
    }
    /// @notice return whether the listing is closed or not
    /// @param listingPayload the listing to check if closed
    /// @dev will revert if the listing does not exist
    function closedOrExpired(bytes32 listingPayload) public view returns (bool) {
        require(_approvedListings[listingPayload].poster != address(0), "AuctionV2: listing does not exist");
        return _approvedListings[listingPayload].closed || _approvedListings[listingPayload].expiresAt <= block.timestamp;
    }

    function closesAt(bytes32 listingPayload) public view returns (uint256) {
        return _approvedListings[listingPayload].expiresAt;
    }

    function currentBidID(bytes32 listingPayload) public view returns (uint256) {
        return _bids[listingPayload].bidID;
    }

    function currentBidAmount(bytes32 listingPayload) public view returns (uint256) {
        return afterTakerFee(_bids[listingPayload].bidPlusTakerFee);
    }

    function withdrawn(bytes32 listingPayload) public view returns (bool) {
        return _approvedListings[listingPayload].withdrawn;
    }

    function buyoutPrice(bytes32 listingPayload) public view returns (uint256) {
        return _approvedListings[listingPayload].buyoutPrice;
    }

    function listingIsAuction(bytes32 listingPayload) public view returns (bool) {
        return _approvedListings[listingPayload].isAuction;
    }

    function listingStartingPrice(bytes32 listingPayload) public view returns (uint256) {
        return _approvedListings[listingPayload].startingPrice;
    }

    /// @notice Returns the listing state for a given listing
    /// @param listingPayload the listing to get the state for
    /// @dev This is used by our off-chain backend to get the state of a listing. It is a simple
    /// composite of existing getters.
    function listingState(bytes32 listingPayload) external view returns (ListingState memory) {
        return ListingState({
            closesAt: closesAt(listingPayload),
            currentBidID: currentBidID(listingPayload),
            currentBidAmount: afterTakerFee(_bids[listingPayload].bidPlusTakerFee),
            closedOrExpired: closedOrExpired(listingPayload),
            approved: listingApproved(listingPayload),
            buyoutPrice: buyoutPrice(listingPayload),
            withdrawn: withdrawn(listingPayload),
            minimumStartingBid: listingStartingPrice(listingPayload),
            isAuction: listingIsAuction(listingPayload)
        });
    }

    // Setters.

    /// @notice Sets a new maker fee
    /// @param newFee The new maker fee, in basis points
    function setMakerFee(uint256 newFee) external onlyOwner {
        require(newFee <= _MAX_BPS, "AuctionV2: maker fee must be <= 100%");
        uint256 _oldFee = _makerFee;
        _makerFee = newFee;

        emit SetMakerFee(
            newFee,
            _oldFee
        );
    }

    /// @notice Sets a new cooldown time
    /// @param newCooldown The new cooldown time, in seconds
    function setCooldown(uint newCooldown) external onlyOwner {
        require(newCooldown <= 365*86400, "AuctionV2: cooldown must be <= 365 days in seconds");

        uint _oldCooldown = _cooldown;
        _cooldown = newCooldown;

        emit SetCooldown(
            newCooldown,
            _oldCooldown
        );
    }

    /// @notice Sets new default listing duration
    /// @param newDuration The new default listing duration, in seconds
    function setDefaultListingDuration(uint256 newDuration) external onlyOwner {
        uint256 _oldDuration = _listingDuration;
        _listingDuration = newDuration;

        emit SetDefaultListingDuration(
            newDuration,
            _oldDuration
        );
    }

    /// @notice Sets a new minimum price increase for subsequent bids
    /// @param newStep The new minimum price for subsequent bids, in basis points
    /// @dev Emits an event with the new minimum step SetMinimumStep
    function setMinimumStep(uint256 newStep) external onlyOwner {
        require(newStep <= _MAX_BPS, "AuctionV2: minimum step must be <= 100%");
        require(newStep > 0, "AuctionV2: minimum step must be > 0");

        uint256 _oldStep = _minimumStep;
        _minimumStep = newStep;

        emit SetMinimumStep(
            newStep,
            _oldStep
        );
    }

    /// @notice Sets a new taker fee
    /// @param newFee The new taker fee, in basis points
    /// @dev Emits an event with the new taker fee SetTakerFee
    function setTakerFee(uint256 newFee) external onlyOwner {
        require(newFee <= _MAX_BPS, "AuctionV2: taker fee must be <= 100%");
        uint256 _oldFee = _takerFee;
        _takerFee = newFee;

        emit SetTakerFee(
            newFee,
            _oldFee
        );
    }

    /// @notice Sets a new withdraw early fee
    /// @param newFee The new withdraw early fee, in basis points
    /// @dev Emits an event with the new taker fee SetWithdrawEarlyFee
    function setWithdrawEarlyFee(uint256 newFee) external onlyOwner {
        require(newFee <= _MAX_BPS, "AuctionV2: withdraw early fee must be <= 100%");

        uint256 _oldFee = _withdrawEarlyFee;
        _withdrawEarlyFee = newFee;

        emit SetWithdrawEarlyFee(
            newFee,
            _oldFee
        );
    }

    /// @notice Sets a new minimum buyout price
    /// @param newMinimumBuyoutPrice The new minimum buyout price
    /// @dev will emit an event, SetMinimumBuyoutPrice, when changed
    function setMinimumBuyoutPrice(uint256 newMinimumBuyoutPrice) external onlyOwner {
        require(newMinimumBuyoutPrice > 0, "AuctionV2: minimum buyout price must be > 0");
        require(newMinimumBuyoutPrice <= 10000 * 1e18, "AuctionV2: minimum buyout price must be <= 10000 TOKEN");

        uint256 _oldMinimumBuyoutPrice = _minimumBuyoutPrice;
        _minimumBuyoutPrice = newMinimumBuyoutPrice;
        emit SetMinimumBuyoutPrice(
            newMinimumBuyoutPrice,
            _oldMinimumBuyoutPrice
        );
    }

    /// @notice Sets a new stake needed to stake a listing
    /// @param newStake The new listing stake, in value of the ERC20 token e.g. 10 e18
    /// @dev Emits an event with the new listing stake SetListingStake
    function setListingStake(uint256 newStake) external onlyOwner {
        require(newStake > 0, "AuctionV2: listing stake must be > 0");
        require(newStake <= 10000 * 1e18, "AuctionV2: listing stake must be <= 10000 TOKEN");

        uint256 _oldStake = _listingStake;
        _listingStake = newStake;

        emit SetListingStake(
            newStake,
            _oldStake
        );
    }

    /// @notice Sets a new fee receiver address
    /// @param receiver The new fee receiver address
    /// @dev This is the address that will receive fees when contract fees are withdrawn
    function setFeeReceiverAddress(address receiver) external onlyOwner {
        require(receiver != address(0), "AuctionV2: fee receiver address cannot be 0x0");
        _feeReceiverAddress = receiver;
    }

    /// @notice Withdraw fees to the fee receiver address
    /// @dev Emits an event with the amount withdrawn WithdrawFees
    function withdrawFees() external onlyOwner {
        uint256 fees = _accruedFees;
        _accruedFees = 0;
        SafeERC20.safeTransfer(IERC20(_arkm), _feeReceiverAddress, fees);

        emit WithdrawFees(
            fees
        );
    }

    /// @notice Renouncing ownership not allowed.
    /// @dev We don't want to allow renouncing ownership under any circumstance.
    function renounceOwnership() public override onlyOwner {
        revert("AuctionV2: Ownership cannot be renounced");
    }

    // Events.
    /// @notice Emitted when someone makes a valid bid on a listing
    event BidMade (
        bytes32 indexed listingPayload,
        address indexed bidder,
        uint256 bidID,
        uint256 effectiveAmount,
        bool buyout
    );

    /// @notice Emitted when a listing is funded with a stake
    event ListingFunded (
        bytes32 indexed listingPayload,
        address indexed seller,
        bool auction,
        uint256 startingPrice,
        uint256 buyoutPrice
    );

    /// @notice Emitted when a listing is claimed
    event ListingClaimed(
        bytes32 indexed listingPayload
    );

    /// @notice Emitted when a listing is rejected
    event ListingClosed (
        bytes32 indexed listingPayload
    );


    /// @notice Emitted when an account is granted approver status
    /// @param account The account granted approver status
    event GrantApprover (
        address indexed account
    );

    /// @notice Emitted when an account has its approver status revoked
    /// @param account The account that had its approver status revoked
    event RevokeApprover (
        address indexed account
    );

    /// @notice Emitted when the maker fee is set
    /// @param newFee The new maker fee
    /// @param oldFee The old maker fee
    event SetMakerFee (
        uint256 newFee,
        uint256 oldFee
    );

    /// @notice Emitted when the taker fee is set
    /// @param newFee The new taker fee
    /// @param oldFee The old taker fee
    event SetTakerFee (
        uint256 newFee,
        uint256 oldFee
    );

    /// @notice Emitted when the withdraw early fee is set
    /// @param newFee The new withdraw early fee
    /// @param oldFee The old withdraw early fee
    event SetWithdrawEarlyFee (
        uint256 newFee,
        uint256 oldFee
    );

    /// @notice Emitted when the listing stake is set
    /// @param newStake The new listing stake
    /// @param oldStake The old listing stake
    event SetListingStake (
        uint256 newStake,
        uint256 oldStake
    );

    /// @notice Emitted when accrued fees are withdrawn
    /// @param amount The amount of accrued fees withdrawn
    event WithdrawFees (
        uint256 amount
    );

    /// @notice Emitted when a new minimum buyout price is set
    event SetMinimumBuyoutPrice(
        uint256 newMinimumBuyoutPrice,
        uint256 oldMinimumBuyoutPrice
    );

    /// @notice Emitted when a new minimum bid step is set
    event SetMinimumStep(
        uint256 newMinimumStep,
        uint256 oldMinimumStep
    );

    /// @notice Emitted when a new default listing duration is set
    event SetDefaultListingDuration(
        uint256 newDuration,
        uint256 oldDuration
    );

      /// @notice Emitted when a new cooldown period is set
    event SetCooldown
    (
        uint256 newDuration,
        uint256 oldDuration
    );

    /// @notice Emitted when contract no longer accepts listings
    event StopAcceptingListings();

    /// @notice Emitted when a listing is approved
    event ListingApproved(
        bytes32 indexed listingPayload
    );

    /// @notice Emitted when pending listings are rejected
    event RejectedPending(
        bytes32 indexed listingPayload,
        uint32 rejected
    );
}

