// ░▒▓████████▓▒░▒▓██████▓▒░░▒▓█▓▒░░▒▓█▓▒░░▒▓██████▓▒░  
// ░▒▓█▓▒░     ░▒▓█▓▒░░▒▓█▓▒░▒▓█▓▒░░▒▓█▓▒░▒▓█▓▒░░▒▓█▓▒░ 
// ░▒▓█▓▒░     ░▒▓█▓▒░      ░▒▓█▓▒░░▒▓█▓▒░▒▓█▓▒░░▒▓█▓▒░ 
// ░▒▓██████▓▒░░▒▓█▓▒░      ░▒▓████████▓▒░▒▓█▓▒░░▒▓█▓▒░ 
// ░▒▓█▓▒░     ░▒▓█▓▒░      ░▒▓█▓▒░░▒▓█▓▒░▒▓█▓▒░░▒▓█▓▒░ 
// ░▒▓█▓▒░     ░▒▓█▓▒░░▒▓█▓▒░▒▓█▓▒░░▒▓█▓▒░▒▓█▓▒░░▒▓█▓▒░ 
// ░▒▓████████▓▒░▒▓██████▓▒░░▒▓█▓▒░░▒▓█▓▒░░▒▓██████▓▒░  
//
// SPDX-License-Identifier: MIT
// Copyright Han, 2023

pragma solidity ^0.8.25;

contract Echo {
    event ArtpieceCreated(address indexed creator);
    event ArtpieceTransferred(address indexed oldOwner, address indexed newOwner);
    event BidAccepted(uint256 value, address indexed fromAddress, address indexed toAddress);
    event BidPlaced(uint256 value, address indexed fromAddress);
    event BidWithdrawn(uint256 value, address indexed fromAddress);
    event ListedForSale(uint256 value, address indexed fromAddress, address indexed toAddress);
    event SaleCanceled(uint256 value, address indexed fromAddress, address indexed toAddress);
    event SaleCompleted(uint256 value, address indexed fromAddress, address indexed toAddress);

    error FundsTransfer();
    error InsufficientFunds();
    error ListedForSaleToSpecificAddress();
    error NoBid();
    error NotForSale();
    error NotOwner();
    error NotRoyaltyRecipient();
    error NotYourBid();
    error NullAddress();
    error RoyaltyTooHigh();

    string public constant MANIFEST = (
        'Gets you drunk.' '\n'
    );

    string public constant CORE = (
        '"use strict";let s={signature:"data:image/svg+xml;base64,PHN2ZyB3aWR0aD0iMjE2IiBoZWlnaHQ9IjIxNiIgdmlld0JveD0iMCAwIDIxNiAyMTYiIGZpbGw9Im5vbmUiIHhtbG5zPSJodHRwOi8vd3d3LnczLm9yZy8yMDAwL3N2ZyI+CjxwYXRoIGZpbGwtcnVsZT0iZXZlbm9kZCIgY2xpcC1ydWxlPSJldmVub2RkIiBkPSJNNDcgMEg5NlYxNkg0N0MyOS44NzkyIDE2IDE2IDI5Ljg3OTIgMTYgNDdWOTZIMFY0N0MwIDIxLjA0MjYgMjEuMDQyNiAwIDQ3IDBaTTEyMCAwVjE2SDE2OUMxODYuMTIxIDE2IDIwMCAyOS44NzkyIDIwMCA0N1Y5NkgyMTZWNDdDMjE2IDIxLjA0MjYgMTk0Ljk1NyAwIDE2OSAwSDEyMFpNMTY5IDIwMEgxMjBWMjE2SDE2OUMxOTQuOTU3IDIxNiAyMTYgMTk0Ljk1NyAyMTYgMTY5VjEyMEgyMDBWMTY5QzIwMCAxODYuMTIxIDE4Ni4xMjEgMjAwIDE2OSAyMDBaTTE2IDE2OVYxMjBIMFYxNjlDMCAxOTQuOTU3IDIxLjA0MjYgMjE2IDQ3IDIxNkg5NlYyMDBINDdDMjkuODc5MiAyMDAgMTYgMTg2LjEyMSAxNiAxNjlaTTEyMCA5Nkg5NlYxMjBIMTIwVjk2WiIgZmlsbD0id2hpdGUiLz4KPC9zdmc+Cg==",mouse_sensitivity:1.5,mouse_limit:.01,frame:0,res:[0,0]};window.h={newline:String.fromCharCode(10),parser:new URL(window.location)};const mobile=/Android|webOS|iPhone|iPad|iPod|BlackBerry|IEMobile|Opera Mini/i.test(navigator.userAgent);let w=window,d=document,b=d.body;d.body.style.touchAction="none",d.body.style.userSelect="none";let c=d.createElement("canvas");c.style.display="block",b.appendChild(c);const image=d.createElement("img");image.src=s.signature.trim(),image.style.cssText="width:40px;z-index:50;position:fixed;bottom:20px;right:20px;",b.appendChild(image);const CREDITS="I am grateful to Dima, WillStall, and IQ (sdf, AO), Demofox (Refraction) for making this piece possible, you are wizards. - Han";console.log(CREDITS);const glOptions={powerPreference:"high-performance"};mobile&&delete glOptions.powerPreference,w.gl=c.getContext("webgl2",glOptions),h.uniform1i=(e,t)=>h.uniform(e,t,"uniform1i"),h.uniform=(e,t,r)=>{s.uniforms||={};(s.uniforms[e]||=(()=>{const o=gl.getUniformLocation(s.current_program,e),i=r||(Array.isArray(t)?"uniform2fv":"uniform1f");return{update:e=>gl[i](o,e)}})()).update(t)},h.resize=()=>{let e,t,r,o={x:h.ix.mouse.x/s.res[0],y:h.ix.mouse.y/s.res[1]};e=w.innerWidth,t=w.innerHeight,s.aspect&&(e>t*s.aspect?e=t*s.aspect:t=e/s.aspect),r=w.devicePixelRatio,s.res[0]=c.width=e*r,s.res[1]=c.height=t*r,c.style.width=e+"px",c.style.height=t+"px",h.ix.set(c.width*o.x,c.height*o.y)},h.ix={start:{x:0,y:0},mouse:{x:0,y:0}},h.ix.set=(e,t)=>{h.ix.mouse={x:e,y:t}},h.ix.start=e=>{h.ix.start.x=e.clientX,h.ix.start.y=e.clientY,d.addEventListener("pointermove",h.ix.move)},h.clamp=(e,t,r)=>Math.max(t,Math.min(r,e)),h.ix.move=e=>{h.ix.mouse.x+=(e.clientX-h.ix.start.x)*window.devicePixelRatio*s.mouse_sensitivity,h.ix.mouse.y-=(e.clientY-h.ix.start.y)*window.devicePixelRatio*s.mouse_sensitivity,h.ix.start.x=e.clientX,h.ix.start.y=e.clientY,h.ix.mouse.x=h.clamp(h.ix.mouse.x,s.res[0]*s.mouse_limit,s.res[0]*(1-s.mouse_limit)),h.ix.mouse.y=h.clamp(h.ix.mouse.y,s.res[1]*s.mouse_limit,s.res[1]*(1-s.mouse_limit))},h.ix.stop=()=>{d.removeEventListener("pointermove",h.ix.move)},h.save={},h.save.toImage=()=>{const e=new Date;let t=String(e.getFullYear()).slice(2,4)+"-"+e.getMonth()+"-"+e.getDate()+" ("+s.frame+").png",r=document.createElement("a");r.setAttribute("download",t);let o=c.toDataURL("image/png").replace("data:image/png","data:application/octet-stream");r.setAttribute("href",o),r.click(),r.remove()},h.buildShader=(e,t)=>{let r=gl.createShader(e);return gl.shaderSource(r,t),gl.compileShader(r),r},h.initProgram=(e,t,r)=>{const o=gl.createProgram(),i=h.buildShader(gl.VERTEX_SHADER,t),c=h.buildShader(gl.FRAGMENT_SHADER,e);gl.attachShader(o,i),gl.attachShader(o,c),gl.linkProgram(o),gl.getShaderParameter(i,gl.COMPILE_STATUS)||console.error("V: "+gl.getShaderInfoLog(i)),gl.getShaderParameter(c,gl.COMPILE_STATUS)||console.error("F: "+gl.getShaderInfoLog(c)),gl.getProgramParameter(o,gl.LINK_STATUS)||console.error("P: "+gl.getProgramInfoLog(o));let a=gl.createBuffer(),s=gl.getAttribLocation(o,"p");return gl.bindBuffer(gl.ARRAY_BUFFER,a),gl.bufferData(gl.ARRAY_BUFFER,r,gl.STATIC_DRAW),gl.enableVertexAttribArray(s),gl.vertexAttribPointer(s,2,gl.FLOAT,!1,0,0),o},s.pixel=new Uint8Array(4),h.render=()=>{gl.viewport(0,0,c.width,c.height),gl.useProgram(s.program),s.current_program=s.program,h.uniform("u_time",.001*performance.now()),h.uniform("u_resolution",s.res),h.uniform("u_mouse",[h.ix.mouse.x,h.ix.mouse.y]),gl.drawArrays(gl.TRIANGLE_STRIP,0,4),gl.readPixels(0,0,1,1,gl.RGBA,gl.UNSIGNED_BYTE,s.pixel),h.save.queued&&(h.save.queued=!1,h.save.toImage()),s.frame++,requestAnimationFrame(h.render)};const init=async()=>{if(gl){h.resize(),h.ix.set(c.width/2,c.height/2),d.addEventListener("pointerdown",h.ix.start),d.addEventListener("pointerup",h.ix.stop),w.addEventListener("resize",h.resize),d.addEventListener("keyup",(e=>{if(!(e.ctrlKey||e.altKey||e.shiftKey||e.metaKey)&&"s"===e.key)h.save.queued=!0}));let e=Float32Array.of(-1,1,-1,-1,1,1,1,-1);const t="#version 300 es"+h.newline+"precision highp float; in vec2 p; void main(){ gl_Position =vec4(p,1.0,1.0); }";let r="precision highp float;out vec4 fragColor;uniform vec2 u_resolution;uniform float u_time;uniform vec2 u_mouse;const vec3 v=vec3(1),f=vec3(.862745098039216);const vec3 n=vec3(0,-.56,-.2);vec2 s(vec2 v,float y,float f,float x,float c){return x+(v-y)/(f-y)*(c-x);}float s(float v,float y,float f,float x,float c){return x+(v-y)/(f-y)*(c-x);}vec3 s(){vec3 v=gl_FragCoord.xyy,f;v=vec3(dot(v,vec3(127.1,311.7,74.7)),dot(v,vec3(269.5,183.3,246.1)),dot(v,vec3(113.5,271.9,124.6)));f=-1.+2.*fract(sin(v)*43758.5453123);return fract(555.*sin(777.*f))/256.;}vec3 s(vec3 v){v+=s();return v;}vec3 s(vec3 v,vec4 f){return v+2.*cross(f.xyz,cross(f.xyz,v)+v*f.w);}vec3 t(vec3 v,float f){return s(v,vec4(sin(f/2.),0,0,cos(f/2.)));}vec3 m(vec3 v,float f){return s(v,vec4(0,sin(f/2.),0,cos(f/2.)));}vec3 x(vec3 v,float f){return s(v,vec4(0,0,sin(f/2.),cos(f/2.)));}float e(vec3 v,vec3 f){float x=dot(f,f),c,r;vec3 y=v-vec3(0);c=2.*dot(f,y);r=dot(y,y)-.49;return c*c-4.*x*r<0.?-1.:(-c-sqrt(c*c-4.*x*r))/(2.*x);}float p(float v,float f){return max(-v,f);}float c(vec3 v,vec3 f){vec3 c=abs(v)-f;return length(max(c,0.))+min(max(c.x,max(c.y,c.z)),0.);}vec2 c(vec3 v,float f,vec2 n){float r=1e2,y=n.x*3.91442+n.y*4.1469,i,s;if(mod(n.y,2.)==1.)f*=-1.;vec2 u=u_mouse.xy/u_resolution.xy;u-=.5;y+=u.x*10.;y-=u.y*5.;v=t(v,f+y);v=x(v,f+y);vec3 d=vec3(.7),e,z,g;i=length(v)-.693;e=m(v,-f*3.+y);z=e;z.xz-=.7;i=p(c(z,d),i);z=e;z.xz+=.7;i=p(c(z,d),i);z=e;z.y-=.7;i=p(c(z,d),i);r=min(r,i);g=m(v,f*2.+y);s=length(g)-.693;z=g;z.xz-=.7;s=p(c(z,d),s);z=g;z.xz+=.7;s=p(c(z,d),s);z=g;z.y+=.7;s=p(c(z,d),s);r=min(r,s);return vec2(r,0);}vec3 e(vec3 v,float f,vec2 y){float m=c(v,f,y).x;vec2 n=vec2(.002,0);vec3 x=m-vec3(c(v-n.xyy,f,y).x,c(v-n.yxy,f,y).x,c(v-n.yyx,f,y));return normalize(x);}vec2 c(vec3 v,vec3 f,float y,float x,vec2 d){float z=0.,r=-1.;for(int i=0;i<64;i++){vec3 m=v+f*z;vec2 n=c(m,y,d);r=n.y;z+=n.x*x;if(abs(n.x)<.001||z>1e2)break;}return vec2(z,r);}vec3 c(){return mix(f,v,.7);}float d(vec3 v,vec3 f){float y=-.001/2.001,z,r;y*=y;z=-dot(f,v);r=1.-z;return y+(1.-y)*pow(r,5.);}vec3 d(vec3 v,vec3 f,vec3 y,float x,vec2 d){f=refract(f,y,1./1.001);v+=-y*.01*1.5;vec2 n=c(v,f,x,-1.,d);return n.x<0.?vec3(1):c()*exp(vec3(-.2)*n.x);}vec3 c(float v,vec3 f,vec3 c,vec3 y){float x=v*2.,r;vec3 z=n,i;z+=vec3(cos(x),sin(x),n.z)*9.34;i=normalize(z-f);r=.18*pow(1.-clamp(dot(c,-y),0.,1.),1.35);r=max(0.,r);return vec3(max(0.,dot(c,i))+r);}float c(float v){return fract(sin(v)*43758.5453123);}vec3 d(float v){float f=2.*acos(-1.)*fract(v/1.61803398874989),y=1.-(2.*v+1.)/float(8),z=sqrt(1.-y*y);return vec3(cos(f)*z,sin(f)*z,y);}float e(vec3 v,vec3 f,float y,float x,vec2 s){float z=0.;for(int r=0;r<8;r++){vec3 i=d(float(r)+y);i*=sign(dot(i,f))*c(float(r));z+=clamp(c(v+f*.01+i*.2*.314,x,s).x*20./.314,0.,1.);}z/=float(8);return clamp(z,0.,1.);}vec3 r(float v,float f){float x=sin(f);return vec3(3.564*x*cos(v),3.564*cos(f),3.564*x*sin(v));}mat3 u(vec3 v,vec2 f){vec3 y=normalize(vec3(0)-v),z=normalize(cross(vec3(0,floor(mod(f.y,2.))==0.?-1.:1.,0),y));return mat3(z,normalize(cross(y,z)),y);}void main(){float v=u_time,z,y,i,x;vec2 n=vec2(0),m,g;vec3 p=r(2.*acos(-1.)*n.x+1.570795,(1.-n.y)*3.14159),a;p=vec3(0,0,3.564);mat3 h=u(p,n);m=gl_FragCoord.xy;z=.5+.5*sin(m.x*147.)*sin(m.y*131.);y=smoothstep(0.,1.,sin(2.*acos(-1.)*v/8.));v+=0.;i=.45;x=.45;g=gl_FragCoord.xy;g=(g-.5*u_resolution.xy)/min(u_resolution.x,u_resolution.y);a=vec3(0);if(abs(g.x)<i&&abs(g.y)<x){g.x=s(g.x,-i,i,0.,2.);g.y=s(g.y,-x,x,0.,2.);vec2 l=floor(vec2(g)),k;g=fract(g)-.5;k=g*.483505735728486;vec3 t=normalize(h*vec3(k,1));float C=v-.1*z,F=2.*acos(-1.)*C/8.,E=e(p,t);if(E>0.){vec2 D=c(p,t,F,1.,l);if(D.x<1e2){vec3 B=p+D.x*t,w=e(B,F,l),o;float A,G,H,I;A=2.*acos(-1.)*(C-y)/8.;a.x=c(A,B,w,t).x;A=2.*acos(-1.)*C/8.;a.y=c(A,B,w,t).y;A=2.*acos(-1.)*(C+y)/8.;a.z=c(A,B,w,t).z;o=d(B,t,w,F,l);G=clamp(d(t,w),0.,1.);a+=mix(o,f,G);H=.1;H=c(sin(gl_FragCoord.x/u_resolution.x*1e3)+sin(gl_FragCoord.y/u_resolution.y*1e3))*.1;I=e(B,w,H,C,l);I*=I;a=mix(a,a*I,.05);}}}if(a.x==0.)a=c();a=s(a);fragColor=vec4(a,1);}";r="#version 300 es"+h.newline+r,window.program=s.program=h.initProgram(r,t,e),h.render()}else{const e=d.createElement("div");e.style.cssText="align-items:center;background:#969696;color:#fff;display:flex;font-family:monospace;font-size:20px;height:100vh;justify-content:center;left:0;position:fixed;top:0;width:100vw;",e.innerHTML="Your browser does not support WebGL.",b.append(e)}};init();'
    );

    modifier onlyOwner() {
        if (owner != msg.sender) {
            revert NotOwner();
        }

        _;
    }

    modifier onlyRoyaltyRecipient() {
        if (royaltyRecipient != msg.sender) {
            revert NotRoyaltyRecipient();
        }

        _;
    }

    struct Offer {
        bool active;
        uint256 value;
        address toAddress;
    }

    struct Bid {
        bool active;
        uint256 value;
        address fromAddress;
    }

    address public owner;

    Offer public currentOffer;

    Bid public currentBid;

    address public royaltyRecipient;

    uint256 public royaltyPercentage;

    mapping (address => uint256) public pendingWithdrawals;

    constructor(uint256 _royaltyPercentage) {
        if (_royaltyPercentage >= 100) {
            revert RoyaltyTooHigh();
        }

        owner = msg.sender;
        royaltyRecipient = msg.sender;
        royaltyPercentage = _royaltyPercentage;

        emit ArtpieceCreated(msg.sender);
    }

    function name() public view virtual returns (string memory) {
        return 'Echo';
    }

    function symbol() public view virtual returns (string memory) {
        return 'E';
    }

    function artpiece() public view virtual returns (string memory) {
        return string.concat(
            '<!DOCTYPE html>'
            '<html>'
                '<head>'
                    '<title>', 'Echo', '</title>'

                    '<meta name="viewport" content="width=device-width, initial-scale=1" />'

                    '<style>html,body{background:#969696;margin:0;padding:0;overflow:hidden;}</style>'
                '</head>'

                '<body>'
                    '<script type="text/javascript">',
                        CORE,
                    '</script>'
                '</body>'
            '</html>'
        );
    }

    function withdraw() public {
        uint256 amount = pendingWithdrawals[msg.sender];

        pendingWithdrawals[msg.sender] = 0;

        _sendFunds(amount);
    }

    function transferOwnership(address newOwner) public onlyOwner {
        if (newOwner == address(0)) {
            revert NullAddress();
        }

        _transferOwnership(newOwner);

        if (currentBid.fromAddress == newOwner) {
            uint256 amount = currentBid.value;

            currentBid = Bid({ active: false, value: 0, fromAddress: address(0) });

            pendingWithdrawals[newOwner] += amount;
        }

        if (currentOffer.active) {
            currentOffer = Offer({ active: false, value: 0, toAddress: address(0) });
        }
    }

    function listForSale(uint256 salePriceInWei) public onlyOwner {
        currentOffer = Offer({ active: true, value: salePriceInWei, toAddress: address(0) });

        emit ListedForSale(salePriceInWei, msg.sender, address(0));
    }

    function listForSaleToAddress(uint256 salePriceInWei, address toAddress) public onlyOwner {
        currentOffer = Offer({ active: true, value: salePriceInWei, toAddress: toAddress });

        emit ListedForSale(salePriceInWei, msg.sender, toAddress);
    }

    function cancelFromSale() public onlyOwner {
        Offer memory oldOffer = currentOffer;

        currentOffer = Offer({ active: false, value: 0, toAddress: address(0) });

        emit SaleCanceled(oldOffer.value, msg.sender, oldOffer.toAddress);
    }

    function buyNow() public payable {
        if (!currentOffer.active) {
            revert NotForSale();
        }

        if (currentOffer.toAddress != address(0) && currentOffer.toAddress != msg.sender) {
            revert ListedForSaleToSpecificAddress();
        }

        if (msg.value != currentOffer.value) {
            revert InsufficientFunds();
        }

        currentOffer = Offer({ active: false, value: 0, toAddress: address(0) });

        uint256 royaltyAmount = _calcRoyalty(msg.value);

        pendingWithdrawals[owner] += msg.value - royaltyAmount;
        pendingWithdrawals[royaltyRecipient] += royaltyAmount;

        emit SaleCompleted(msg.value, owner, msg.sender);

        _transferOwnership(msg.sender);
    }

    function placeBid() public payable {
        if (msg.value <= currentBid.value) {
            revert InsufficientFunds();
        }

        if (currentBid.value > 0) {
            pendingWithdrawals[currentBid.fromAddress] += currentBid.value;
        }

        currentBid = Bid({ active: true, value: msg.value, fromAddress: msg.sender });

        emit BidPlaced(msg.value, msg.sender);
    }

    function acceptBid() public onlyOwner {
        if (!currentBid.active) {
            revert NoBid();
        }

        uint256 amount = currentBid.value;
        address bidder = currentBid.fromAddress;

        currentOffer = Offer({ active: false, value: 0, toAddress: address(0) });
        currentBid = Bid({ active: false, value: 0, fromAddress: address(0) });

        uint256 royaltyAmount = _calcRoyalty(amount);

        pendingWithdrawals[owner] += amount - royaltyAmount;
        pendingWithdrawals[royaltyRecipient] += royaltyAmount;

        emit BidAccepted(amount, owner, bidder);

        _transferOwnership(bidder);
    }

    function withdrawBid() public {
        if (msg.sender != currentBid.fromAddress) {
            revert NotYourBid();
        }

        uint256 amount = currentBid.value;

        currentBid = Bid({ active: false, value: 0, fromAddress: address(0) });

        _sendFunds(amount);

        emit BidWithdrawn(amount, msg.sender);
    }

    function setRoyaltyRecipient(address newRoyaltyRecipient) public onlyRoyaltyRecipient {
        if (newRoyaltyRecipient == address(0)) {
            revert NullAddress();
        }

        royaltyRecipient = newRoyaltyRecipient;
    }

    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = owner;

        owner = newOwner;

        emit ArtpieceTransferred(oldOwner, newOwner);
    }

    function _sendFunds(uint256 amount) internal virtual {
        (bool success, ) = msg.sender.call{value: amount}('');

        if (!success) {
            revert FundsTransfer();
        }
    }

    function _calcRoyalty(uint256 amount) internal virtual returns (uint256) {
        return (amount * royaltyPercentage) / 100;
    }
}