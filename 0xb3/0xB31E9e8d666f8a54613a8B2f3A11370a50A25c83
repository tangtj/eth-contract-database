
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}


// File: contracts/lybra/interfaces/IesLBR.sol
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.17;

interface IesLBR {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function mint(address user, uint256 amount) external returns(bool);
    function burn(address user, uint256 amount) external returns(bool);
    function getPastVotes(address account, uint256 timepoint) external view returns (uint256);
    function getPastTotalSupply(uint256 timepoint) external view returns (uint256);
}

// File: contracts/lybra/interfaces/IMiningIncentives.sol
// SPDX-License-Identifier: BUSL-1.1
pragma solidity ^0.8.17;

interface IMiningIncentives {
    function totalStaked() external view returns (uint256);
    function stakedOf(address user) external view returns (uint256);
    function earned(address user) external view returns (uint256);
    function isOtherEarningsClaimable(address user) external view returns (bool);
    function esLBR() external view returns (address);
    function LBR() external view returns (address);
    function refreshReward(address _account) external;
}


// File: contracts/lybra/miner/esLBRBoost.sol
// SPDX-License-Identifier: GPL-3.0

pragma solidity ^0.8.17;

import "@openzeppelin/contracts/access/Ownable.sol";
import "../interfaces/IesLBR.sol";
import "../interfaces/IMiningIncentives.sol";


contract esLBRBoost is Ownable {
    esLBRLockSetting[] public esLBRLockSettings;
    mapping(address => LockStatus) public userLockStatus;
    IMiningIncentives public miningIncentives;

    // Define a struct for the lock settings
    struct esLBRLockSetting {
        uint256 duration;
        uint256 miningBoost;
    }

    // Define a struct for the user's lock status
    struct LockStatus {
        uint256 lockAmount;
        uint256 unlockTime;
        uint256 duration;
        uint256 miningBoost;
    }

    event StakeLBR(address indexed user, uint256 amount, uint256 time);
    event NewLockSetting(uint256 duration, uint256 miningBoost);
    event UserLockStatus(address indexed user, uint256 lockAmount, uint256 unlockTime, uint256 duration, uint256 miningBoost);
    event Unlock(address indexed user, uint256 unLockAmount, uint256 unlockTime);

    // Constructor to initialize the default lock settings
    constructor(address _miningIncentives) {
        esLBRLockSettings.push(esLBRLockSetting(30 days, 5 * 1e18));
        esLBRLockSettings.push(esLBRLockSetting(90 days, 10 * 1e18));
        esLBRLockSettings.push(esLBRLockSetting(180 days, 25 * 1e18));
        esLBRLockSettings.push(esLBRLockSetting(365 days, 50 * 1e18));
        miningIncentives = IMiningIncentives(_miningIncentives);
    }

    // Function to add a new lock setting
    function addLockSetting(esLBRLockSetting memory setting) external onlyOwner {
        esLBRLockSettings.push(setting);
        emit NewLockSetting(setting.duration, setting.miningBoost);
    }

    function setMiningIncentives(address _addr) external onlyOwner {
        miningIncentives = IMiningIncentives(_addr);
    }

    /**
     * @notice The user can set the lock status and choose to use either esLBR or LBR.
     * @param id The ID of the lock setting.
     * @param lbrAmount The amount of LBR to be locked.
     * @param useLBR A flag indicating whether to use LBR or not.
     */
    function setLockStatus(uint256 id, uint256 lbrAmount, bool useLBR) external {
        require(id < esLBRLockSettings.length, "Invalid lock setting ID");
        esLBRLockSetting memory _setting = esLBRLockSettings[id];
        LockStatus memory userStatus = userLockStatus[msg.sender];
        if (userStatus.unlockTime > block.timestamp) {
            require(userStatus.duration <= _setting.duration, "Your lock-in period has not ended, and the term can only be extended, not reduced.");
        }
        if(useLBR) {
            IesLBR(miningIncentives.LBR()).burn(msg.sender, lbrAmount);
            IesLBR(miningIncentives.esLBR()).mint(msg.sender, lbrAmount);
            emit StakeLBR(msg.sender, lbrAmount, block.timestamp);
        }
        require(IesLBR(miningIncentives.esLBR()).balanceOf(msg.sender) >= userStatus.lockAmount + lbrAmount, "IB");
        miningIncentives.refreshReward(msg.sender);
        userLockStatus[msg.sender] = LockStatus(userStatus.lockAmount + lbrAmount, block.timestamp + _setting.duration, _setting.duration, _setting.miningBoost);
        emit UserLockStatus(msg.sender, userLockStatus[msg.sender].lockAmount, userLockStatus[msg.sender].duration, _setting.duration, _setting.miningBoost);
    }

    function unlock() external {
        LockStatus storage userStatus = userLockStatus[msg.sender];
        require(userStatus.unlockTime < block.timestamp, "TNM");
        emit Unlock(msg.sender, userStatus.lockAmount, block.timestamp);
        userStatus.lockAmount = 0;
    }

    // Function to get the user's unlock time
    function getUnlockTime(address user) external view returns (uint256 unlockTime) {
        unlockTime = userLockStatus[user].unlockTime;
    }

    /**
     * @notice calculate the user's mining boost based on their lock status
     * @dev Based on the user's userUpdatedAt time, finishAt time, and the current time,
     * there are several scenarios that could occur, including no acceleration, full acceleration, and partial acceleration.
     */
    function getUserBoost(address user, uint256 userUpdatedAt, uint256 finishAt) external view returns (uint256) {
        LockStatus memory userStatus = userLockStatus[user];
        uint256 boostEndTime = userStatus.unlockTime;
        if (userUpdatedAt >= boostEndTime || userUpdatedAt >= finishAt || userStatus.lockAmount == 0) {
            return 0;
        }
        uint needLockedAmount = getAmountNeedLocked(user);
        if(needLockedAmount == 0) return 0;
        uint256 maxBoost = userLockStatus[user].miningBoost;

        if (finishAt > boostEndTime && block.timestamp > boostEndTime) {
            uint256 time = block.timestamp > finishAt ? finishAt : block.timestamp;
            maxBoost = ((boostEndTime - userUpdatedAt) * maxBoost) / (time - userUpdatedAt);
        }
        if (userStatus.lockAmount >= needLockedAmount) {
            return maxBoost;
        }
        return maxBoost * userStatus.lockAmount / needLockedAmount;
    }

    function getAmountNeedLocked(address user) public view returns (uint256) {
        uint256 stakedAmount = miningIncentives.stakedOf(user);
        uint256 totalStaked = miningIncentives.totalStaked();
        if(stakedAmount == 0 || totalStaked == 0) return 0;
        return stakedAmount * IesLBR(miningIncentives.esLBR()).totalSupply() / totalStaked;
    }
}

