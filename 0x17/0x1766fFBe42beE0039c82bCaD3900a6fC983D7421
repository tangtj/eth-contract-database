
// File: contracts/CenoaCustomProxy.sol
// SPDX-License-Identifier: MIT
pragma solidity 0.8.12;

import "hardhat-deploy/solc_0.8/proxy/Proxy.sol";

interface ERC165 {
    function supportsInterface(bytes4 id) external view returns (bool);
}

///@notice Proxy implementing EIP173 for ownership management
contract CenoaCustomProxy is Proxy {
    // ////////////////////////// EVENTS ///////////////////////////////////////////////////////////////////////

    event AdminChanged(address indexed previousOwner, address indexed newOwner);
    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
    event ImplementationProposerTransferred(address indexed previousImplementationProposer, address indexed newImplementationProposer);
    event NewImplementationProposed(address indexed previousProposedImplementation, address indexed newProposedImplementation);

    // /////////////////////// CONSTRUCTOR //////////////////////////////////////////////////////////////////////

    constructor(
        address implementationAddress,
        address ownerAddress,
        address implementationProposerAddress,
        bytes memory data
    ) payable {
        _setImplementation(implementationAddress, data);
        _setOwner(ownerAddress);
        _setImplementationProposer(implementationProposerAddress);
    }

    // ///////////////////// EXTERNAL ///////////////////////////////////////////////////////////////////////////

    function owner() external view returns (address) {
        return _owner();
    }

    function implementationProposer() external view returns (address) {
        return _implementationProposer();
    }

    function supportsInterface(bytes4 id) external view returns (bool) {
        if (id == 0x01ffc9a7 || id == 0x7f5828d0) {
            return true;
        }
        if (id == 0xFFFFFFFF) {
            return false;
        }

        ERC165 implementation;
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            implementation := sload(0xdc57da9fc785afa68ff5837a720d1c3b9e3a13ff1f14ba5b97b5dbbaaf524d24)
        }

        // Technically this is not standard compliant as ERC-165 require 30,000 gas which that call cannot ensure
        // because it is itself inside `supportsInterface` that might only get 30,000 gas.
        // In practise this is unlikely to be an issue.
        try implementation.supportsInterface(id) returns (bool support) {
            return support;
        } catch {
            return false;
        }
    }

    function transferOwnership(address newOwner) external onlyOwner {
        _setOwner(newOwner);
    }

    function transferImplementationProposer(address newImplementationProposer) external onlyImplementationProposer {
        _setImplementationProposer(newImplementationProposer);
    }

    function proposeImplementation(address newImplementation) external onlyImplementationProposer {
        _setProposedImplementation(newImplementation);
    }

    function upgradeTo(address newImplementation) external onlyOwner {
        require(_proposedImplementation() == newImplementation, "INVALID_IMPLEMENTATION");
        _setImplementation(newImplementation, "");
    }

    function upgradeToAndCall(address newImplementation, bytes calldata data) external payable onlyOwner {
        require(_proposedImplementation() == newImplementation, "INVALID_IMPLEMENTATION");
        _setImplementation(newImplementation, data);
    }

    // /////////////////////// MODIFIERS ////////////////////////////////////////////////////////////////////////

    modifier onlyOwner() {
        require(msg.sender == _owner(), "NOT_AUTHORIZED");
        _;
    }

    modifier onlyImplementationProposer() {
        require(msg.sender == _implementationProposer(), "NOT_AUTHORIZED");
        _;
    }

    // ///////////////////////// INTERNAL //////////////////////////////////////////////////////////////////////

    function _owner() internal view returns (address adminAddress) {
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            adminAddress := sload(0xa70e4adad54efeb91debe9075288ce332b8d0038c41ef590e91504c8a8b5331f)
        }
    }

    function _setOwner(address newOwner) internal {
        address previousOwner = _owner();
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            sstore(0xa70e4adad54efeb91debe9075288ce332b8d0038c41ef590e91504c8a8b5331f, newOwner)
        }
        emit OwnershipTransferred(previousOwner, newOwner);
        emit AdminChanged(previousOwner, newOwner);
    }

    function _implementationProposer() internal view returns (address implementationProposerAddress) {
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            implementationProposerAddress := sload(0xc485db7be7e777a685e6f5a6f3490ab355ab74c6db454143b8e0d4151bd1e749)
        }
    }

    function _setImplementationProposer(address newImplementationProposer) internal {
        address previousImplementationProposer = _implementationProposer();
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            sstore(0xc485db7be7e777a685e6f5a6f3490ab355ab74c6db454143b8e0d4151bd1e749, newImplementationProposer)
        }
        emit ImplementationProposerTransferred(previousImplementationProposer, newImplementationProposer);
    }

    function _proposedImplementation() internal view returns (address proposedImplementationAddress) {
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            proposedImplementationAddress := sload(0x7a8edd22602eecd2ac7d57069d609b6c1785dcec5a7d528beb69ef539c883e68)
        }
    }

    function _setProposedImplementation(address newProposedImplementation) internal {
        address previousProposedImplementation = _proposedImplementation();
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            sstore(0x7a8edd22602eecd2ac7d57069d609b6c1785dcec5a7d528beb69ef539c883e68, newProposedImplementation)
        }
        emit NewImplementationProposed(previousProposedImplementation, newProposedImplementation);
    }

    receive() external override payable virtual {
        _fallback();
    }

}


// File: hardhat-deploy/solc_0.8/proxy/Proxy.sol
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

// EIP-1967
abstract contract Proxy {
    // /////////////////////// EVENTS ///////////////////////////////////////////////////////////////////////////

    event ProxyImplementationUpdated(address indexed previousImplementation, address indexed newImplementation);

    // ///////////////////// EXTERNAL ///////////////////////////////////////////////////////////////////////////

    receive() external payable virtual {
        revert("ETHER_REJECTED"); // explicit reject by default
    }

    fallback() external payable {
        _fallback();
    }

    // ///////////////////////// INTERNAL //////////////////////////////////////////////////////////////////////

    function _fallback() internal {
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            let implementationAddress := sload(0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc)
            calldatacopy(0x0, 0x0, calldatasize())
            let success := delegatecall(gas(), implementationAddress, 0x0, calldatasize(), 0, 0)
            let retSz := returndatasize()
            returndatacopy(0, 0, retSz)
            switch success
            case 0 {
                revert(0, retSz)
            }
            default {
                return(0, retSz)
            }
        }
    }

    function _setImplementation(address newImplementation, bytes memory data) internal {
        address previousImplementation;
        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            previousImplementation := sload(0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc)
        }

        // solhint-disable-next-line security/no-inline-assembly
        assembly {
            sstore(0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc, newImplementation)
        }

        emit ProxyImplementationUpdated(previousImplementation, newImplementation);

        if (data.length > 0) {
            (bool success, ) = newImplementation.delegatecall(data);
            if (!success) {
                assembly {
                    // This assembly ensure the revert contains the exact string data
                    let returnDataSize := returndatasize()
                    returndatacopy(0, 0, returnDataSize)
                    revert(0, returnDataSize)
                }
            }
        }
    }
}

