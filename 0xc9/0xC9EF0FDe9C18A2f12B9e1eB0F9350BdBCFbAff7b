
// File: @openzeppelin/contracts/access/Ownable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby disabling any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}


// File: @openzeppelin/contracts/token/ERC20/ERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/ERC20.sol)

pragma solidity ^0.8.0;

import "./IERC20.sol";
import "./extensions/IERC20Metadata.sol";
import "../../utils/Context.sol";

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.openzeppelin.com/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * The default value of {decimals} is 18. To change this, you should override
 * this function so it returns a different value.
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the default value returned by this function, unless
     * it's overridden.
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _transfer(owner, to, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * NOTE: If `amount` is the maximum `uint256`, the allowance is not updated on
     * `transferFrom`. This is semantically equivalent to an infinite approval.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * NOTE: Does not update the allowance if the current allowance
     * is the maximum `uint256`.
     *
     * Requirements:
     *
     * - `from` and `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     * - the caller must have allowance for ``from``'s tokens of at least
     * `amount`.
     */
    function transferFrom(address from, address to, uint256 amount) public virtual override returns (bool) {
        address spender = _msgSender();
        _spendAllowance(from, spender, amount);
        _transfer(from, to, amount);
        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        address owner = _msgSender();
        _approve(owner, spender, allowance(owner, spender) + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        address owner = _msgSender();
        uint256 currentAllowance = allowance(owner, spender);
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(owner, spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `from` to `to`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `from` must have a balance of at least `amount`.
     */
    function _transfer(address from, address to, uint256 amount) internal virtual {
        require(from != address(0), "ERC20: transfer from the zero address");
        require(to != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
            // Overflow not possible: the sum of all balances is capped by totalSupply, and the sum is preserved by
            // decrementing then incrementing.
            _balances[to] += amount;
        }

        emit Transfer(from, to, amount);

        _afterTokenTransfer(from, to, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        unchecked {
            // Overflow not possible: balance + amount is at most totalSupply + amount, which is checked above.
            _balances[account] += amount;
        }
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
            // Overflow not possible: amount <= accountBalance <= totalSupply.
            _totalSupply -= amount;
        }

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(address owner, address spender, uint256 amount) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Updates `owner` s allowance for `spender` based on spent `amount`.
     *
     * Does not update the allowance amount in case of infinite allowance.
     * Revert if not enough allowance is available.
     *
     * Might emit an {Approval} event.
     */
    function _spendAllowance(address owner, address spender, uint256 amount) internal virtual {
        uint256 currentAllowance = allowance(owner, spender);
        if (currentAllowance != type(uint256).max) {
            require(currentAllowance >= amount, "ERC20: insufficient allowance");
            unchecked {
                _approve(owner, spender, currentAllowance - amount);
            }
        }
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual {}
}


// File: @openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (token/ERC20/extensions/ERC20Burnable.sol)

pragma solidity ^0.8.0;

import "../ERC20.sol";
import "../../../utils/Context.sol";

/**
 * @dev Extension of {ERC20} that allows token holders to destroy both their own
 * tokens and those that they have an allowance for, in a way that can be
 * recognized off-chain (via event analysis).
 */
abstract contract ERC20Burnable is Context, ERC20 {
    /**
     * @dev Destroys `amount` tokens from the caller.
     *
     * See {ERC20-_burn}.
     */
    function burn(uint256 amount) public virtual {
        _burn(_msgSender(), amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, deducting from the caller's
     * allowance.
     *
     * See {ERC20-_burn} and {ERC20-allowance}.
     *
     * Requirements:
     *
     * - the caller must have allowance for ``accounts``'s tokens of at least
     * `amount`.
     */
    function burnFrom(address account, uint256 amount) public virtual {
        _spendAllowance(account, _msgSender(), amount);
        _burn(account, amount);
    }
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}


// File: @openzeppelin/contracts/token/ERC20/extensions/IERC20Permit.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.4) (token/ERC20/extensions/IERC20Permit.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 *
 * ==== Security Considerations
 *
 * There are two important considerations concerning the use of `permit`. The first is that a valid permit signature
 * expresses an allowance, and it should not be assumed to convey additional meaning. In particular, it should not be
 * considered as an intention to spend the allowance in any specific way. The second is that because permits have
 * built-in replay protection and can be submitted by anyone, they can be frontrun. A protocol that uses permits should
 * take this into consideration and allow a `permit` call to fail. Combining these two aspects, a pattern that may be
 * generally recommended is:
 *
 * ```solidity
 * function doThingWithPermit(..., uint256 value, uint256 deadline, uint8 v, bytes32 r, bytes32 s) public {
 *     try token.permit(msg.sender, address(this), value, deadline, v, r, s) {} catch {}
 *     doThing(..., value);
 * }
 *
 * function doThing(..., uint256 value) public {
 *     token.safeTransferFrom(msg.sender, address(this), value);
 *     ...
 * }
 * ```
 *
 * Observe that: 1) `msg.sender` is used as the owner, leaving no ambiguity as to the signer intent, and 2) the use of
 * `try/catch` allows the permit to fail and makes the code tolerant to frontrunning. (See also
 * {SafeERC20-safeTransferFrom}).
 *
 * Additionally, note that smart contract wallets (such as Argent or Safe) are not able to produce permit signatures, so
 * contracts should have entry points that don't rely on permit.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     *
     * CAUTION: See Security Considerations above.
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}


// File: @openzeppelin/contracts/token/ERC20/IERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 amount) external returns (bool);
}


// File: @openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.3) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../extensions/IERC20Permit.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    /**
     * @dev Transfer `value` amount of `token` from the calling contract to `to`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeTransfer(IERC20 token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    /**
     * @dev Transfer `value` amount of `token` from `from` to `to`, spending the approval given by `from` to the
     * calling contract. If `token` returns no value, non-reverting calls are assumed to be successful.
     */
    function safeTransferFrom(IERC20 token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(IERC20 token, address spender, uint256 value) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    /**
     * @dev Increase the calling contract's allowance toward `spender` by `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeIncreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        uint256 oldAllowance = token.allowance(address(this), spender);
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, oldAllowance + value));
    }

    /**
     * @dev Decrease the calling contract's allowance toward `spender` by `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful.
     */
    function safeDecreaseAllowance(IERC20 token, address spender, uint256 value) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, oldAllowance - value));
        }
    }

    /**
     * @dev Set the calling contract's allowance toward `spender` to `value`. If `token` returns no value,
     * non-reverting calls are assumed to be successful. Meant to be used with tokens that require the approval
     * to be set to zero before setting it to a non-zero value, such as USDT.
     */
    function forceApprove(IERC20 token, address spender, uint256 value) internal {
        bytes memory approvalCall = abi.encodeWithSelector(token.approve.selector, spender, value);

        if (!_callOptionalReturnBool(token, approvalCall)) {
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, 0));
            _callOptionalReturn(token, approvalCall);
        }
    }

    /**
     * @dev Use a ERC-2612 signature to set the `owner` approval toward `spender` on `token`.
     * Revert on invalid signature.
     */
    function safePermit(
        IERC20Permit token,
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal {
        uint256 nonceBefore = token.nonces(owner);
        token.permit(owner, spender, value, deadline, v, r, s);
        uint256 nonceAfter = token.nonces(owner);
        require(nonceAfter == nonceBefore + 1, "SafeERC20: permit did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        require(returndata.length == 0 || abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     *
     * This is a variant of {_callOptionalReturn} that silents catches all reverts and returns a bool instead.
     */
    function _callOptionalReturnBool(IERC20 token, bytes memory data) private returns (bool) {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We cannot use {Address-functionCall} here since this should return false
        // and not revert is the subcall reverts.

        (bool success, bytes memory returndata) = address(token).call(data);
        return
            success && (returndata.length == 0 || abi.decode(returndata, (bool))) && Address.isContract(address(token));
    }
}


// File: @openzeppelin/contracts/utils/Address.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     *
     * Furthermore, `isContract` will also return true if the target contract within
     * the same transaction is already scheduled for destruction by `SELFDESTRUCT`,
     * which only has an effect at the end of a transaction.
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.8.0/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}


// File: @openzeppelin/contracts/utils/Context.sol
// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.4) (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    function _contextSuffixLength() internal view virtual returns (uint256) {
        return 0;
    }
}


// File: contracts/BountyV4.sol
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.19;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";

/**
    @title Bounty Contract Version 4

    @notice This contract allows for the creation, funding, and completion of blockchain intelligence
    bounties in a decentralized manner.

    @dev This contract is intended to be used with the ARKM token.

    Users can submit bounties by putting up ARKM, and other users can complete the bounties by
    staking ARKM to submit IDs that correspond to solutions.

    Submission IDs are not submitted raw, but instead hashed with the submitter's address to
    prevent front-running. The IDs are effectively secrets that only the submitters know on
    submission.

    Submissions are put in a queue for a bounty and given first-come first-served preference for
    evaluation.

    bountyIDs and submissionIDs correspond to off-chain records that detail the bounty and contain
    solutions. In v4, these records live on the Arkham platform. In the future there could be any
    number of secondary services that host these records.

    Once submissions are made, only a set of approver addresses can approve or reject them -- NOT
    the posters of the bounty. Approver addresses can be added or removed by the the contract
    owner.

    Once a bounty is funded, it cannot be closed until after its expiration. Bounties also must
    remain open until all submissions to the bounty have been approved or rejected. Once a bounty is
    closed, it cannot be reopened. If a funder closes their bounty, they receive the amount of the
    bounty back. (Bounty funders can receive a maximum of 100% of their initial funding back. If
    there is excess funding from rejected submissions, it is accrued as fees.)

    The contract owner sets the initial submission stake, maker fee, and taker fee.

    A maker fee is charged when a bounty is funded. A taker fee is charged when a submission is
    paid out. These fees are in basis points, e.g. 100 basis points = 1%. Fees are disbursed at the
    discretion of the contract owner and are withdrawn to an address set at contract creation and
    changeable by the owner.

    If a submission is approved, the submitter receives the bounty amount less a taker fee, plus
    their stake back. If a submission is rejected, the submitter does NOT receive their stake back,
    and the bounty amount increases by the amount staked. Only one submission may be active for a
    given bounty at a time. Stakes for un-evaluated submissions are paid back to the submitters
    when the bounty is closed.

    Delivery of the information corresponding to the accepted submission to the funder is handled
    off-chain.
 */
contract BountyV4 is Ownable {
    address private immutable _arkm;

    uint256 private _submissionStake;
    uint256 private _makerFee;
    uint256 private _takerFee;
    uint32  private _maxActiveSubmissions;
    bool    private _acceptingBounties;
    uint256 private _accruedFees;
    address private _feeReceiverAddress;
    uint256 private _minBounty;

    /// @dev Durations are in days
    uint32  private _defaultDuration;
    uint32  private _minDuration;
    uint32  private _maxDuration;

    uint32  private immutable _minDurationLimit = 1;
    uint32  private immutable _maxDurationLimit = 365;

    uint64  private constant _MAX_BPS = 10000;

    /// @dev Bounty payload --> Bounty
    mapping(bytes32 => Bounty) private _bounties;

    /// @dev Array of bounty payloads
    bytes32[] private _bountyPayloads;

    /// @dev Bounty payload --> Submission[], contains queues of submissions to bounties
    mapping(bytes32 => Submission[]) private _submissions;

    /// @dev Bounty payload -> Address[], contains funders of the bounty.
    mapping(bytes32 => address[]) private _bountyFunders;

    /// @dev Approver address --> is approver
    mapping(address => bool) private _approvers;

    /// @dev Arbiter address --> is arbiter
    mapping(address => bool) private _arbiters;

    /// @dev Bounty payload -> accepted submission payload
    mapping(bytes32 => bytes32) private _acceptedPayloads;
    mapping(bytes32 => bool) private _rejectedPayloads;

    /// @notice Struct representing a bounty
    /// @dev No submissions may be posted to the bounty after the unlock time. If there is are
    /// active submissions when the unlock time is reached, they may still be approved after the
    /// unlock time. The bounty is only considered closed when it is past the expiration AND there
    /// are no active submissions. The hashed ID corresponds to a record kept on the Arkham platform
    /// - or in the future, on any number of secondary serivices.
    struct Bounty {
        uint256 amount;
        uint256 initialAmount;
        uint64 expiration;
        uint32 queueIndex;
        bool closed;
    }

    /// @notice Struct representing a submjsion to a bounty
    /// @dev The payload is a hash of the submission ID and the submitter's address to prevent
    /// front-running. This ID corresponds to a record on the Arkham platform - or, in the future,
    /// any secondary service that bounty-approvers can use to verify the submission.
    struct Submission {
        bytes32 submissionPayload;
        address submitter;
        uint256 stake;
    }

    /// @notice Composite view of listing returned by the external bountyState function
    struct BountyState {
        uint256     expiration;
        uint256     amount;
        bool        closed;
        uint32      queueIndex;
        uint32      submissionCount;
        address[]   funders;
        uint256     initialAmount;
    }

    /// @notice Emitted when a bounty is funded
    /// @param bountyPayload The payload (address x ID) of the funded bounty
    /// @param funder The address of the funder
    /// @param initialValue The initial value of the bounty
    /// @param expiration The unlock time of the bounty
    event FundBounty (
        bytes32 indexed bountyPayload,
        address indexed funder,
        uint256 initialValue,
        uint64  expiration
    );

    /// @notice Emitted when a submission is made for a bounty
    /// @param bountyPayload The payload of the bounty for which the submission is made
    /// @param submitter The address of the submitter
    /// @param stake The stake of the submission
    /// @param submissionPayload The payload of the submission
    /// @param queueIndex The queue index of the submission
    /// @param currentQueueIndex The current queue index of the bounty the submission was made to
    event FundSubmission (
        bytes32 indexed bountyPayload,
        address indexed submitter,
        uint256 stake,
        bytes32 submissionPayload,
        uint32 queueIndex,
        uint32 currentQueueIndex
    );

    /// @notice Emitted when a submission for a bounty is rejected
    /// @param bountyPayload The payload of the bounty for which the submission is rejected
    /// @param submitter The address of the submitter
    /// @param stake The stake of the rejected submission
    /// @param submissionPayload The payload of the rejected submission
    /// @param queueIndex The queue index of the submission
    /// @param queueLength The queue length of the bounty the submission was made to
    event RejectSubmission (
        bytes32 indexed bountyPayload,
        address indexed submitter,
        uint256 stake,
        bytes32 submissionPayload,
        uint32 queueIndex,
        uint32 queueLength
    );

    /// @notice Emitted when a submission for a bounty is approved
    /// @param bountyPayload The payload of the bounty for which the submission is approved
    /// @param submitter The address of the submitter
    /// @param stake The stake of the approved submission
    /// @param submissionPayload The payload of the approved submission
    /// @param payoutToSubmitter The payout to the submitter
    /// @param queueIndex The queue index of the submission
    /// @param queueLength The queue length of the bounty the submission was made to
    event SubmissionApproved (
        bytes32 indexed bountyPayload,
        address indexed submitter,
        uint256 stake,
        bytes32 submissionPayload,
        uint256 payoutToSubmitter,
        uint32 queueIndex,
        uint32 queueLength
    );

    /// @notice Emitted when a bounty is closed
    /// @param bountyPayload The payload of the closed bounty
    /// @param funder The address of the funder
    /// @param payoutToFunder The payout to the funder
    /// @param excessFromStaking The excess fees
    /// @param queueLength The total number of rejected submissions in the queue
    event CloseBounty (
        bytes32 indexed bountyPayload,
        address indexed funder,
        address indexed closedBy,
        uint256 payoutToFunder,
        uint256 excessFromStaking,
        uint32 queueLength
    );

    /// @notice Emitted when an account is granted approver status
    /// @param account The account granted approver status
    event GrantApprover (
        address indexed account
    );

    /// @notice Emitted when an account has its approver status revoked
    /// @param account The account that had its approver status revoked
    event RevokeApprover (
        address indexed account
    );

    /// @notice Emitted when an account is granted arbiter status
    /// @param account The account granted arbiter status
    event GrantArbiter (
        address indexed account
    );

    /// @notice Emitted when an account has its arbiter status revoked
    /// @param account The account that had its arbiter status revoked
    event RevokeArbiter (
        address indexed account
    );

    /// @notice Emitted when the maker fee is set
    /// @param newFee The new maker fee
    /// @param oldFee The old maker fee
    event SetMakerFee (
        uint256 newFee,
        uint256 oldFee
    );

    /// @notice Emitted when the taker fee is set
    /// @param newFee The new taker fee
    /// @param oldFee The old taker fee
    event SetTakerFee (
        uint256 newFee,
        uint256 oldFee
    );

    /// @notice Emitted when the submission stake is set
    /// @param newStake The new submission stake
    /// @param oldStake The old submission stake
    event SetSubmissionStake (
        uint256 newStake,
        uint256 oldStake
    );

    /// @notice Emitted when the contract stops accepting bounties
    event CloseBountySubmissions ();

    /// @notice Emitted when accrued fees are withdrawn
    /// @param amount The amount of accrued fees withdrawn
    event WithdrawFees (
        uint256 amount
    );

    /// @notice Contract constructor that sets initial values
    /// @param arkmAddress Address of the ERC20 token to be used (ARKM)
    /// @param initialSubmissionStake Initial stake required for a submission, e.g. 10 e18
    /// @param initialMakerFee Initial fee for creating a bounty, in basis points
    /// @param initialTakerFee Initial fee for completing a bounty, in basis points
    /// @param feeReceiverAddress Address to receive fees
    /// @param initialMaxActiveSubmissions Initial max number of active submissions
    /// @param initialMinBounty Initial minimum bounty amount

    /// @dev The burnable check makes sure the token is an ERC20. We are not currently using burn
    /// functionality. ARKM passes the burnable check, so if it fails we know we are trying to
    /// deploy with the wrong token contract address.
    constructor(address arkmAddress, uint256 initialSubmissionStake, uint256 initialMakerFee, uint256 initialTakerFee, uint32 initialDefaultDuration, address feeReceiverAddress, uint32 initialMaxActiveSubmissions, uint256 initialMinBounty, uint32 initialMinDuration, uint32 initialMaxDuration) {
        require(initialMakerFee <= _MAX_BPS, "BountyV4: maker fee must be <= 10000");
        require(initialTakerFee <= _MAX_BPS, "BountyV4: taker fee must be <= 10000");
        require(feeReceiverAddress != address(0), "BountyV4: fee receiver address cannot be 0x0");


        require(initialMinDuration >= _minDurationLimit, "BountyV4: min duration must be >= 1 day");
        require(initialMaxDuration <= _maxDurationLimit, "BountyV4: max duration must be <= 365 days");
        require(initialMinDuration <= initialMaxDuration, "BountyV4: min duration must be <= max duration");
        require(initialMinDuration <= initialDefaultDuration, "BountyV4: min duration must be <= default duration");
        require(initialMaxDuration >= initialDefaultDuration, "BountyV4: max duration must be >= default duration");

        try ERC20Burnable(arkmAddress).totalSupply() returns (uint256) {
            _arkm = arkmAddress;
        } catch {
            revert("BountyV4: provided token address does not implement ERC20Burnable");
        }

        _submissionStake = initialSubmissionStake;
        _makerFee = initialMakerFee;
        _takerFee = initialTakerFee;
        _acceptingBounties = true;
        _defaultDuration = initialDefaultDuration;
        _feeReceiverAddress = feeReceiverAddress;
        _maxActiveSubmissions = initialMaxActiveSubmissions;
        _minBounty = initialMinBounty;
        _minDuration = initialMinDuration;
        _maxDuration = initialMaxDuration;
    }

    /// @return The amount of ARKM accrued from fees
    function accruedFees() public view virtual returns (uint256) {
        return _accruedFees;
    }

    /// @return The address of the ERC20 token to be used
    function arkm() public view virtual returns (address) {
        return _arkm;
    }

    /// @return The fee for creating a bounty, in basis points
    function makerFee() public view virtual returns (uint256) {
        return _makerFee;
    }

    /// @return The fee for completing a bounty, in basis points
    function takerFee() public view virtual returns (uint256) {
        return _takerFee;
    }

    /// @return The stake required for a submission, in value of the ERC20 token
    function submissionStake() public view virtual returns (uint256) {
        return _submissionStake;
    }

    /// @return The minimum bounty amount
    function minBounty() public view virtual returns (uint256) {
        return _minBounty;
    }

    /// @return The max number of active submissions
    function maxActiveSubmissions() public view virtual returns (uint32) {
        return _maxActiveSubmissions;
    }

    /// @return The default duration of a bounty, in days
    function defaultDuration() public view virtual returns (uint32) {
        return _defaultDuration;
    }

    /// @return The minimum duration of a bounty, in days
    function minDuration() public view virtual returns (uint32) {
        return _minDuration;
    }

    /// @return The maximum duration of a bounty, in days
    function maxDuration() public view virtual returns (uint32) {
        return _maxDuration;
    }

    /// @param bountyPayload The payload for the bounty
    /// @return The addresses of the funders of the bounty
    function funders(bytes32 bountyPayload) public view virtual returns (address[] memory) {
        return _bountyFunders[bountyPayload];
    }

    /// @param bountyPayload The payload for the bounty
    /// @return The amount of the bounty
    function amount(bytes32 bountyPayload) public view virtual returns (uint256) {
        return _bounties[bountyPayload].amount;
    }

    /// @param bountyPayload The payload for the bounty
    /// @return The initial amount of the bounty
    function initialAmount(bytes32 bountyPayload) public view virtual returns (uint256) {
        return _bounties[bountyPayload].initialAmount;
    }

    /// @param bountyPayload The payload for the bounty
    /// @return The expiration time of the bounty
    function expiration(bytes32 bountyPayload) public view virtual returns (uint64) {
        return _bounties[bountyPayload].expiration;
    }

    /// @param bountyPayload The payload for the bounty
    /// @return The current queue index of the bounty
    /// @dev The queue index is used to move through the list of submission made for a bounty. If
    /// the queue index is 3, the 4th submission in the list is the current one -- or, if there
    /// are only 3 submissions, there is no current submission. Anything submissions prior to th
    /// current queueIndex are rejected, any above are yet to be officially reviewed.
    function bountyQueueIndex(bytes32 bountyPayload) public view virtual returns (uint32) {
        return _bounties[bountyPayload].queueIndex;
    }

    /// @param bountyPayload The payload for the bounty
    /// @return The payload of the approved submission or 0 if none
    function approvedSubmission(bytes32 bountyPayload) public view virtual returns (bytes32) {
        if (!_bounties[bountyPayload].closed) return 0;
        return _acceptedPayloads[bountyPayload];
    }

    /// @param bountyPayload The payload for the bounty
    /// @return The total number of submissions received for the bounty
    function submissionsCount(bytes32 bountyPayload) public view virtual returns (uint32) {
        return uint32(_submissions[bountyPayload].length);
    }

    /// @param bountyPayload the payload for the bounty
    /// @return The number of active (un-rejected) submissions for the bounty
    function activeSubmissionsCount(bytes32 bountyPayload) internal view returns (uint32) {
        return submissionsCount(bountyPayload) - bountyQueueIndex(bountyPayload);
    }

    /// @param bountyPayload The payload for the bounty
    /// @return Whether the bounty is closed or not
    /// @dev 'closed' is only true once a submission has been approved OR the bounty has been
    /// closed by the funder, which can only happen after the unlock time when there is no active
    /// submission.
    function closed(bytes32 bountyPayload) public view virtual returns (bool) {
        return _bounties[bountyPayload].closed;
    }

    /// @param submissionPayload The payload to check
    /// @return Whether the payload has been rejected or not
    function rejectedSubmissionPayload(bytes32 submissionPayload) public view virtual returns (bool) {
        return _rejectedPayloads[submissionPayload];
    }

    /// @param submission The ID of the submission
    /// @param bountyPayload The payload for the bounty the submission corresponds to
    /// @return The position of the submission in the queue
    /// @dev Used to tell our off-chain record about a submission's position.
    function submissionQueuePosition(uint256 submission, bytes32 bountyPayload) public view virtual returns (uint32) {
        uint32 _numSubmissions = submissionsCount(bountyPayload);
        for (uint32 i = 0; i < _numSubmissions; i++) {
            if (_submissions[bountyPayload][i].submissionPayload == keccak256(abi.encodePacked(submission, _submissions[bountyPayload][i].submitter))) {
                return i;
            }
        }

        revert("BountyV4: submission not found");
    }

    /// @param position The position in the queue
    /// @param bountyPayload The payload for the bounty the submission corresponds to
    /// @return The submitter of the submission at the position in the queue
    function submitterAtPosition(uint32 position, bytes32 bountyPayload) public view virtual returns (address) {
        return _submissions[bountyPayload][position].submitter;
    }

    /// @param position The position in the queue
    /// @param bountyPayload The payload for the bounty the submission corresponds to
    /// @return The stake of the submission at the position in the queue
    function stakeAtPosition(uint32 position, bytes32 bountyPayload) public view virtual returns (uint256) {
        return _submissions[bountyPayload][position].stake;
    }

    /// @param account The account to check
    /// @return Whether the account is an approver or not
    function approver(address account) public view virtual returns (bool) {
        return _approvers[account];
    }

    /// @param account The account to check
    /// @return Whether the account is an arbiter or not
    function arbiter(address account) public view virtual returns (bool) {
        return _arbiters[account];
    }

    /// @return Whether the contract is accepting bounties or not
    function acceptingBounties() public view virtual returns (bool) {
        return _acceptingBounties;
    }

    /// @param value The value to calculate the fee from
    /// @param maker Whether the fee is for creating a bounty or not
    /// @return The calculated fee
    /// @dev The 10000 accounts for denomination in basis points.
    function fee(uint256 value, bool maker) public view virtual returns (uint256) {
        return value * (maker ? _makerFee : _takerFee) / 10000;
    }

    /// @param bountyID The ID of the bounty to check
    /// @return The payload of the bounty, if it is found
    /// @dev This is used to check if a bountyID is funded. Check each existing bounty record, and
    /// see if we can hash the bountyID and one of the funders addresses to produce the payload.
    function findBountyByID(uint256 bountyID) public view virtual returns (bytes32) {
        bytes32 _payload;
        for (uint32 i = 0; i < _bountyPayloads.length; i++) {
            for (uint32 j = 0; j < _bountyFunders[_bountyPayloads[i]].length; j++) {
                _payload = keccak256(abi.encodePacked(bountyID, _bountyFunders[_bountyPayloads[i]][j]));
                if (_payload == _bountyPayloads[i]) {
                    return _payload;
                }
            }
        }

        revert("BountyV4: bountyID not found");
    }

    /// @param bountyPayload The payload of the bounty to check
    /// @param submissionID The ID of the submission to check
    /// @return The payload of the submission, if it is found
    /// @dev This is used to get the submission payload, which we use to check if a submission has
    /// been rejected.
    function findSubmissionPayload(bytes32 bountyPayload, uint256 submissionID) public view virtual returns (bytes32) {
        bytes32 _payload;
        for (uint32 i = 0; i < _submissions[bountyPayload].length; i++) {
            _payload = keccak256(abi.encodePacked(submissionID, _submissions[bountyPayload][i].submitter));
            if (_payload == _submissions[bountyPayload][i].submissionPayload) {
                return _payload;
            }
        }

        return 0;
    }

    /// @notice Gets the state of a bounty
    /// @param bountyPayload The payload of the bounty to get the state of
    /// @return The state of the bounty
    /// @dev This is an aggregate of existing getters. It's here to save us the trouble of calling
    /// them all each time.
    function bountyState(bytes32 bountyPayload) public view virtual returns (BountyState memory) {
        return BountyState({
            expiration: expiration(bountyPayload),
            amount: amount(bountyPayload),
            closed: closed(bountyPayload),
            queueIndex: bountyQueueIndex(bountyPayload),
            submissionCount: submissionsCount(bountyPayload),
            funders: funders(bountyPayload),
            initialAmount: initialAmount(bountyPayload)
        });
    }

    /// @notice Modifier to require that the caller is an approver
    modifier onlyApprover() {
        require(approver(_msgSender()), "BountyV4: caller is not approver");
        _;
    }

    /// @notice Modifier to require that the caller is an arbiter
    modifier onlyArbiter() {
        require(arbiter(_msgSender()), "BountyV4: caller is not arbiter");
        _;
    }

    /// @notice Funds a bounty
    /// @param bountyPayload The payload of the bounty to fund
    /// @param _amount The amount of the ERC20 to fund the bounty with
    /// @dev Additional bounty information like name and description is stored on an off-chain
    /// platform, e.g. Arkham. In the current implementation, Bounty records are generated
    /// on the Arkham platform and the bounty payload is the hash of the bounty ID and the poster
    /// address. This prevents front-running of bounty creation. The first funder who has an
    /// address that can hash with a bountyID to produce the payload is considered the only funder.
    function fundBounty(bytes32 bountyPayload, uint256 _amount, uint32 duration) external {
        require(_acceptingBounties, "BountyV4: contract no longer accepting bounties");
        require(_amount >= _minBounty, "BountyV4: below minimum bounty");
        require(!_bounties[bountyPayload].closed, "BountyV4: bounty already closed");
        require(_bountyFunders[bountyPayload].length < 11, "BountyV4: bounty already has 10 funders");

        uint32 _intendedDuration = duration == 0 ? _defaultDuration : duration;
        require(_intendedDuration >= _minDuration, "BountyV4: duration below minimum");
        require(_intendedDuration <= _maxDuration, "BountyV4: duration above maximum");

        uint256 _fee = fee(_amount, true);

        _accruedFees += _fee;

        uint64 _expiration = uint64(block.timestamp + _intendedDuration * 1 days);

        // If there is already a payload for this bounty, and the bounty is not closed, add the
        // amount. Otherwise create the bounty.
        if (initialAmount(bountyPayload) > 0) {
            _bounties[bountyPayload].amount += _amount;
            _bounties[bountyPayload].initialAmount += _amount;
            _bountyFunders[bountyPayload].push(_msgSender());
        } else {
            _bounties[bountyPayload] = Bounty({
                amount: _amount,
                initialAmount: _amount,
                expiration: _expiration,
                closed: false,
                queueIndex: 0
            });

            _bountyPayloads.push(bountyPayload);
            _bountyFunders[bountyPayload].push(_msgSender());
        }

        SafeERC20.safeTransferFrom(IERC20(_arkm), _msgSender(), address(this), _amount + _fee);

        emit FundBounty(
            bountyPayload,
            _msgSender(),
            _amount,
            _expiration
        );
    }

    /// @notice Closes a bounty and returns the funds to the funder
    /// @param bountyPayload The payload for the bounty to close
    /// @dev The bounty may be closed by anyone after the unlock time when there is no active
    /// submission. Approvers can close bounties before expiration. This function will only work if
    /// one of the funders hashes with the bountyID to produce the payload. It is possible that
    /// there is no valid funder, in which case the funds are locked in the contract, effectively
    /// burning them.
    function closeBounty(bytes32 bountyPayload, uint256 bountyID) external {
        require(amount(bountyPayload) > 0, "BountyV4: bounty not funded");
        require(expiration(bountyPayload) <= block.timestamp || approver(_msgSender()), "BountyV4: only approvers can close before expiration");
        require(activeSubmissionsCount(bountyPayload) == 0, "BountyV4: has active submission");
        require(!_bounties[bountyPayload].closed, "BountyV4: bounty already closed");

        /// Find the funder that hashes with the bountyID to produce the payload.
        /// @dev Note that it is important that this list be FIFO, to prevent later funders from
        /// getting the payout instead of the first legitimate funder.
        address _legitimateFunder;
        for (uint32 i = 0; i < _bountyFunders[bountyPayload].length; i++) {
            if (keccak256(abi.encodePacked(bountyID, _bountyFunders[bountyPayload][i])) == bountyPayload) {
                _legitimateFunder = _bountyFunders[bountyPayload][i];
                break;
            }
        }

        require(_legitimateFunder != address(0), "BountyV4: failed to hash bountyID with funder address");

        /// @dev Staked funds from rejected submissions have added to the amount. Accrue as fees.
        uint256 excessFromStaking = _bounties[bountyPayload].amount - _bounties[bountyPayload].initialAmount;
        _accruedFees += excessFromStaking;

        // There are no active submissions, so there are no more stakes to be returned.

        SafeERC20.safeTransfer(IERC20(_arkm), _legitimateFunder, _bounties[bountyPayload].initialAmount);
        _bounties[bountyPayload].closed = true;

        emit CloseBounty(
            bountyPayload,
            _legitimateFunder,
            _msgSender(),
            _bounties[bountyPayload].initialAmount,
            excessFromStaking,
            submissionsCount(bountyPayload)
        );
    }

    /// @notice Makes a submission for a bounty by staking the ERC20
    /// @param bountyPayload The payload for the bounty to make a submission to
    /// @param submissionPayload The payload of the submission, used to validate the sender's
    /// address
    /// @dev The payload is a hash of the submission ID and the submitter's address to prevent
    /// front-running. All submissions must provide a stake, which is returned if the submission
    /// is approved (less fees) or forfeit (and added to the bounty) if the submission is rejected.
    /// This is to prevent spamming of submissions.
    function makeSubmission(bytes32 bountyPayload, bytes32 submissionPayload) external {
        require(!_bounties[bountyPayload].closed, "BountyV4: bounty closed");
        require(amount(bountyPayload) > 0, "BountyV4: bounty not funded");
        require(expiration(bountyPayload) > block.timestamp, "BountyV4: bounty expired");
        require(!rejectedSubmissionPayload(submissionPayload), "BountyV4: payload rejected");
        require(!approver(_msgSender()), "BountyV4: approvers cannot submit");
        require(activeSubmissionsCount(bountyPayload) < _maxActiveSubmissions, "BountyV4: max active submissions reached");

        _submissions[bountyPayload].push(Submission({
            submissionPayload: submissionPayload,
            submitter: _msgSender(),
            stake: _submissionStake
        }));

        SafeERC20.safeTransferFrom(IERC20(_arkm), _msgSender(), address(this), _submissionStake);

        emit FundSubmission(
            bountyPayload,
            _msgSender(),
            _submissionStake,
            submissionPayload,
            bountyQueueIndex(bountyPayload),
            uint32(_submissions[bountyPayload].length - 1)
        );
    }

    /// @notice Approves and pays out for a submission to a bounty
    /// @param bountyPayload The payload for the bounty whose submission to approve
    /// @param index The index of the submission to approve
    /// @dev The bounty is paid out to the address that submitted the approved submission, plus
    /// their initial stake, less fees. The bounty is closed and will no longer receive
    /// submissions. Off-chain, the information corresponding to the submission ID is delivered to
    /// the funder.
    function approveSubmissionAt(bytes32 bountyPayload, uint32 index) internal {
        // Approving a submission on a closed bounty is not allowed but handled by check that makes
        // it impossible to submit to a closed bounty and the check that makes it impossible to close
        // a bounty that has an active submission.

        // Mark as approved.
        _acceptedPayloads[bountyPayload] = _submissions[bountyPayload][index].submissionPayload;

        /// @dev If there have been other rejected submissions, their stake will be reflected in
        /// the bounty amount.
        uint256 _amount = _bounties[bountyPayload].amount;
        _bounties[bountyPayload].amount = 0;

        uint256 _stake = _submissions[bountyPayload][index].stake;
        _accruedFees += fee(_amount, false);
        _submissions[bountyPayload][index].stake = 0;

        uint256 payout = _amount + _stake - fee(_amount, false);
        SafeERC20.safeTransfer(IERC20(_arkm), _submissions[bountyPayload][index].submitter, payout);

        _bounties[bountyPayload].queueIndex = index + 1;
        _bounties[bountyPayload].closed = true;

        emit SubmissionApproved(
            bountyPayload,
            _submissions[bountyPayload][index].submitter,
            _stake,
            _submissions[bountyPayload][index].submissionPayload,
            payout,
            index,
            submissionsCount(bountyPayload)
        );
    }

    /// @notice Approves the Nth submission for a bounty (0-indexed)
    /// @param bountyPayload The payload for the bounty whose submission to approve
    /// @param submissionID The ID of the submission to approve
    /// @dev Un-assessed submissions get their stakes returned.
    function approveSubmission(bytes32 bountyPayload, uint256 submissionID) external onlyArbiter {

        // If the bounty is closed, this function should revert. But, this is a good check to have
        // as a hedge against future changes.
        require(!_bounties[bountyPayload].closed, "BountyV4: bounty closed");

        // Failure value is out of range.
        uint32 _approveAtIndex = submissionsCount(bountyPayload);

        // Find the index of the submission.
        for (uint32 i = _bounties[bountyPayload].queueIndex; i < submissionsCount(bountyPayload); i++) {
            if (_submissions[bountyPayload][i].submissionPayload == keccak256(abi.encodePacked(submissionID, _submissions[bountyPayload][i].submitter))) {
                _approveAtIndex = i;
                break;
            }
        }

        require(_approveAtIndex < submissionsCount(bountyPayload), "BountyV4: submission not found");

        // Reject all submissions before the approved one.
        for (uint32 i = _bounties[bountyPayload].queueIndex; i < _approveAtIndex; i++) {
            // This increments the queue index.
            rejectSubmission(bountyPayload);
        }

        // Appprove the submission. This increments the index.
        approveSubmissionAt(bountyPayload, _approveAtIndex);

        // Refund remaining stakes.
        for (uint32 i = _approveAtIndex + 1; i < submissionsCount(bountyPayload); i++) {
            SafeERC20.safeTransfer(IERC20(_arkm), _submissions[bountyPayload][i].submitter, _submissions[bountyPayload][i].stake);
            _submissions[bountyPayload][i].stake = 0;
            _bounties[bountyPayload].queueIndex += 1;
        }
    }

    /// @notice Rejects N submissions for a bounty
    /// @param bountyPayload The payload for the bounty whose submissions to reject
    /// @param n The number of submissions to reject
    function rejectSubmissions(bytes32 bountyPayload, uint32 n) public onlyApprover {
        require(activeSubmissionsCount(bountyPayload) >= n, "BountyV4: not enough active submissions");

        for (uint32 i = 0; i < n; i++) {
            rejectSubmission(bountyPayload);
        }
    }

    /// @notice Rejects a submission for a bounty
    /// @param bountyPayload The payload for the bounty whose submission to reject
    /// @dev The stake of the rejected submission is added to the bounty amount. The queueIndex for
    /// the bounty is incremented to move to the next submission in the queue.
    function rejectSubmission(bytes32 bountyPayload) internal {
        // If there are active submissions, we can reject one.

        uint32 _currentPosition = bountyQueueIndex(bountyPayload);

        uint256 _stake = _submissions[bountyPayload][_currentPosition].stake;
        _submissions[bountyPayload][_currentPosition].stake = 0;

        uint256 _stakeFee = fee(_stake, true);
        _accruedFees += _stakeFee;
        _bounties[bountyPayload].amount += _stake - _stakeFee;
        _bounties[bountyPayload].queueIndex += 1;

        bytes32 _payload = _submissions[bountyPayload][_currentPosition].submissionPayload;
        _rejectedPayloads[_payload] = true;

        emit RejectSubmission(
            bountyPayload,
            _submissions[bountyPayload][_currentPosition].submitter,
            _stake,
            _payload,
            bountyQueueIndex(bountyPayload),
            submissionsCount(bountyPayload)
        );
    }

    /// @notice Grants approver status to an account
    /// @param account The account to grant approver status to
    /// @dev There can be any number of approvers. Only one approver is required to approve a
    /// submission.
    function grantApprover(address account) external onlyOwner {
        require(!approver(account), "BountyV4: already approver");
        _approvers[account] = true;

        emit GrantApprover(
            account
        );
    }

    /// @notice Revokes approver status from an account
    /// @param account The account to revoke approver status from
    function revokeApprover(address account) external onlyOwner {
        require(approver(account), "BountyV4: not approver");
        _approvers[account] = false;

        emit RevokeApprover(
            account
        );
    }

    /// @notice Grants arbiter status to an account
    /// @param account The account to grant arbiter status to
    /// @dev There can be any number of arbiters. Only one arbiter is required to approve a
    /// submission.
    function grantArbiter(address account) external onlyOwner {
        require(!arbiter(account), "BountyV4: already arbiter");
        _arbiters[account] = true;

        emit GrantArbiter(
            account
        );
    }

    /// @notice Revokes arbiter status from an account
    /// @param account The account to revoke arbiter status from
    function revokeArbiter(address account) external onlyOwner {
        require(arbiter(account), "BountyV4: not arbiter");
        _arbiters[account] = false;

        emit RevokeArbiter(
            account
        );
    }


    /// @notice Sets a new maker fee
    /// @param newFee The new maker fee, in basis points
    function setMakerFee(uint256 newFee) external onlyOwner {
        require(newFee <= _MAX_BPS, "BountyV4: maker fee must be <= 100%");
        uint256 _oldFee = _makerFee;
        _makerFee = newFee;

        emit SetMakerFee(
            newFee,
            _oldFee
        );
    }

    /// @notice Sets a new taker fee
    /// @param newFee The new taker fee, in basis points
    function setTakerFee(uint256 newFee) external onlyOwner {
        require(newFee <= _MAX_BPS, "BountyV4: taker fee must be <= 100%");
        uint256 _oldFee = _takerFee;
        _takerFee = newFee;

        emit SetTakerFee(
            newFee,
            _oldFee
        );
    }

    /// @notice Sets a new submission stake
    /// @param newStake The new submission stake, in value of the ERC20 token e.g. 10 e18
    function setSubmissionStake(uint256 newStake) external onlyOwner {
        uint256 _oldStake = _submissionStake;
        _submissionStake = newStake;

        emit SetSubmissionStake(
            newStake,
            _oldStake
        );
    }

    /// @notice Sets a new minimum bounty
    /// @param newMinBounty The new minimum bounty
    function setMinBounty(uint256 newMinBounty) external onlyOwner {
        _minBounty = newMinBounty;
    }

    /// @notice Sets a new max active submissions
    /// @param newMaxActiveSubmissions The new max active submissions
    function setMaxActiveSubmissions(uint32 newMaxActiveSubmissions) external onlyOwner {
        _maxActiveSubmissions = newMaxActiveSubmissions;
    }

    /// @notice Sets a new default duration
    /// @param newDefaultDuration The new default duration
    function setDefaultDuration(uint32 newDefaultDuration) external onlyOwner {
        require(newDefaultDuration <= _maxDuration, "BountyV4: default duration must be <= max duration");
        require(newDefaultDuration >= _minDuration, "BountyV4: default duration must be >= min duration");
        _defaultDuration = newDefaultDuration;
    }

    /// @notice Sets a new min duration
    /// @param newMinDuration The new min duration
    /// @dev Requiring that the new min duration be <= the default duration ensures that it is also
    /// <= the max duration.
    function setMinDuration(uint32 newMinDuration) external onlyOwner {
        require(newMinDuration >= _minDurationLimit, "BountyV4: min duration must be >= 1 day");
        require(newMinDuration <= _defaultDuration, "BountyV4: min duration must be <= default duration");
        _minDuration = newMinDuration;
    }

    /// @notice Sets a new max duration
    /// @param newMaxDuration The new max duration
    /// @dev Requiring that the new max duration be >= the default duration ensures that it is also
    /// >= the min duration.
    function setMaxDuration(uint32 newMaxDuration) external onlyOwner {
        require(newMaxDuration <= _maxDurationLimit, "BountyV4: max duration must be <= 365 days");
        require(newMaxDuration >= _defaultDuration, "BountyV4: max duration must be >= default duration");
        _maxDuration = newMaxDuration;
    }

    /// @notice Sets a new fee receiver address
    /// @param feeReceiverAddress The new fee receiver address
    /// @dev This is the address that will receive fees.
    function setFeeReceiverAddress(address feeReceiverAddress) external onlyOwner {
        require(feeReceiverAddress != address(0), "BountyV4: fee receiver address cannot be 0x0");
        _feeReceiverAddress = feeReceiverAddress;
    }

    /// @notice Prevent any further bounties from being created
    /// @dev This is how we will sunset the contract when we want to move to a new version. It's
    /// important that we continue to allow submissions to existing bounties until they all expire.
    /// Once all bounties have expired and we have approved or rejected all submissions, we can
    /// disburse any remaining fees.
    function stopAcceptingBounties() external onlyOwner {
        _acceptingBounties = false;

        emit CloseBountySubmissions();
    }

    /// @notice Withdraw accrued fees
    /// @dev This can be called periodically by the owner.
    function withdrawFees() external onlyOwner {
        uint256 fees = _accruedFees;
        _accruedFees = 0;
        SafeERC20.safeTransfer(IERC20(_arkm), _feeReceiverAddress, fees);

        emit WithdrawFees(
            fees
        );
    }
}

